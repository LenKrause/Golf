0100 REM "GW011 - 04/18/17 Golfer Menu
0110 SETESC 8000; SETERR 8000
0120 LET TAB$="",ROW$=""; REM "Can't clear HTML$ because some apps return here
0130 IF POS(WASES.ADMIN$="WLF") THEN LET Y.VDTSFX$=WASES.ADMIN$; CALL "CDW100",Y$,"SYS",HTML$; LET HTML$=HTML$+"<br>"
0140 IF POS(" "<>WASES.CID$) THEN GOSUB PLAYER
0150 IF LEN(MSG$) THEN LET HTML$="<p class='msg'>"+MSG$+"</p>"+HTML$
0160 IF LEN(HTML$)=0 THEN CALL "CDW279",WASES$,HTML$
0170 RUN "CDW999"

1000 PLAYER:
1010 IF GR04=0 THEN CALL "CDS095",GR04,"XXGR04","YUY",GR04$
1020 READ RECORD(GR04,KNUM=0,KEY=WASES.LEAGUE$+WASES.CID$,ERR=1440)GR04$
1030 IF LEN(FIELD(CGI$,"FIRSTNAME",ERR=1040))>=0 THEN GOSUB STORE
1040 GOSUB INFOFORM
1050 GOSUB LASTNEXT
1054 CALL "GW054",Y$,WASES$,POINTSDATE,FLTS$,HTML$[ALL]
1055 LET Z=POS(GR04.FLT$=FLTS$)
1056 IF Z THEN LET HTML$=HTML$+HTML$[Z]+"<br>"
1060 CALL "GW063",Y$,GR04$,HTML$; REM "Charts
1065 CALL "CDW038",Y$,"Best 10 Scores",Y5A$,HTMLBEST$,W038$,"XXGR05",0,"","","GR05.DATE|SCORE|HC|NET|PUTTS","GR05.LEAGUE$="""+WASES.LEAGUE$+""" AND GR05.ID$="""+WASES.CID$+""" and GR05.NET","REC.SCORE$+NOT(REC.DATE$)","",10
1066 LET HTML$=HTML$+"<br>"+HTMLBEST$
1070 LET HTML$="<center>"+HTML$+"</center>"
1080 RETURN

1100 STORE:
1110 IF POS(" "<>CGI.FIRSTNAME$) AND FND("FIRSTNAME") THEN LET GR04.FIRSTNAME$=CGI.FIRSTNAME$; LET MSG$="First Name changed to "+GR04.FIRSTNAME$; GOSUB ACTIVITY
1120 IF POS(" "<>CGI.LASTNAME$) AND FND("LASTNAME") THEN LET GR04.LASTNAME$=CGI.LASTNAME$,MSG$="Last Name changed to "+GR04.LASTNAME$; GOSUB ACTIVITY
1130 IF POS(" "<>CGI.EMAIL$) THEN GOSUB EMAIL
1140 IF POS(" "<>CGI.PHONE$) THEN GOSUB PHONE
1150 IF POS(" "<>CGI.PASSWORD$) AND FND("PASSWORD") THEN LET GR04.PASSWORD$=CGI.PASSWORD$,MSG$="Password Changed"; GOSUB ACTIVITY
1160 IF POS(FIELD(CGI$,"AUTOSCHEDULE",ERR=1161)="YN") AND FND("AUTOSCHEDULE") THEN LET GR04.AUTOSCHEDULE$=CGI.AUTOSCHEDULE$,MSG$="Auto Schedule set to "+ GR04.AUTOSCHEDULE$; GOSUB ACTIVITY
1162 IF POS(FIELD(CGI$,"TIMEPREFERENCE",ERR=1163)="ELN") AND FND("TIMEPREFERENCE") THEN LET GR04.TIMEPREFERENCE$=CGI.TIMEPREFERENCE$,MSG$="Time Preference set to "+GR04.TIMEPREFERENCE$; GOSUB ACTIVITY
1164 IF POS(FIELD(CGI$,"PLAYTHISYEAR",ERR=1170)="YN") AND FND("PLAYTHISYEAR") THEN LET GR04.PLAYTHISYEAR$=CGI.PLAYTHISYEAR$,MSG$="Play this year set to "+FNYN$(GR04.PLAYTHISYEAR$); GOSUB ACTIVITY
1170 WRITE RECORD(GR04)GR04$
1180 LET MSG$=ALLMSG$,ALLMSG$=""
1190 RETURN

1200 INFOFORM:
1210 LET HTML$=HTML$+"<form>"
1220 GOSUB TOPROW
1230 CALL "CDW000",ROW$,"dcs3","Golfer Information"
1240 CALL "CDW000",TAB$,"r class='title'",ROW$
1250 CALL "CDW000",ROW$,"d class='head'",""
1260 CALL "CDW000",ROW$,"dc class='head'","Current"
1270 CALL "CDW000",ROW$,"dc class='head'","Revised"
1280 CALL "CDW000",TAB$,"r class='head'",ROW$
1290 LET TXT$="First Name",FLD$="FIRSTNAME"; GOSUB ROW
1300 LET TXT$="Last Name",FLD$="LASTNAME"; GOSUB ROW
1310 LET TXT$="Email",FLD$="EMAIL"; GOSUB ROW
1320 LET TXT$="Telephone",FLD$="PHONE"; GOSUB ROW
1330 LET TXT$="Password",FLD$="PASSWORD"; GOSUB ROW

1331 CALL "CDW000",ROW$,"dr class='head'","Auto Schedule"
1332 IF GR04.AUTOSCHEDULE$="Y" THEN LET VAL$="Yes",YES$=" Checked",NO$="" ELSE LET VAL$="No",YES$="",NO$=" Checked"
1333 LET FLD$="AUTOSCHEDULE"
1334 CALL "CDW000",ROW$,"d",VAL$
1335 CALL "CDW000",ROW$,"d","<input type='radio' name='"+FLD$+"' value='Y'"+YES$+">Yes<br><input type='radio' name='"+FLD$+"' value='N'"+NO$+">No"
1336 CALL "CDW000",TAB$,"r",ROW$

1337 CALL "CDW000",ROW$,"dr class='head'","Time Preference"
1338 def fno$(val$, text$)="<input type='radio' name='" + fld$ + "' value='" + val$ + "'" + iff(field(gr04$,fld$)=val$, " Checked", "") + ">" + text$
1339 FLD$="TIMEPREFERENCE"
1340 call "CDW000",row$,"d",iff(gr04.timepreference$ = "E", "Early", iff(gr04.timepreference$ = "L", "Late", "No Preference"))
1341 options$ = fno$("E", "Early")
1343 options$ = options$ + "<br>" + fno$("L", "Late")
1344 options$ = options$ + "<br>" + fno$("N", "No Preference")
1345 CALL "CDW000",ROW$,"d",options$
1346 rem CALL "CDW000",ROW$,"d","<input type='radio' name='"+FLD$+"' value='E'"+YES$+">Early<br><input type='radio' name='"+FLD$+"' value='L'"+NO$+">Late"
1347 CALL "CDW000",TAB$,"r",ROW$

1356 CALL "CDW000",ROW$,"dr class='head'","Play in "+DATE(0:"%Yl")
1358 IF GR04.PLAYTHISYEAR$="Y" THEN LET VAL$="Yes",YES$=" Checked",NO$="" ELSE LET VAL$="No",YES$="",NO$=" Checked"
1360 LET FLD$="PLAYTHISYEAR"
1370 CALL "CDW000",ROW$,"d",VAL$
1380 CALL "CDW000",ROW$,"d","<input type='radio' name='"+FLD$+"' value='Y'"+YES$+">Yes<br><input type='radio' name='"+FLD$+"' value='N'"+NO$+">No"
1390 CALL "CDW000",TAB$,"r",ROW$

1400 CALL "CDW000",ROW$,"dcs3","<input type='submit' value='Save Changes' onclick='window.location=""[DSN]?p=GW011""'><input type='button' value='Cancel & Logout' onclick='window.location=""[DSN]?p=GW999""'>"
1410 CALL "CDW000",TAB$,"r class='foot'",ROW$
1420 CALL "CDW000",HTML$,"tc",TAB$
1430 LET HTML$=HTML$+"</form>"
1440 RETURN

1500 LASTNEXT:
1510 IF GR05=0 THEN CALL "CDS095",GR05,"XXGR05","YUY",GR05$
1520 READ (GR05,KNUM=0,KEY=WASES.LEAGUE$+WASES.CID$+Y.JDATE$,DOM=1530)
1530 READ RECORD(GR05,END=1570)GR05$
1540 IF GR05.LEAGUE$<>WASES.LEAGUE$ OR GR05.ID$<>WASES.CID$ THEN GOTO 1570
1550 IF GR05.SCORE=0 THEN GOTO 1530
1560 GOSUB LASTROUND
1570 READ RECORD(GR05,KNUM=0,KEY=WASES.LEAGUE$+WASES.CID$+BIN(Y.JDATE+6,3),DOM=1580)GR05$; GOSUB PLAYNEXT; GOTO 1600
1580 READ RECORD(GR05,END=1600)GR05$
1590 IF GR05.LEAGUE$=WASES.LEAGUE$ AND GR05.ID$=WASES.CID$ AND GR05.DATE>=Y.JDATE THEN GOSUB PLAYNEXT
1600 IF LASTROUND OR PLAYNEXT THEN CALL "CDW000",HTML$,"Tc",TAB$; LET HTML$=HTML$+"<br>"
1610 RETURN

1700 ROW:
1710 CALL "CDW000",ROW$,"dr class='head'",TXT$
1720 CALL "CDW000",ROW$,"d",FIELD(GR04$,FLD$)
1730 LET LGTH=LEN(FIELD(GR04$,FLD$))
1740 IF FLD$="EMAIL" AND 0 THEN LET VAL$=" value='"+BADEMAIL$+"'" ELSE LET VAL$=""
1750 CALL "CDW000",ROW$,"d","<input type='text' size="+STR(LGTH+2)+" maxlength="+STR(LGTH)+" name='"+FLD$+"'"+VAL$+">"
1760 CALL "CDW000",TAB$,"r",ROW$
1770 RETURN

1800 EMAIL:
1810 LET BADEMAIL$=CGI.EMAIL$,EMAIL$=CVS(CGI.EMAIL$,8)
1820 CALL "CDS275::VALIDATE",EMAIL$,MSG$
1830 IF LEN(MSG$)=0 THEN LET BADEMAIL$="",CGI.EMAIL$=EMAIL$; IF FND("EMAIL") THEN LET GR04.EMAIL$=EMAIL$,MSG$="Email Address Changed to "+EMAIL$
1840 IF LEN(MSG$) THEN GOSUB ACTIVITY
1850 RETURN

1900 PHONE:
1910 LET PHONE$=""
1920 FOR I=1 TO LEN(CGI.PHONE$)
1930 IF CGI.PHONE$(I,1)>="0" AND CGI.PHONE$(I,1)<="9" THEN LET PHONE$=PHONE$+CGI.PHONE$(I,1)
1940 NEXT I
1950 IF LEN(PHONE$)=11 AND PHONE$(1,1)="1" THEN LET PHONE$=PHONE$(2)
1960 IF LEN(PHONE$)<>10 THEN LET MSG$="Phone Number must contain 10 digits" ELSE LET CGI.PHONE$=PHONE$(1,3)+"-"+PHONE$(4,3)+"-"+PHONE$(7); IF FND("PHONE") THEN LET GR04.PHONE$=CGI.PHONE$,MSG$="Phone# changed to "+GR04.PHONE$
1970 IF LEN(MSG$) THEN GOSUB ACTIVITY
1980 RETURN

2000 LASTROUND:
2005 LET POINTSDATE=GR05.DATE+7
2010 GOSUB TOPROW
2020 CALL "CDW000",ROW$,"dcs3","Last Round"
2030 CALL "CDW000",TAB$,"r class='title'",ROW$
2040 CALL "CDW000",ROW$,"dr",DATE(GR05.DATE:"%Ds %Mz/%Dz/%Yz")
2050 CALL "CDW000",ROW$,"dc",CVS(GR04.FIRSTNAME$,2)+" "+CVS(GR04.LASTNAME$,2)
2055 LET OPP=POS(" "<>GR05.OPP$)
2060 IF OPP THEN CALL "SW001","XXGR04",0,GR05.LEAGUE$+GR05.OPP$,GR04A$,SW001$
2070 IF OPP THEN CALL "CDW000",ROW$,"dc",CVS(GR04A.FIRSTNAME$,2)+" "+CVS(GR04A.LASTNAME$,2)
2080 CALL "CDW000",TAB$,"r",ROW$
2090 CALL "SW001","XXGR05",0,GR05.LEAGUE$+GR05.OPP$+GR05.DATE$,GR05A$,SW001$
2100 CALL "CDW000",ROW$,"dr","Score"
2110 CALL "CDW000",ROW$,"dc",STR(GR05.SCORE)
2120 IF OPP THEN CALL "CDW000",ROW$,"dc",STR(GR05A.SCORE)
2130 CALL "CDW000",TAB$,"r",ROW$
2140 CALL "CDW000",ROW$,"dr","Handicap"
2150 CALL "CDW000",ROW$,"dc",STR(GR05.HC)
2160 IF OPP THEN CALL "CDW000",ROW$,"dc",STR(GR05A.HC)
2170 CALL "CDW000",TAB$,"r",ROW$
2180 CALL "CDW000",ROW$,"dr","Net"
2190 CALL "CDW000",ROW$,"dc",STR(GR05.NET)
2200 IF OPP THEN CALL "CDW000",ROW$,"dc",STR(GR05A.NET)
2210 CALL "CDW000",TAB$,"r",ROW$
2220 CALL "CDW000",ROW$,"dr","Putts"
2230 CALL "CDW000",ROW$,"dc",STR(GR05.PUTTS)
2240 IF OPP THEN CALL "CDW000",ROW$,"dc",STR(GR05A.PUTTS)
2250 CALL "CDW000",TAB$,"r",ROW$
2260 IF OPP THEN CALL "CDW000",ROW$,"dr","Points"
2270 IF OPP THEN CALL "CDW000",ROW$,"dc",STR(GR05.POINTS)
2280 IF OPP THEN CALL "CDW000",ROW$,"dc",STR(GR05A.POINTS)
2290 IF OPP THEN CALL "CDW000",TAB$,"r",ROW$
2300 LET LASTROUND=1
2310 RETURN

2400 TOPROW:
2410 CALL "CDW000",ROW$,"dcs3",WASES.LEAGUENAME$
2420 CALL "CDW000",TAB$,"r class='top'",ROW$
2430 RETURN

2500 PLAYNEXT:
2505 LET POINTSDATE=GR05.DATE
2506 IF POS(" "<>GR05.OPP$)=0 THEN RETURN
2510 IF !(LASTROUND) THEN GOSUB TOPROW
2520 CALL "CDW000",ROW$,"dcs3","Next Round"
2530 CALL "CDW000",TAB$,"r class='title'",ROW$
2540 CALL "CDW000",ROW$,"dr",DATE(GR05.DATE:"%Ds %Mz/%Dz/%Yz")
2550 CALL "CDW000",ROW$,"dc",CVS(GR04.FIRSTNAME$,2)+" "+CVS(GR04.LASTNAME$,2)
2560 CALL "SW001","XXGR04",0,GR05.LEAGUE$+GR05.OPP$,GR04A$,SW001$
2570 CALL "SW001","XXGR05",0,GR05.LEAGUE$+GR05.OPP$+GR05.DATE$,GR05A$,SW001$
2580 CALL "CDW000",ROW$,"dc",CVS(GR04A.FIRSTNAME$,2)+" "+CVS(GR04A.LASTNAME$,2)
2590 CALL "CDW000",TAB$,"r",ROW$
2600 CALL "CDW000",ROW$,"dr","Handicap"
2610 CALL "CDW000",ROW$,"dc",STR(GR05.HC)
2620 CALL "CDW000",ROW$,"dc",STR(GR05A.HC)
2630 CALL "CDW000",TAB$,"r",ROW$
2640 CALL "CDW000",ROW$,"dr","Email"
2650 CALL "CDW000",ROW$,"dc",CVS(GR04.EMAIL$,2)
2660 CALL "CDW000",ROW$,"dc","<a href='mailto:"+CVS(GR04A.EMAIL$,2)+"?subject=Golf'>"+CVS(GR04A.EMAIL$,2)+"</a>"
2670 CALL "CDW000",TAB$,"r",ROW$
2680 CALL "CDW000",ROW$,"dr","Phone"
2690 CALL "CDW000",ROW$,"dc",CVS(GR04.PHONE$,2)
2700 CALL "CDW000",ROW$,"dc",CVS(GR04A.PHONE$,2)
2710 CALL "CDW000",TAB$,"r",ROW$
2720 LET PLAYNEXT=1
2730 RETURN

2800 ACTIVITY:
2810 IF GR07=0 THEN CALL "CDS095",GR07,"XXGR07","YNY",GR07$
2820 LET GR07.LEAGUE$=GR04.LEAGUE$
2830 LET GR07.ID$=GR04.ID$
2840 LET GR07.DATE=JUL(0,0,0)
2850 LET GR07.SEQ=0
2860 LET GR07.MSG$=MSG$
2870 LET GR07.SEQ=GR07.SEQ+1
2880 WRITE RECORD(GR07,DOM=2870)GR07$
2890 IF LEN(ALLMSG$) THEN LET ALLMSG$=ALLMSG$+"<br>"
2900 LET ALLMSG$=ALLMSG$+MSG$
2910 RETURN

3000 FUNCTIONS:
3010 DEF FNYN$(VAL$)
3020 IF VAL$="Y" THEN LET OUT$="Yes" ELSE LET OUT$="No"
3030 RETURN OUT$
3040 FNEND

3050 DEF FND(FLDNAM$)=(CVS(FIELD(CGI$,FLDNAM$),2)<>CVS(FIELD(GR04$,FLDNAM$),2))

8000 REM "Call Error/Escape Routine
8010 CALL "CDS063",STR(TCB(5)),Y$,PGM(-2)
8020 ON Y.ERRSTS GOTO 8030,8040,8050,8060
8030 SETERR 0
8040 RETRY
8050 RETURN
8060 RETRY

9000 EOJ:
9010 IF TCB(13) THEN EXIT
9020 RUN "CDS001"

9100 LOGOUT:
9110 RUN "CDW999"

9996 REM EDITOR
9997 SETERR 9999; SETESC 9998; CALL "CDS140",O$,O1$; SETERR 9997; EXECUTE O$
9998 DIM O$(0); GOTO 9997
9999 END
