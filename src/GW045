0100 rem "GW045 - 05/30/17 Score Posting Sheets & Handicap Listing
0110 setesc 8000; seterr 8000
0120 call "SW010", "XXGR04", 0, "", "FLT|SEQ|ID", fsi$, "", "", "REC.LEAGUE$=""" + wases.league$ + """ AND REC.FLT$<>"" """
0125 dim ptoday$[1:4]; flts$ = ""
0130 call "SW001", "XXGR01", 0, wases.league$, gr01$
0131 call "SW001", "XXGR08", 0, gr01.id$, gr08$, sw001$
0140 call "CDS095", gr03, "XXGR03", "YPY", gr03$
0150 read record(gr03, knum=0, key=wases.league$ + bin(num(cgi.date$), 3))gr03$
0151 call "CDS041", "GW032", gw032$, "YY"
0152 call "GW032", y$, gr03$, gw032$
0153 lastwkofyr = gw032.lastweek
0155 half = gr03.half
0160 if gr03.nine$ = "F" then
:      nine$ = "Front Nine", hole0 = 1, par = gr08.parf
:    else
:      nine$ = "Back Nine", hole0 = 10, par = gr08.parb
:    fi
0170 call "CDS095", gr04, "XXGR04", "YPY", gr04$
0175 call "CDS133", "XXGR05", "", "NNYY";
:    rem "Archive scores file for possible rollback if prior scores are lost
0180 call "CDS095", gr05, "XXGR05", "YNY", gr05$, gr05k$
0182 gr05s = unt
0183 select (gr05s)gr05$ from y.compcode$ + "GR05" where gr05.league$ = wases.league$ and gr05.date = num(cgi.date$) and gr05.score = 0
0184 read record(gr05s, end=0189)gr05$
0185 remove (gr05, key=kgen(gr05$, gr05k$, 0))
0186 goto 0184
0189 close (gr05s)
0190 call "CDS041", "CDS084", s084$
0200 s084.mode$ = "E", s084.name$ = "APDF", s084.size = 2
0210 call "CDS084", y$, y5$, u0, s084$, y6, y6$
0215 hcfile$ = y6.fileout$(pos("/tmp/" = y6.fileout$))
0220 gosub hclist
0230 call "CDS094", y6$, "E"
0240 s084.mode$ = "E", s084.name$ = "APDF", s084.size = 2, s084.fileout$ = ""
0250 call "CDS084", y$, y5$, u0, s084$, y6, y6$
0260 flt$ = "", spfile$ = y6.fileout$(pos("/tmp/" = y6.fileout$))
0270 doc$ = "Score", maxcols = 10; gosub dodoc
0280 doc$ = "Skin", maxcols = 12; gosub dodoc
0290 call "CDS094", y6$, "E"
0300 html$ = "<html><head><title>" + cvs(gr01.name$, 2) + "</title></head><frameset cols='*,*'><frame src='" + hcfile$ + "'><frame src='" + spfile$ + "'></frameset></html>"
0310 tmp$ = stbl("!CLEAR", "$title")
0320 dim s084$(0)
0330 htmfil$ = ",htm"; call "CDS064", y$, tmp, 0, 0, 0, 0, "STR", htmfil$
0340 write (tmp)html$
0350 close (tmp)
0355 htmfil$ = htmfil$(pos("/tmp/" = htmfil$))
0360 html$ = "<script>window.open('" + htmfil$ + "');</script>"
0370 run "GW011"

1000 dodoc:
1005 flt$ = ""
1010 read (gr04, knum=1, key=wases.league$, dom=1020)
1020 read record(gr04, end=1170)gr04$
1025 if gr04.league$ <> wases.league$ then goto 1170
1030 if gr04.flt$ = " " then goto 1020
1040 hc$ = ""
1050 if doc$ = "Score" then
:      read record(gr05, knum=0, key=gr04.league$ + gr04.id$ + bin(num(cgi.date$), 3))gr05$;
:      if gr05.hc then
:        hc$ = str(gr05.hc)
:      else
:        hc$ = ""
:      fi
:    fi
1060 if len(flt$) and gr04.flt$ <> flt$ then gosub finishflt
1070 if gr04.flt$ <> flt$ then gosub newflt
1080 rowno = rowno + 1
1085 for colno = 1 to maxcols
1090   if colno = 1 then
:        call "CDS454", y6$, colno, rowno, "C", str(gr04.seq), 0, 0, 0, 14, 70;
:        continue
:      fi
1100   if colno = 2 then
:        call "CDS454", y6$, colno, rowno, "L", cvs(cvs(gr04.firstname$, 2) + " " + cvs(gr04.lastname$, 2), 1), 0, 0, 0, 14, 70;
:        continue
:      fi
1110   if colno = 3 and doc$ = "Score" then
:        call "CDS454", y6$, colno, rowno, "L", gr04.phone$, 0, 0, 0, 14, 70;
:        continue
:      fi
1112   if colno = 4 and doc$ = "Score" then
:        call "CDS454", y6$, colno, rowno, "L", cvs(gr04.email$, 2), 0, 0, 0, 14, 70;
:        continue
:      fi
1114   if colno = 5 and doc$ = "Score" then
:        if half then
:          call "CDS454", y6$, colno, rowno, "C", str(asc(playtoday$(gr04.seq))), 0, 0, 0, 14, 70
:        fi;
:        continue
:      fi
1120   if colno = 7 and doc$ = "Score" then
:        call "CDS454", y6$, colno, rowno, "C", hc$, 0, 0, 0, 14, 70;
:        continue
:      fi
1140   if doc$ <> "Score" or colno <> 10 or half then call "CDS454", y6$, colno, rowno
1150 next colno
1160 goto 1020
1170 gosub finishflt
1180 return

1200 finishflt:
1210 if doc$ = "Skin" then return
1220 if 0 then call "CDS254", y6$, "T1,,,,12,,4148", " " + $0d0a0d0a$
1221 if 1 then call "CDS254", y6$, "T1,+75,,,12,,4148"
1230 call "CDS257", y$, gr01.league$ + flt$, "", "XXGR02", "PN", 8, 0, 0, 0, y6$
1240 return

1300 newflt:
1310 call "CDS094", y6$
1315 if doc$ = "Skin" then
:      print (y6.ch)'bo', y6.depth11$, 'eo', ;
:      across = 8.5, row = 150, skip = 120
:    else
:      across = 11, row = 50, skip = 50
:    fi
1320 flt$ = gr04.flt$, center$ = "T" + str((across - .5) * 300 / 2) + "C,"
1321 right$ = "T" + str((across - .5) * 300) + "R,"
1330 call "CDS254", y6$, center$ + str(row) + ",3,1,14,,4148", gr01.name$
1340 if 0 then row = row + 75
1350 call "CDS254", y6$, "T0," + str(row) + ",3,1,14,,4148", doc$ + " Posting Sheet"
1360 if 0 then row = row + 75
1370 call "CDS254", y6$, right$ + str(row) + ",3,1,14,,4148", flt$ + " Flight - " + date(num(cgi.date$))
1380 row = row + skip
1390 call "SW004", "XXGR04", 1, wases.league$ + flt$, recs, 0, "", "NP"
1400 rowno = 1
1410 on doc$ = "Score" gosub nskin, nscore
1420 return

1500 nscore:
1510 call "CDS454::SETUP", 0, row, "74,560,408,872," + str(111 * sgn(half)) + ",165,165,165,165,165", "84," + fill(recs * 3, "84,"), 1
1520 call "CDS454", y6$, 1, 1, "C", "ID", 1, 1, 1, 14, 70
1530 call "CDS454", y6$, 2, 1, "L", "Name", 1, 1, 1, 14, 70
1532 call "CDS454", y6$, 3, 1, "L", "Phone", 1, 1, 1, 14, 70
1534 call "CDS454", y6$, 4, 1, "L", "Email", 1, 1, 1, 14, 70
1535 if half then call "CDS454", y6$, 5, 1, "L", "Play", 1, 1, 1, 14, 70
1540 call "CDS454", y6$, 6, 1, "C", "Gross", 1, 1, 1, 14, 70
1550 call "CDS454", y6$, 7, 1, "C", "HC", 1, 1, 1, 14, 70
1560 call "CDS454", y6$, 8, 1, "C", "Net", 1, 1, 1, 14, 70
1570 call "CDS454", y6$, 9, 1, "C", "Putts", 1, 1, 1, 14, 70
1580 if half then call "CDS454", y6$, 10, 1, "C", "Points", 1, 1, 1, 14, 70
1585 playtoday$ = ptoday$[pos(flt$ = flts$)]
1590 return

1600 nskin:
1610 call "CDS454::SETUP", 150, row, "74,560" + fill(10 * 4,",112"), "100" + fill(recs * 4, ",100"), 1
1620 call "CDS454", y6$, 1, 1, "C", "ID", 1, 1, 1, 14, 70
1630 call "CDS454", y6$, 2, 1, "L", "Name", 1, 1, 1, 14, 70
1640 call "CDS454", y6$, 3, 1, "C", "Paid", 1, 1, 1, 14, 70
1650 hole = hole0
1660 for colno = 4 to maxcols
1670   call "CDS454", y6$, colno, 1, "C", str(hole), 1, 1, 1, 14, 70
1680   hole = hole + 1
1690 next colno
1700 return

1800 hclist:
1810 gosub wkno
1811 if half = 1 or num(cgi.date$) = first2halfdate then
:      showhalf = 1
:    else
:      showhalf = 2
:    fi
1815 for pass = 1 to 2
1819   flt$ = ""
1820   read (gr04, knum=1, key=wases.league$ + "A", dom=1830)
1830   read record(gr04, end=2150)gr04$
1840   if gr04.league$ <> wases.league$ then goto 2150
1850   hc$ = ""
1860   dim rnd$[1:9]
1870   if len(flt$) and gr04.flt$ <> flt$ and pass = 2 then
:        flt$ = "#", doc$ = "";
:        gosub finishflt
:      fi
1880   if gr04.flt$ <> flt$ then flt$ = gr04.flt$; if pass = 2 then gosub newhcflt
1885   gosub calcs
1886   if pass = 1 then goto 1830
1890   rowno = rowno + 1
1900   call "CDS454", y6$, 1, rowno, "C", str(gr04.seq)
1910   call "CDS454", y6$, 2, rowno, "L", cvs(cvs(gr04.firstname$, 2) + " " + cvs(gr04.lastname$, 2), 1)
1920   call "CDS454", y6$, 3, rowno, "C", gr04.phone$
1930   if half then call "CDS454", y6$, 4, rowno, "C", str(asc(playtoday$(gr04.seq)))
1932   z = pos(gr04.flt$ + playtoday$(gr04.seq, 1) = fsi$, 6), ptid$ = fsi$(z + 2, 4)
1940   if !(lastwkofyr) and half then
:        call "CDS454", y6$, 5, rowno, "C", str(asc(playnext$(gr04.seq)))
:      fi
1960   call "CDS454", y6$, 6, rowno, "C", hc$ + ""
1965   z = pos(flt$ = best$, 9)
1966   if z and points[showhalf] and points[showhalf] = num(best$(z + 1, 4)) then
:        red$ = "red"
:      else
:        red$ = ""
:      fi
1970   if half then
:        call "CDS454", y6$, 7, rowno, "R", str(points[showhalf]), 0, 0, 0, 0, 0, 0, red$
:      fi
1980   call "CDS454", y6$, 8, rowno, "R", avgscore$
1985   if z and avgputts$ = best$(z + 5, 4) then red$ = "red" else red$ = ""
1990   call "CDS454", y6$, 9, rowno, "R", avgputts$, 0, 0, 0, 0, 0, 0, red$
1995   if bestscore = lastscore then red$ = "red", bestrndno = 0 else red$ = ""
2000   call "CDS454", y6$, 10, rowno, "L", lastround$, 0, 0, 0, 0, 0, 0, red$
2010   colno = 11
2020   for rnd = 1 to 9
2025     if rnd = bestrndno then red$ = "red" else red$ = ""
2030     call "CDS454", y6$, colno, rowno, "L", rnd$[rnd], 0, 0, 0, 8, 33, 0, red$
2040     colno = colno + 1
2050   next rnd
2060   dim gr05$:fattr(gr05$)
2070   gr05.league$ = gr01.league$
2080   gr05.id$ = gr04.id$
2090   gr05.date = num(cgi.date$)
2100   extract record(gr05, knum=0, key=kgen(gr05$, gr05k$, 0), dom=2110)gr05$;
:      if gr05.score then
:        goto 2120
:      fi
2110   gr05.hc = num(hc$)
2120   if gr05.score and gr05.hc then gr05.net = gr05.score - gr05.hc
2125   gr05.flt$ = gr04.flt$
2127   if gr05.score = 0 then gr05.opp$ = ptid$, gr05.par = par
2130   write record(gr05)gr05$
2140   goto 1830
2150   if pass = 2 then flt$ = "#", doc$ = ""; gosub finishflt
2155 next pass
2160 return

2200 newhcflt:
2210 call "CDS094", y6$
2220 row = 25
2230 call "CDS254", y6$, "T1575C," + str(row) + ",3,1,14,,4148", gr01.name$
2240 row = row + 65
2250 call "CDS254", y6$, "T1575C," + str(row) + ",3,1,14,,4148", "Handicap Listing"
2260 row = row + 65
2270 call "CDS254", y6$, "T1575C," + str(row) + ",3,1,14,,4148", flt$ + " Flight - " + date(num(cgi.date$)) + " - " + nine$
2280 row = row + 40
2290 call "SW004", "XXGR04", 1, wases.league$ + flt$, recs, 0, "", "NP"
2300 rowno = 1
2310 call "CDS454::SETUP", 0, row, "52,384,288," + str(107 * sgn(half)) + "," + str(107 * !(lastwkofyr) * sgn(half)) + ",107," + str(107 * sgn(half)) + ",107,107,310" + fill(9 * 4,",100"), "120," + fill(recs * 4, "060,"), 1
2320 call "CDS454", y6$, 1, 1, "C", "ID", 1
2330 call "CDS454", y6$, 2, 1, "L", "Name", 1
2340 call "CDS454", y6$, 3, 1, "C", "Phone", 1
2350 if half then call "CDS454", y6$, 4, 1, "C", "Play" + $0a$ + "Today", 1
2360 if !(lastwkofyr) and half then
:      call "CDS454", y6$, 5, 1, "C", "Play" + $0a$ + "Next", 1
:    fi
2370 call "CDS454", y6$, 6, 1, "C", "HC", 1
2380 if half then call "CDS454", y6$, 7, 1, "C", "Points", 1
2390 call "CDS454", y6$, 8, 1, "C", "Avg" + $0a$ + "Score", 1
2400 call "CDS454", y6$, 9, 1, "C", "Avg" + $0a$ + "Putts", 1
2410 call "CDS454", y6$, 10, 1, "C", "Last Round", 1
2420 call "CDS454", y6$, 11, 1, "C", "Prior Rounds (Gross & Points)", 1, 9
2430 gosub playwho
2440 return

2500 playwho:
2510 playtoday$ = "", playnext$ = ""
2520 strt = wkno - 10 * num(date(0:"%Yz"))
2525 while strt < 1; strt = strt + recs - 1; wend
2526 strt0 = strt
2530 for seq = 1 to recs - 1
2540   if strt <> len(playtoday$) + 1 then
:        play = strt
:      else
:        play = recs, last = len(playtoday$) + 1
:      fi
2550   playtoday$ = playtoday$ + chr(play), strt = strt - 1
2560   if strt = 0 then strt = recs - 1
2570 next seq
2580 playtoday$ = playtoday$ + chr(last), last = 0
2590 strt = strt0 + 1
2595 while strt < 1; strt = strt + recs - 1; wend
2600 for seq = 1 to recs - 1
2610   if strt <> len(playnext$) + 1 then
:        play = strt
:      else
:        play = recs, last = len(playnext$) + 1
:      fi
2620   playnext$ = playnext$ + chr(play), strt = strt - 1
2630   if strt = 0 then strt = recs - 1
2640 next seq
2650 playnext$ = playnext$ + chr(max(last, 1))
2655 flts$ = flts$ + flt$, ptoday$[len(flts$)] = playtoday$
2656 if 0 then
:      call "CDS094", y6$, "e";
:      call "CDS173";
:      html$ = "<p>See Dump</p>";
:      run "CDW999"
:    fi
2660 return

2700 wkno:
2710 wkno = 0, first2halfdate = 0
2715 dim dates$[3]
2720 read (gr03, knum=0, key=wases.league$ + $ffffff$, dom=2730)
2730 read record(gr03, end=2770)gr03$
2740 if gr03.league$ <> wases.league$ then goto 2770
2750 if gr03.date > num(cgi.date$) then goto 2730
2755 if gr03.half = 2 then first2halfdate = gr03.date
2760 if date(gr03.date:"%Y") = date(num(cgi.date$):"%Y") then
:      dates$[gr03.half] = dates$[gr03.half] + gr03.date$;
:      if gr03.half = 1 or gr03.half = 2 then
:        wkno = wkno + 1
:      fi;
:      goto 2730
:    fi
2770 return

2800 calcs:
2810 dim rnd$[1:9], points[2]
2820 scores = 0, totscore = 0, totputts = 0, lastround$ = "", sclist$ = "", rnd = 0, puttsc = 0, lastscore = 0, bestscore = 100, bestrndno = 0, totpar = 0
2830 read (gr05, knum=0, key=gr04.league$ + gr04.id$ + bin(num(cgi.date$), 3), dom=2840)
2840 read record(gr05, end=2910)gr05$
2850 if gr05.score = 0 then goto 2840
2855 if gr05.par = 0 then gr05.par = 36
2860 if gr05.league$ <> gr04.league$ or gr05.id$ <> gr04.id$ then goto 2910
2870 if len(lastround$) = 0 then
:      lastscore = gr05.score, lastround$ = date(gr05.date:"%M/%Dz") + str(gr05.score:"##0") + str(gr05.putts:"###B") + fnh$(gr05.points)
:    else
:      rnd = rnd + 1, rnd$[rnd] = str(gr05.score) + " " + fnh$(gr05.points)
:    fi
2875 if gr05.score < bestscore then bestscore = gr05.score, bestrndno = rnd
2880 if pos(gr05.date$ = dates$[1], 3) then
:      dh = 1
:    else
:      if pos(gr05.date$ = dates$[2], 3) then
:        dh = 2
:      else
:        dh = 0
:      fi
:    fi
2881 if dh then points[dh] = points[dh] + gr05.points
2890 scores = scores + 1, sclist$ = sclist$ + str(gr05.score:"00") + str(gr05.putts:"00") + str(gr05.par:"00")
2900 if scores < 10 then goto 2840
2910 if scores then
:      gosub avg
:    else
:      avgscore$ = "", avgputts$ = "";
:      if gr04.hc then
:        hc$ = str(gr04.hc)
:      else
:        if gr04.flt$ = "A" then
:          hc$ = "8"
:        else
:          hc$ = "14"
:        fi
:      fi
:    fi
2920 return

3000 avg:
3010 sclist$ = ssort(sclist$, 6)
3020 h = 1, scores = min(8, scores)
3030 for score = 1 to scores
3040   totscore = totscore + num(sclist$(h, 2))
3050   if num(sclist$(h + 2, 2)) then
:        totputts = totputts + num(sclist$(h + 2, 2)), puttsc = puttsc + 1
:      fi
3055   totpar = totpar + num(sclist$(h + 4, 2))
3060   h = h + 6
3070 next score
3080 avgscore$ = str(totscore / scores:"##.0")
3090 if puttsc then
:      avgputts$ = str(totputts / puttsc:"##.0")
:    else
:      avgputts$ = ""
:    fi
3100 hc = (totscore - totpar) / scores
3105 if gr01.hcmult and gr01.hcmult < 100 then hc = hc * gr01.hcmult / 100
3110 if scores < 8 then hc = hc * .75
3120 if fpt(hc) then hc = int(hc) + 1
3130 hc$ = str(min(max(hc, 0), gr01.maxhc))
3135 if pass = 1 then gosub best
3140 return

3150 best:
3160 z = pos(flt$ = best$, 9)
3165 if z = 0 then z = len(best$) + 1, best$ = best$ + flt$ + "00.099.9"
3170 if points[showhalf] > num(best$(z + 1, 4)) then
:      best$(z + 1, 4) = str(points[showhalf]:"##.0")
:    fi
3175 if len(avgputts$) and avgputts$ < best$(z + 5, 4) then
:      best$(z + 5, 4) = avgputts$
:    fi
3190 return

3200 functions:
3210 def fnh$(pts)
3220 out$ = str(int(pts))
3230 if fpt(pts) then out$ = out$ + ".5"
3240 return out$
3250 fnend

7000 test:
7010 begin
7020 call "CDS091", y$
7030 call "CDS041", "WASES", wases$, "YY"
7040 wases.league$ = "WAL"
7050 wases.leaguename$ = "Test League Name"
7060 dim cgi$:"DATE:N(7)"
7070 cgi.date = jul(2012,5,9)
7080 goto 0120

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 if tcb(13) then exit
9020 run "WA010"
