0100 REM "GW045 - 05/30/17 Score Posting Sheets & Handicap Listing
0110 SETESC DT_ERR; SETERR DT_ERR
0120 CALL "SW010","XXGR04",0,"","FLT|SEQ|ID",FSI$,"","","REC.LEAGUE$="""+WASES.LEAGUE$+""" AND REC.FLT$<>"" """
0125 DIM PTODAY$[1:4]; LET FLTS$=""
0130 CALL "SW001","XXGR01",0,WASES.LEAGUE$,GR01$
0131 CALL "SW001","XXGR08",0,GR01.ID$,GR08$,SW001$
0140 CALL "CDS095",GR03,"XXGR03","YPY",GR03$
0150 READ RECORD(GR03,KNUM=0,KEY=WASES.LEAGUE$+BIN(NUM(CGI.DATE$),3))GR03$
0151 CALL "CDS041","GW032",GW032$,"YY"
0152 CALL "GW032",Y$,GR03$,GW032$
0153 LET LASTWKOFYR=GW032.LASTWEEK
0155 LET HALF=GR03.HALF
0160 IF GR03.NINE$="F"
:       THEN
:          LET NINE$="Front Nine",HOLE0=1,PAR=GR08.PARF
:       ELSE
:          LET NINE$="Back Nine",HOLE0=10,PAR=GR08.PARB
0170 CALL "CDS095",GR04,"XXGR04","YPY",GR04$
0175 CALL "CDS133","XXGR05","","NNYY";
:    REM "Archive scores file for possible rollback if prior scores are lost
0180 CALL "CDS095",GR05,"XXGR05","YNY",GR05$,GR05K$
0182 LET GR05S=UNT
0183 SELECT (GR05S)GR05$ FROM Y.COMPCODE$+"GR05" WHERE GR05.LEAGUE$=WASES.LEAGUE$ AND GR05.DATE=NUM(CGI.DATE$) AND GR05.SCORE=0
0184 READ RECORD(GR05S,END=0189)GR05$
0185 REMOVE (GR05,KEY=KGEN(GR05$,GR05K$,0))
0186 GOTO 0184
0189 CLOSE (GR05S)
0190 CALL "CDS041","CDS084",S084$
0200 LET S084.MODE$="E",S084.NAME$="APDF",S084.SIZE=2
0210 CALL "CDS084",Y$,Y5$,U0,S084$,Y6,Y6$
0215 LET HCFILE$=Y6.FILEOUT$(POS("/tmp/"=Y6.FILEOUT$))
0220 GOSUB HCLIST
0230 CALL "CDS094",Y6$,"E"
0240 LET S084.MODE$="E",S084.NAME$="APDF",S084.SIZE=2,S084.FILEOUT$=""
0250 CALL "CDS084",Y$,Y5$,U0,S084$,Y6,Y6$
0260 LET FLT$="",SPFILE$=Y6.FILEOUT$(POS("/tmp/"=Y6.FILEOUT$))
0270 LET DOC$="Score",MAXCOLS=10; GOSUB DODOC
0280 LET DOC$="Skin",MAXCOLS=12; GOSUB DODOC
0290 CALL "CDS094",Y6$,"E"
0300 LET HTML$="<html><head><title>"+CVS(GR01.NAME$,2)+"</title></head><frameset cols='*,*'><frame src='"+HCFILE$+"'><frame src='"+SPFILE$+"'></frameset></html>"
0310 LET TMP$=STBL("!CLEAR","$title")
0320 DIM S084$(0)
0330 LET HTMFIL$=",htm"; CALL "CDS064",Y$,TMP,0,0,0,0,"STR",HTMFIL$
0340 WRITE (TMP)HTML$
0350 CLOSE (TMP)
0355 LET HTMFIL$=HTMFIL$(POS("/tmp/"=HTMFIL$))
0360 LET HTML$="<script>window.open('"+HTMFIL$+"');</script>"
0370 RUN "GW011"

1000 DODOC:
1005 LET FLT$=""
1010 READ (GR04,KNUM=1,KEY=WASES.LEAGUE$,DOM=1020)
1020 READ RECORD(GR04,END=1170)GR04$
1025 IF GR04.LEAGUE$<>WASES.LEAGUE$ THEN GOTO 1170
1030 IF GR04.FLT$=" " THEN GOTO 1020
1040 LET HC$=""
1050 IF DOC$="Score"
:       THEN
:          READ RECORD(GR05,KNUM=0,KEY=GR04.LEAGUE$+GR04.ID$+BIN(NUM(CGI.DATE$),3))GR05$;
:          IF GR05.HC
:             THEN
:                LET HC$=STR(GR05.HC)
:             ELSE
:                LET HC$=""
1060 IF LEN(FLT$) AND GR04.FLT$<>FLT$ THEN GOSUB FINISHFLT
1070 IF GR04.FLT$<>FLT$ THEN GOSUB NEWFLT
1080 LET ROWNO=ROWNO+1
1085 FOR COLNO=1 TO MAXCOLS
1090    IF COLNO=1
:          THEN
:             CALL "CDS454",Y6$,COLNO,ROWNO,"C",STR(GR04.SEQ),0,0,0,14,70;
:             CONTINUE
1100    IF COLNO=2
:          THEN
:             CALL "CDS454",Y6$,COLNO,ROWNO,"L",CVS(CVS(GR04.FIRSTNAME$,2)+" "+CVS(GR04.LASTNAME$,2),1),0,0,0,14,70;
:             CONTINUE
1110    IF COLNO=3 AND DOC$="Score"
:          THEN
:             CALL "CDS454",Y6$,COLNO,ROWNO,"L",GR04.PHONE$,0,0,0,14,70;
:             CONTINUE
1112    IF COLNO=4 AND DOC$="Score"
:          THEN
:             CALL "CDS454",Y6$,COLNO,ROWNO,"L",CVS(GR04.EMAIL$,2),0,0,0,14,70;
:             CONTINUE
1114    IF COLNO=5 AND DOC$="Score"
:          THEN
:             IF HALF
:                THEN
:                   CALL "CDS454",Y6$,COLNO,ROWNO,"C",STR(ASC(PLAYTODAY$(GR04.SEQ))),0,0,0,14,70
:             FI;
:             CONTINUE
1120    IF COLNO=7 AND DOC$="Score"
:          THEN
:             CALL "CDS454",Y6$,COLNO,ROWNO,"C",HC$,0,0,0,14,70;
:             CONTINUE
1140    IF DOC$<>"Score" OR COLNO<>10 OR HALF THEN CALL "CDS454",Y6$,COLNO,ROWNO
1150 NEXT COLNO
1160 GOTO 1020
1170 GOSUB FINISHFLT
1180 RETURN

1200 FINISHFLT:
1210 IF DOC$="Skin" THEN RETURN
1220 IF 0 THEN CALL "CDS254",Y6$,"T1,,,,12,,4148"," "+$0D0A0D0A$
1221 IF 1 THEN CALL "CDS254",Y6$,"T1,+75,,,12,,4148"
1230 CALL "CDS257",Y$,GR01.LEAGUE$+FLT$,"","XXGR02","PN",8,0,0,0,Y6$
1240 RETURN

1300 NEWFLT:
1310 CALL "CDS094",Y6$
1315 IF DOC$="Skin"
:       THEN
:          PRINT (Y6.CH)'BO',Y6.DEPTH11$,'EO',;
:          LET ACROSS=8.5,ROW=150,SKIP=120
:       ELSE
:          LET ACROSS=11,ROW=50,SKIP=50
1320 LET FLT$=GR04.FLT$,CENTER$="T"+STR((ACROSS-.5)*300/2)+"C,"
1321 LET RIGHT$="T"+STR((ACROSS-.5)*300)+"R,"
1330 CALL "CDS254",Y6$,CENTER$+STR(ROW)+",3,1,14,,4148",GR01.NAME$
1340 IF 0 THEN LET ROW=ROW+75
1350 CALL "CDS254",Y6$,"T0,"+STR(ROW)+",3,1,14,,4148",DOC$+" Posting Sheet"
1360 IF 0 THEN LET ROW=ROW+75
1370 CALL "CDS254",Y6$,RIGHT$+STR(ROW)+",3,1,14,,4148",FLT$+" Flight - "+DATE(NUM(CGI.DATE$))
1380 LET ROW=ROW+SKIP
1390 CALL "SW004","XXGR04",1,WASES.LEAGUE$+FLT$,RECS,0,"","NP"
1400 LET ROWNO=1
1410 ON DOC$="Score" GOSUB NSKIN,NSCORE
1420 RETURN

1500 NSCORE:
1510 CALL "CDS454::SETUP",0,ROW,"74,560,408,872,"+STR(111*SGN(HALF))+",165,165,165,165,165","84,"+FILL(RECS*3,"84,"),1
1520 CALL "CDS454",Y6$,1,1,"C","ID",1,1,1,14,70
1530 CALL "CDS454",Y6$,2,1,"L","Name",1,1,1,14,70
1532 CALL "CDS454",Y6$,3,1,"L","Phone",1,1,1,14,70
1534 CALL "CDS454",Y6$,4,1,"L","Email",1,1,1,14,70
1535 IF HALF THEN CALL "CDS454",Y6$,5,1,"L","Play",1,1,1,14,70
1540 CALL "CDS454",Y6$,6,1,"C","Gross",1,1,1,14,70
1550 CALL "CDS454",Y6$,7,1,"C","HC",1,1,1,14,70
1560 CALL "CDS454",Y6$,8,1,"C","Net",1,1,1,14,70
1570 CALL "CDS454",Y6$,9,1,"C","Putts",1,1,1,14,70
1580 IF HALF THEN CALL "CDS454",Y6$,10,1,"C","Points",1,1,1,14,70
1585 LET PLAYTODAY$=PTODAY$[POS(FLT$=FLTS$)]
1590 RETURN

1600 NSKIN:
1610 CALL "CDS454::SETUP",150,ROW,"74,560"+FILL(10*4,",112"),"100"+FILL(RECS*4,",100"),1
1620 CALL "CDS454",Y6$,1,1,"C","ID",1,1,1,14,70
1630 CALL "CDS454",Y6$,2,1,"L","Name",1,1,1,14,70
1640 CALL "CDS454",Y6$,3,1,"C","Paid",1,1,1,14,70
1650 LET HOLE=HOLE0
1660 FOR COLNO=4 TO MAXCOLS
1670    CALL "CDS454",Y6$,COLNO,1,"C",STR(HOLE),1,1,1,14,70
1680    LET HOLE=HOLE+1
1690 NEXT COLNO
1700 RETURN

1800 HCLIST:
1810 GOSUB WKNO
1811 IF HALF=1 OR NUM(CGI.DATE$)=FIRST2HALFDATE
:       THEN
:          LET SHOWHALF=1
:       ELSE
:          LET SHOWHALF=2
1815 FOR PASS=1 TO 2
1819    LET FLT$=""
1820    READ (GR04,KNUM=1,KEY=WASES.LEAGUE$+"A",DOM=1830)
1830    READ RECORD(GR04,END=2150)GR04$
1840    IF GR04.LEAGUE$<>WASES.LEAGUE$ THEN GOTO 2150
1850    LET HC$=""
1860    DIM RND$[1:9]
1870    IF LEN(FLT$) AND GR04.FLT$<>FLT$ AND PASS=2
:          THEN
:             LET FLT$="#",DOC$="";
:             GOSUB FINISHFLT
1880    IF GR04.FLT$<>FLT$ THEN LET FLT$=GR04.FLT$; IF PASS=2 THEN GOSUB NEWHCFLT
1885    GOSUB CALCS
1886    IF PASS=1 THEN GOTO 1830
1890    LET ROWNO=ROWNO+1
1900    CALL "CDS454",Y6$,1,ROWNO,"C",STR(GR04.SEQ)
1910    CALL "CDS454",Y6$,2,ROWNO,"L",CVS(CVS(GR04.FIRSTNAME$,2)+" "+CVS(GR04.LASTNAME$,2),1)
1920    CALL "CDS454",Y6$,3,ROWNO,"C",GR04.PHONE$
1930    IF HALF THEN CALL "CDS454",Y6$,4,ROWNO,"C",STR(ASC(PLAYTODAY$(GR04.SEQ)))
1932    LET Z=POS(GR04.FLT$+PLAYTODAY$(GR04.SEQ,1)=FSI$,6),PTID$=FSI$(Z+2,4)
1940    IF !(LASTWKOFYR) AND HALF
:          THEN
:             CALL "CDS454",Y6$,5,ROWNO,"C",STR(ASC(PLAYNEXT$(GR04.SEQ)))
1960    CALL "CDS454",Y6$,6,ROWNO,"C",HC$+""
1965    LET Z=POS(FLT$=BEST$,9)
1966    IF Z AND POINTS[SHOWHALF] AND POINTS[SHOWHALF]=NUM(BEST$(Z+1,4))
:          THEN
:             LET RED$="red"
:          ELSE
:             LET RED$=""
1970    IF HALF
:          THEN
:             CALL "CDS454",Y6$,7,ROWNO,"R",STR(POINTS[SHOWHALF]),0,0,0,0,0,0,RED$
1980    CALL "CDS454",Y6$,8,ROWNO,"R",AVGSCORE$
1985    IF Z AND AVGPUTTS$=BEST$(Z+5,4) THEN LET RED$="red" ELSE LET RED$=""
1990    CALL "CDS454",Y6$,9,ROWNO,"R",AVGPUTTS$,0,0,0,0,0,0,RED$
1995    IF BESTSCORE=LASTSCORE THEN LET RED$="red",BESTRNDNO=0 ELSE LET RED$=""
2000    CALL "CDS454",Y6$,10,ROWNO,"L",LASTROUND$,0,0,0,0,0,0,RED$
2010    LET COLNO=11
2020    FOR RND=1 TO 9
2025       IF RND=BESTRNDNO THEN LET RED$="red" ELSE LET RED$=""
2030       CALL "CDS454",Y6$,COLNO,ROWNO,"L",RND$[RND],0,0,0,8,33,0,RED$
2040       LET COLNO=COLNO+1
2050    NEXT RND
2060    DIM GR05$:FATTR(GR05$)
2070    LET GR05.LEAGUE$=GR01.LEAGUE$
2080    LET GR05.ID$=GR04.ID$
2090    LET GR05.DATE=NUM(CGI.DATE$)
2100    EXTRACT RECORD(GR05,KNUM=0,KEY=KGEN(GR05$,GR05K$,0),DOM=2110)GR05$;
:       IF GR05.SCORE
:          THEN
:             GOTO 2120
2110    LET GR05.HC=NUM(HC$)
2120    IF GR05.SCORE AND GR05.HC THEN LET GR05.NET=GR05.SCORE-GR05.HC
2125    LET GR05.FLT$=GR04.FLT$
2127    IF GR05.SCORE=0 THEN LET GR05.OPP$=PTID$,GR05.PAR=PAR
2130    WRITE RECORD(GR05)GR05$
2140    GOTO 1830
2150    IF PASS=2 THEN LET FLT$="#",DOC$=""; GOSUB FINISHFLT
2155 NEXT PASS
2160 RETURN

2200 NEWHCFLT:
2210 CALL "CDS094",Y6$
2220 LET ROW=25
2230 CALL "CDS254",Y6$,"T1575C,"+STR(ROW)+",3,1,14,,4148",GR01.NAME$
2240 LET ROW=ROW+65
2250 CALL "CDS254",Y6$,"T1575C,"+STR(ROW)+",3,1,14,,4148","Handicap Listing"
2260 LET ROW=ROW+65
2270 CALL "CDS254",Y6$,"T1575C,"+STR(ROW)+",3,1,14,,4148",FLT$+" Flight - "+DATE(NUM(CGI.DATE$))+" - "+NINE$
2280 LET ROW=ROW+40
2290 CALL "SW004","XXGR04",1,WASES.LEAGUE$+FLT$,RECS,0,"","NP"
2300 LET ROWNO=1
2310 CALL "CDS454::SETUP",0,ROW,"52,384,288,"+STR(107*SGN(HALF))+","+STR(107*!(LASTWKOFYR)*SGN(HALF))+",107,"+STR(107*SGN(HALF))+",107,107,310"+FILL(9*4,",100"),"120,"+FILL(RECS*4,"060,"),1
2320 CALL "CDS454",Y6$,1,1,"C","ID",1
2330 CALL "CDS454",Y6$,2,1,"L","Name",1
2340 CALL "CDS454",Y6$,3,1,"C","Phone",1
2350 IF HALF THEN CALL "CDS454",Y6$,4,1,"C","Play"+$0A$+"Today",1
2360 IF !(LASTWKOFYR) AND HALF
:       THEN
:          CALL "CDS454",Y6$,5,1,"C","Play"+$0A$+"Next",1
2370 CALL "CDS454",Y6$,6,1,"C","HC",1
2380 IF HALF THEN CALL "CDS454",Y6$,7,1,"C","Points",1
2390 CALL "CDS454",Y6$,8,1,"C","Avg"+$0A$+"Score",1
2400 CALL "CDS454",Y6$,9,1,"C","Avg"+$0A$+"Putts",1
2410 CALL "CDS454",Y6$,10,1,"C","Last Round",1
2420 CALL "CDS454",Y6$,11,1,"C","Prior Rounds (Gross & Points)",1,9
2430 GOSUB PLAYWHO
2440 RETURN

2500 PLAYWHO:
2510 LET PLAYTODAY$="",PLAYNEXT$=""
2520 LET STRT=WKNO-10*NUM(DATE(0:"%Yz"))
2525 WHILE STRT<1; LET STRT=STRT+RECS-1; WEND
2526 LET STRT0=STRT
2530 FOR SEQ=1 TO RECS-1
2540    IF STRT<>LEN(PLAYTODAY$)+1
:          THEN
:             LET PLAY=STRT
:          ELSE
:             LET PLAY=RECS,LAST=LEN(PLAYTODAY$)+1
2550    LET PLAYTODAY$=PLAYTODAY$+CHR(PLAY),STRT=STRT-1
2560    IF STRT=0 THEN LET STRT=RECS-1
2570 NEXT SEQ
2580 LET PLAYTODAY$=PLAYTODAY$+CHR(LAST),LAST=0
2590 LET STRT=STRT0+1
2595 WHILE STRT<1; LET STRT=STRT+RECS-1; WEND
2600 FOR SEQ=1 TO RECS-1
2610    IF STRT<>LEN(PLAYNEXT$)+1
:          THEN
:             LET PLAY=STRT
:          ELSE
:             LET PLAY=RECS,LAST=LEN(PLAYNEXT$)+1
2620    LET PLAYNEXT$=PLAYNEXT$+CHR(PLAY),STRT=STRT-1
2630    IF STRT=0 THEN LET STRT=RECS-1
2640 NEXT SEQ
2650 LET PLAYNEXT$=PLAYNEXT$+CHR(MAX(LAST,1))
2655 LET FLTS$=FLTS$+FLT$,PTODAY$[LEN(FLTS$)]=PLAYTODAY$
2656 IF 0
:       THEN
:          CALL "CDS094",Y6$,"e";
:          CALL "CDS173";
:          LET HTML$="<p>See Dump</p>";
:          RUN "CDW999"
2660 RETURN

2700 WKNO:
2710 LET WKNO=0,FIRST2HALFDATE=0
2715 DIM DATES$[3]
2720 READ (GR03,KNUM=0,KEY=WASES.LEAGUE$+$FFFFFF$,DOM=2730)
2730 READ RECORD(GR03,END=2770)GR03$
2740 IF GR03.LEAGUE$<>WASES.LEAGUE$ THEN GOTO 2770
2750 IF GR03.DATE>NUM(CGI.DATE$) THEN GOTO 2730
2755 IF GR03.HALF=2 THEN LET FIRST2HALFDATE=GR03.DATE
2760 IF DATE(GR03.DATE:"%Y")=DATE(NUM(CGI.DATE$):"%Y")
:       THEN
:          LET DATES$[GR03.HALF]=DATES$[GR03.HALF]+GR03.DATE$;
:          IF GR03.HALF=1 OR GR03.HALF=2
:             THEN
:                LET WKNO=WKNO+1
:          FI;
:          GOTO 2730
2770 RETURN

2800 CALCS:
2810 DIM RND$[1:9],POINTS[2]
2820 LET SCORES=0,TOTSCORE=0,TOTPUTTS=0,LASTROUND$="",SCLIST$="",RND=0,PUTTSC=0,LASTSCORE=0,BESTSCORE=100,BESTRNDNO=0,TOTPAR=0
2830 READ (GR05,KNUM=0,KEY=GR04.LEAGUE$+GR04.ID$+BIN(NUM(CGI.DATE$),3),DOM=2840)
2840 READ RECORD(GR05,END=2910)GR05$
2850 IF GR05.SCORE=0 THEN GOTO 2840
2855 IF GR05.PAR=0 THEN LET GR05.PAR=36
2860 IF GR05.LEAGUE$<>GR04.LEAGUE$ OR GR05.ID$<>GR04.ID$ THEN GOTO 2910
2870 IF LEN(LASTROUND$)=0
:       THEN
:          LET LASTSCORE=GR05.SCORE,LASTROUND$=DATE(GR05.DATE:"%M/%Dz")+STR(GR05.SCORE:"##0")+STR(GR05.PUTTS:"###B")+FNH$(GR05.POINTS)
:       ELSE
:          LET RND=RND+1,RND$[RND]=STR(GR05.SCORE)+" "+FNH$(GR05.POINTS)
2875 IF GR05.SCORE<BESTSCORE THEN LET BESTSCORE=GR05.SCORE,BESTRNDNO=RND
2880 IF POS(GR05.DATE$=DATES$[1],3)
:       THEN
:          LET DH=1
:       ELSE
:          IF POS(GR05.DATE$=DATES$[2],3)
:             THEN
:                LET DH=2
:             ELSE
:                LET DH=0
2881 IF DH THEN LET POINTS[DH]=POINTS[DH]+GR05.POINTS
2890 LET SCORES=SCORES+1,SCLIST$=SCLIST$+STR(GR05.SCORE:"00")+STR(GR05.PUTTS:"00")+STR(GR05.PAR:"00")
2900 IF SCORES<10 THEN GOTO 2840
2910 IF SCORES
:       THEN
:          GOSUB AVG
:       ELSE
:          LET AVGSCORE$="",AVGPUTTS$="";
:          IF GR04.HC
:             THEN
:                LET HC$=STR(GR04.HC)
:             ELSE
:                IF GR04.FLT$="A"
:                   THEN
:                      LET HC$="8"
:                   ELSE
:                      LET HC$="14"
2920 RETURN

3000 AVG:
3010 LET SCLIST$=SSORT(SCLIST$,6)
3020 LET H=1,SCORES=MIN(8,SCORES)
3030 FOR SCORE=1 TO SCORES
3040    LET TOTSCORE=TOTSCORE+NUM(SCLIST$(H,2))
3050    IF NUM(SCLIST$(H+2,2))
:          THEN
:             LET TOTPUTTS=TOTPUTTS+NUM(SCLIST$(H+2,2)),PUTTSC=PUTTSC+1
3055    LET TOTPAR=TOTPAR+NUM(SCLIST$(H+4,2))
3060    LET H=H+6
3070 NEXT SCORE
3080 LET AVGSCORE$=STR(TOTSCORE/SCORES:"##.0")
3090 IF PUTTSC
:       THEN
:          LET AVGPUTTS$=STR(TOTPUTTS/PUTTSC:"##.0")
:       ELSE
:          LET AVGPUTTS$=""
3100 LET HC=(TOTSCORE-TOTPAR)/SCORES
3105 IF GR01.HCMULT AND GR01.HCMULT<100 THEN LET HC=HC*GR01.HCMULT/100
3110 IF SCORES<8 THEN LET HC=HC*.75
3120 IF FPT(HC) THEN LET HC=INT(HC)+1
3130 LET HC$=STR(MIN(MAX(HC,0),GR01.MAXHC))
3135 IF PASS=1 THEN GOSUB BEST
3140 RETURN

3150 BEST:
3160 LET Z=POS(FLT$=BEST$,9)
3165 IF Z=0 THEN LET Z=LEN(BEST$)+1,BEST$=BEST$+FLT$+"00.099.9"
3170 IF POINTS[SHOWHALF]>NUM(BEST$(Z+1,4))
:       THEN
:          LET BEST$(Z+1,4)=STR(POINTS[SHOWHALF]:"##.0")
3175 IF LEN(AVGPUTTS$) AND AVGPUTTS$<BEST$(Z+5,4)
:       THEN
:          LET BEST$(Z+5,4)=AVGPUTTS$
3190 RETURN

3200 FUNCTIONS:
3210 DEF FNH$(PTS)
3220 LET OUT$=STR(INT(PTS))
3230 IF FPT(PTS) THEN LET OUT$=OUT$+".5"
3240 RETURN OUT$
3250 FNEND

7000 TEST:
7010 BEGIN
7020 CALL "CDS091",Y$
7030 CALL "CDS041","WASES",WASES$,"YY"
7040 LET WASES.LEAGUE$="WAL"
7050 LET WASES.LEAGUENAME$="Test League Name"
7060 DIM CGI$:"DATE:N(7)"
7070 LET CGI.DATE=JUL(2012,5,9)
7080 GOTO 0120

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 IF TCB(13) THEN EXIT
9020 RUN "WA010"
