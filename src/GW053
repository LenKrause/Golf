0100 REM "GW053 - 07/10/17 Golf Point Ranking - html
0110 SETESC DT_ERR; SETERR DT_ERR
0130 LET HTML$="",TOEMAIL$="",HALF=NUM(CGI.PERIOD$)
0140 LET TMP$=STBL("WMS_CDW000","D")
0170 CALL "CDS095",GR03,"XXGR03","YNY",GR03$
0180 CALL "CDS095",GR04,"XXGR04","YNY",GR04$
0200 LET GR03S=UNT
0210 SELECT (GR03S)GR03$ FROM Y.COMPCODE$+"GR03" WHERE GR03.LEAGUE$=WASES.LEAGUE$ AND DATE(GR03.DATE:"%Yl")=CGI.YEAR$
0220 READ RECORD(GR03S,END=0250)GR03$
0230 IF HALF=3 OR GR03.HALF=HALF THEN LET DATES$=DATES$+GR03.DATE$
0240 GOTO 0220
0250 CLOSE (GR03S)
0252 LET DATES=LEN(DATES$)/3
0254 IF DATES=0 THEN LET MSG$="No rounds played"; RUN "GW011"
0260 LET DATES$=SSORT(DATES$,3)
0270 DIM DATES$[1:DATES]
0280 CALL "CDS095",GR05,"XXGR05","YNY",GR05$
0290 DIM GS$[1:100]
0300 LET GR05S=UNT
0310 SELECT (GR05S)GR05$ FROM Y.COMPCODE$+"GR05" WHERE GR05.LEAGUE$=WASES.LEAGUE$ AND POS(GR05.DATE$=DATES$,3) SORTBY GR05.ID$+GR05.DATE$
0320 READ RECORD(GR05S,END=0380)GR05$
0330 IF LEN(CG$) AND CG$(1,4)<>GR05.ID$
:       THEN
:          IF LEN(CG$)>4
:             THEN
:                LET IDS=IDS+1,GS$[IDS]=LASTFLT$+STR(LASTHC:"##")+CG$
:          FI;
:          LET CG$="",LASTHC=0
0334 LET LASTFLT$=GR05.FLT$,LASTHC=GR05.HC
0335 IF GR05.SCORE=0
:       THEN
:          GOTO 0320;
:          REM "Need lasthc from last week in year even if golfer did not play
0340 IF LEN(CG$)=0 THEN LET CG$=GR05.ID$
0350 LET Z=POS(STR(GR05.DATE)=DATL$,7);
:    IF Z=0
:       THEN
:          LET DATL$=DATL$+STR(GR05.DATE)
0360 LET CG$=CG$+STR(GR05.DATE)+STR(GR05.POINTS:"##.0")
0370 GOTO 0320
0380 CLOSE (GR05S)
0390 IF LEN(CG$)>4
:       THEN
:          LET IDS=IDS+1,GS$[IDS]=LASTFLT$+STR(LASTHC:"##")+CG$,CG$=""
0395 IF IDS=0 THEN LET MSG$="No rounds played"; RUN "GW011"
0400 LET DATL$=SSORT(DATL$,7)
0410 LET WKS=LEN(DATL$)/7,SPAN$="s"+STR(WKS+4)
0420 DIM PTS[1:IDS,1:WKS+5];
:    REM "+1 weeks, +2 total pts, +3 min pts, +4 min wk#, +5 HC
0430 FOR ID=1 TO IDS
0440    LET CG$=GS$[ID],TIMES=0,TOT=0,MINPTS=99
0450    LET PTS[ID,WKS+5]=NUM(CG$(2,2))
0460    FOR J=8 TO LEN(CG$) STEP 11
0470       LET WK=(POS(CG$(J,7)=DATL$,7)-1)/7+1
0480       LET PTS[ID,WK]=NUM(CG$(J+7,4)),TIMES=TIMES+1,TOT=TOT+PTS[ID,WK]
0490       IF PTS[ID,WK]<MINPTS THEN LET MINPTS=PTS[ID,WK],MINWKNO=WK
0500    NEXT J
0510    LET PTS[ID,WKS+1]=TIMES
0520    LET PTS[ID,WKS+2]=TOT
0530    IF TIMES=WKS AND CGI.DROP$="Y"
:          THEN
:             LET PTS[ID,WKS+3]=MINPTS,PTS[ID,WKS+4]=MINWKNO
0540    LET SEQ$=SEQ$+CG$(1,1)+STR(200-(PTS[ID,WKS+2]-PTS[ID,WKS+3]):"###.0")+STR(ID:"##0")
0550 NEXT ID
0560 LET SEQ$=SSORT(SEQ$,9)
0580 CALL "CDW000",ROW$,"hbc"+SPAN$,WASES.LEAGUENAME$
0581 CALL "CDW000",TAB$,"r",ROW$
0588 CALL "CDW000",ROW$,"hbc"+SPAN$,"Point Ranking - "+FNH$(HALF)
0590 CALL "CDW000",TAB$,"r",ROW$
0610 FOR SEQP=1 TO LEN(SEQ$) STEP 9
0620    IF SEQP=1 OR SEQ$(SEQP,1)<>FLT$ THEN GOSUB NEWFLT
0630    LET ID=NUM(SEQ$(SEQP+6,3))
0640    READ RECORD(GR04,KNUM=0,KEY=WASES.LEAGUE$+GS$[ID](4,4))GR04$
0660    CALL "CDW000",ROW$,"d",CVS(GR04.FIRSTNAME$,2)+" "+CVS(GR04.LASTNAME$,2)
0670    CALL "CDW000",ROW$,"dc",STR(PTS[ID,WKS+5])
0675    IF POS(" "<>GR04.EMAIL$)
:          THEN
:             LET TOEMAIL$=TOEMAIL$+","+CVS(GR04.EMAIL$,2),SENT=SENT+1
0680    FOR WK=1 TO WKS
0685       IF WK=PTS[ID,WKS+4] THEN LET BKG$="gff9999" ELSE LET BKG$=""
0690       CALL "CDW000",ROW$,"dc"+BKG$,STR(PTS[ID,WK]:"###.0");
:          REM ,(WK=PTS[ID,WKS+4])
0700    NEXT WK
0710    CALL "CDW000",ROW$,"dc",STR(PTS[ID,WKS+1])
0720    CALL "CDW000",ROW$,"dr",STR(PTS[ID,WKS+2]-PTS[ID,WKS+3]:"###.0")
0725    CALL "CDW000",TAB$,"r",ROW$
0730 NEXT SEQP
0735 CALL "CDW000",HTML$,"t",TAB$
0737 LET HTML$=HTML$+"<br>Week with red shading was excluded from point totals for those who played every week."
0740 CALL "CDS041","CDS084",S084$,"YY"
0741 LET S084.NAME$="MAIL",S084.SUBJECT$=WASES.LEAGUENAME$,S084.FROMNAME$="ScoresAndCharts.com",S084.FROMEMAIL$="webmaster@scoresandcharts.com",S084.TONAME$="Golfers",S084.EMAILBCC$=TOEMAIL$,S084.EMAILTYPE$="H",S084.MODE$="E"
0742 IF 0 THEN LET S084.EMAILBCC$="len@excellware.com"
0743 CALL "CDS084",Y$,Y5$,U0,S084$,Y6,Y6$
0744 PRINT (Y6.CH)HTML$
0750 GOTO EOJ

1000 NEWFLT:
1020 LET FLT$=SEQ$(SEQP,1)
1030 LET TXT$=FLT$+" Flight"
1040 CALL "CDW000",ROW$,"hbc"+SPAN$,TXT$
1045 CALL "CDW000",TAB$,"r",ROW$
1060 CALL "CDW000",ROW$,"hb","Name"
1070 CALL "CDW000",ROW$,"hbc","Handicap"
1080 FOR DATP=1 TO LEN(DATL$) STEP 7
1090    LET TXT$=DATE(NUM(DATL$(DATP,7)):" %M/%Dz")
1100    CALL "CDW000",ROW$,"hbc",TXT$
1110 NEXT DATP
1120 CALL "CDW000",ROW$,"hbc","Weeks"
1130 CALL "CDW000",ROW$,"hbr","Points"
1135 CALL "CDW000",TAB$,"r",ROW$
1140 RETURN
7010 DEF FNH$(HALF)
7020 SWITCH HALF
7030 CASE 1; LET OUT$="1st half "; BREAK
7040 CASE 2; LET OUT$="2nd half "; BREAK
7045 CASE 3; LET OUT$=""; BREAK
7050 SWEND
7060 RETURN OUT$+CGI.YEAR$
7070 FNEND

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9005 LET MSG$=STR(SENT)+" emails sent",HTML$=""
9010 RUN "GW011"
