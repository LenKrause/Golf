0100 rem "GW053 - 09/19/21 Golf Point Ranking - html
0110 setesc 8000; seterr 8000
0130 html$ = "", toemail$ = "", half = num(cgi.period$)
0140 tmp$ = stbl("WMS_CDW000", "D")
0170 call "CDS095", gr03, "XXGR03", "YNY", gr03$
0180 call "CDS095", gr04, "XXGR04", "YNY", gr04$
0200 gr03s = unt
0210 select (gr03s)gr03$ from y.compcode$ + "GR03" where gr03.league$ = wases.league$ and date(gr03.date:"%Yl") = cgi.year$
0220 read record(gr03s, end=0250)gr03$
0230 if half = 3 or gr03.half = half then dates$ = dates$ + gr03.date$
0240 goto 0220
0250 close (gr03s)
0252 dates = len(dates$) / 3
0254 if dates = 0 then msg$ = "No rounds played"; run "GW011"
0260 dates$ = ssort(dates$, 3)
0270 dim dates$[1:dates]
0280 call "CDS095", gr05, "XXGR05", "YNY", gr05$
0290 dim gs$[1:100]
0300 gr05s = unt
0310 select (gr05s)gr05$ from y.compcode$ + "GR05" where gr05.league$ = wases.league$ and pos(gr05.date$ = dates$, 3) sortby gr05.id$ + gr05.date$
0320 read record(gr05s, end=0380)gr05$
0330 if len(cg$) and cg$(1,4) <> gr05.id$ then
:      if len(cg$) > 4 then
:        ids = ids + 1, gs$[ids] = lastflt$ + str(lasthc:"##") + cg$
:      fi;
:      cg$ = "", lasthc = 0
:    fi
0334 lastflt$ = gr05.flt$, lasthc = gr05.hc
0335 if gr05.score = 0 then
:      goto 0320;
:      rem "Need lasthc from last week in year even if golfer did not play
0340 if len(cg$) = 0 then cg$ = gr05.id$
0350 z = pos(str(gr05.date) = datl$, 7);
:    if z = 0 then
:      datl$ = datl$ + str(gr05.date)
:    fi
0360 cg$ = cg$ + str(gr05.date) + str(gr05.points:"##.0")
0370 goto 0320
0380 close (gr05s)
0390 if len(cg$) > 4 then
:      ids = ids + 1, gs$[ids] = lastflt$ + str(lasthc:"##") + cg$, cg$ = ""
:    fi
0395 if ids = 0 then msg$ = "No rounds played"; run "GW011"
0400 datl$ = ssort(datl$, 7)
0410 wks = len(datl$) / 7, span$ = "s" + str(wks + 4)
0420 dim pts[1:ids, 1:wks + 5];
:    rem "+1 weeks, +2 total pts, +3 min pts, +4 min wk#, +5 HC
0430 for id = 1 to ids
0440   cg$ = gs$[id], times = 0, tot = 0, minpts = 99
0450   pts[id, wks + 5] = num(cg$(2,2))
0460   for j = 8 to len(cg$) step 11
0470     wk = (pos(cg$(j, 7) = datl$, 7) - 1) / 7 + 1
0480     pts[id, wk] = num(cg$(j + 7, 4)), times = times + 1, tot = tot + pts[id, wk]
0490     if pts[id, wk] < minpts then minpts = pts[id, wk], minwkno = wk
0500   next j
0510   pts[id, wks + 1] = times
0520   pts[id, wks + 2] = tot
0530   if times = wks and cgi.drop$ = "Y" then
:        pts[id, wks + 3] = minpts, pts[id, wks + 4] = minwkno
:      fi
0540   seq$ = seq$ + cg$(1,1) + str(200 - (pts[id, wks + 2] - pts[id, wks + 3]):"###.0") + str(id:"##0")
0550 next id
0560 seq$ = ssort(seq$, 9)
0580 call "CDW000", row$, "hbc" + span$, wases.leaguename$
0581 call "CDW000", tab$, "r", row$
0588 call "CDW000", row$, "hbc" + span$, "Point Ranking - " + fnh$(half)
0590 call "CDW000", tab$, "r", row$
0610 for seqp = 1 to len(seq$) step 9
0620   if seqp = 1 or seq$(seqp, 1) <> flt$ then gosub newflt
0630   id = num(seq$(seqp + 6, 3))
0640   read record(gr04, knum=0, key=wases.league$ + gs$[id](4,4))gr04$
0660   call "CDW000", row$, "d", cvs(gr04.firstname$, 2) + " " + cvs(gr04.lastname$, 2)
0670   call "CDW000", row$, "dc", str(pts[id, wks + 5])
0675   if pos(" " <> gr04.email$) then
:        toemail$ = toemail$ + "," + cvs(gr04.email$, 2), sent = sent + 1
:      fi
0680   for wk = 1 to wks
0685     if wk = pts[id, wks + 4] then bkg$ = "gff9999" else bkg$ = ""
0690     call "CDW000", row$, "dc" + bkg$, str(pts[id, wk]:"###.0");
:        rem , (wk = pts[id, wks + 4])
0700   next wk
0710   call "CDW000", row$, "dc", str(pts[id, wks + 1])
0720   call "CDW000", row$, "dr", str(pts[id, wks + 2] - pts[id, wks + 3]:"###.0")
0725   call "CDW000", tab$, "r", row$
0730 next seqp
0735 call "CDW000", html$, "t", tab$
0737 html$ = html$ + "<br>Week with red shading was excluded from point totals for those who played every week."
0740 call "CDS041", "CDS084", s084$, "YY"
0741 s084.name$ = "MAIL", s084.subject$ = wases.leaguename$, s084.fromname$ = "ScoresAndCharts.com", s084.fromemail$ = "webmaster@scoresandcharts.com", s084.toname$ = "Golfers", s084.emailbcc$ = toemail$, s084.emailtype$ = "H", s084.mode$ = "E"
0742 if 1 then s084.emailbcc$ = "len@excellware.com", sent = 1
0743 call "CDS084", y$, y5$, u0, s084$, y6, y6$
0744 print (y6.ch)html$
0750 use ::yearLongSkins.bbj::yearLongSkins
0760 declare yearLongSkins sg!
0770 sg! = new yearLongSkins("SAW")
0780 print (y6.ch)"<br>", sg!.getHtml()
0990 goto eoj

1000 newflt:
1020 flt$ = seq$(seqp, 1)
1030 txt$ = flt$ + " Flight"
1040 call "CDW000", row$, "hbc" + span$, txt$
1045 call "CDW000", tab$, "r", row$
1060 call "CDW000", row$, "hb", "Name"
1070 call "CDW000", row$, "hbc", "Handicap"
1080 for datp = 1 to len(datl$) step 7
1090   txt$ = date(num(datl$(datp, 7)):" %M/%Dz")
1100   call "CDW000", row$, "hbc", txt$
1110 next datp
1120 call "CDW000", row$, "hbc", "Weeks"
1130 call "CDW000", row$, "hbr", "Points"
1135 call "CDW000", tab$, "r", row$
1140 return

7010 def fnh$(half)
7020 switch half
7030   case 1; out$ = "1st half "; break
7040   case 2; out$ = "2nd half "; break
7045   case 3; out$ = ""; break
7050 swend
7060 return out$ + cgi.year$
7070 fnend

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9005 msg$ = str(sent) + " emails sent", html$ = ""
9010 run "GW011"
