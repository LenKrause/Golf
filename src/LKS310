0100 REM "LKS310 - 02/16/12 Proportional font analyzer
0110 PRINT 'CS',
0120 BEGIN
0130 CALL "CDS095",ERR=EOJ,SM80,"CDSM80","YPY",SM80$,SM80K$
0140 CALL "CDS276","Option|Typeface|Point size",COL0
0150 LET OPT=3,SM80.STYLE=4148,SM80.POINT=10
0160 REPEAT
0170 CALL "CDS069",1,4,COL0,2,"+option+","#{|,2,|,R,,Y}1Print font width grid|2Print font verification for selected font|3Print font verification for all fonts|4Maintain font width table",OPT
0180 IF CTL=4 THEN GOTO EOJ
0190 LET VER$="<#####"
0200 IF OPT<>4 THEN LET VER$="F"+VER$+"{CDSM80}"
0210 IF OPT<>3 THEN CALL "CDS039",1,0,COL0,3,"+typeface",VER$,SM80$,"STYLE"
0215 IF SM80.STYLE=4116
:       THEN
:          LET STYLE$="1"
:       ELSE
:          LET STYLE$="0";
:          REM "Need italic for Coronet
0220 LET VER$="<###"
0230 IF OPT<>4 THEN LET VER$="F"+VER$+"{CDSM80,-,,,,,,,,,"+SM80.STYLE$+"}"
0240 IF OPT<>3 THEN CALL "CDS039",0,0,COL0,4,"+point size",VER$,SM80$,"POINT"
0250 LET Z$="Y"; CALL "CDS069",1,1,0,0,"Are entries correct (Y/N)+","AYN",0,Z$
0260 IF CTL=4 THEN GOTO EOJ
0270 UNTIL Z$="Y"
0280 IF OPT<>4 THEN CALL "CDS084",ERR=EOJ,"","",0,"N",Y6,Y6$
0290 IF OPT<>4 THEN PRINT (Y6.CH)'BO',Y6.DEPTH85$,'EO',
0295 IF OPT=1 OR OPT=2
:       THEN
:          FOR I=1 TO 95;
:             LET SM80.SIZE[I]=0;
:          NEXT I;
:          READ RECORD(SM80,KNUM=0,KEY=KGEN(SM80$,SM80K$,0),DOM=0296)SM80$
0300 ON OPT-1 GOSUB ONE,ONE,ALL,STORE
0305 CALL "CDS094",Y6$,"E"
0310 PRINT 'CF',
0320 GOTO 0160

0900 ALL:
0910 READ (SM80,KEY="",DOM=0920)
0920 READ RECORD(SM80,END=0990)SM80$
0925 PRINT @(8,23),"Style:",SM80.STYLE," Point:",SM80.POINT,
0930 GOSUB ONE
0940 GOTO 0920
0990 RETURN

1000 ONE:
1005 IF SM80.POINT<=14 THEN LET MULT=1 ELSE LET MULT=2
1010 IF OPT=1 THEN LET LR$="L" ELSE LET LR$="R"
1020 IF LR$="L" THEN LET AT$="0" ELSE LET AT$="3050R"
1030 FOR CHAR=ASC(" ") TO 126
1040    IF MOD(CHAR,32)=0 THEN GOSUB NEWPAGE
1050    LET TXT$=FILL(50/MULT,CHR(CHAR))
1060    IF TXT$(1,1)=" "
:          THEN
:             IF LR$="L"
:                THEN
:                   LET TXT$=TXT$+"<"
:                ELSE
:                   LET TXT$=">"+TXT$
1070    CALL "CDS254",Y6$,"T"+AT$+","+STR(ROW)+",,"+STYLE$+","+STR(SM80.POINT)+",,"+STR(SM80.STYLE),TXT$
1075    IF OPT=1
:          THEN
:             CALL "CDS254",Y6$,"T3060,"+STR(ROW)+",,,10,,4148",CHR(CHAR);
:             CALL "CDS254",Y6$,"T3110,"+STR(ROW)+",,,10,,4148",STR(SM80.SIZE[CHAR-31])
1080    LET ROW=ROW+50
1090 NEXT CHAR
1110 RETURN

1200 NEWPAGE:
1210 CALL "CDS094",Y6$
1220 CALL "CDS254",Y6$,"T1,20,,,10,,4148","Style "+STR(SM80.STYLE)+", "+STR(SM80.POINT)+" point"
1230 FOR J=1 TO 61
1240    LET COL=50*J
1250    CALL "CDS254",Y6$,"V"+STR(COL)+",90,1720"
1260    CALL "CDS254",Y6$,"T"+STR(COL)+"C,80,,,6,,4148",STR(J*MULT)
1265    CALL "CDS254",Y6$,"T"+STR(COL)+"C,1745,,,6,,4148",STR(J*MULT)
1270 NEXT J
1280 LET ROW=150
1290 RETURN

1300 STORE:
1310 EXTRACT RECORD(SM80,KNUM=0,KEY=KGEN(SM80$,SM80K$,0),DOM=1320)SM80$
1320 LET ENTRY=0; GOSUB LOOP
1330 CALL "CDS069",0,0,0,0,"Enter=Edit+"; IF CTL=4 THEN RETURN
1340 LET ENTRY=1; GOSUB LOOP
1350 WRITE RECORD(SM80)SM80$
1355 LET TMP$=STBL("!CLEAR","SMS_CDS254."+STR(SM80.STYLE)+"."+STR(SM80.POINT),ERR=1356)
1360 CALL "CDS069"
1370 RETURN

1400 LOOP:
1410 FOR I=32 TO 126
1420    LET J=I-32,COL=10*(INT(J/16)),ROW=5+MOD(J,16)
1430    IF !(ENTRY) THEN PRINT @(COL,ROW),CHR(I)," ",SM80.SIZE[I-31]:"###",
1440    IF ENTRY THEN CALL "CDS039",1,255,COL+2,ROW,"+dots","##0",SM80$,"SIZE",I-31
1450    IF CTL=3 AND I>32 THEN LET I=I-1; GOTO 1420
1460 NEXT I
1470 RETURN

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 RUN "CDS001"
