0100 REM "GW063 - 06/09/12 Golf Charts
0110 SETESC DT_ERR; SETERR DT_ERR
0120 ENTER Y$,GR04$,HTML$
0130 CALL "CDS095",GR05,"XXGR05","YUY",GR05$
0140 GOSUB CHART
0150 EXIT

1400 CHART:
1410 LET TMP$=STBL("WMS_CDW000_RC",""); REM "Disable odd/even row background
1420 DIM R$:"S:C(2*),STOT:B,SRND:B,SMIN:B,SMAX:B,P:C(2*),PTOT:B,PRND:B,PMIN:B,PMAX:B,H:C(2*),HTOT:B,HRND:B,HMIN:B,HMAX:B"
1430 LET R.SMIN=99,R.PMIN=99,R.HMIN=99
1440 DIM L$:FATTR(R$); LET L$=R$
1450 DIM YY$:FATTR(R$); LET YY$=R$
1460 LET CHXL0$="",CHXL1$="",CHXL2$="",CHXL0Y$="",YSCORES$="",YPUTTS$="",YHC$="",GR05C=UNT,TOTS=0,TOTP=0,TOTH=0,RNDP=0,RNDH=0,MINS=99,MAXS=0,MINP=99,MAXP=0,MINH=99,MAXH=0,CY=0,YTOTS=0,YTOTP=0,YTOTH=0,YRNDS=0,YRNDP=0,YRNDH=0,YS$="",YP$="",YH$="",YRS=0
1470 DIM GR05C$:FATTR(GR05$)
1480 SELECT (GR05C)GR05C$ FROM Y.COMPCODE$+"GR05" WHERE GR05C.LEAGUE$=GR04.LEAGUE$ AND GR05C.ID$=GR04.ID$ AND GR05C.SCORE
1490 READ RECORD(GR05C,END=1570)GR05C$
1500 IF L.SRND<20 THEN GOSUB LATEST
1510 LET TY=NUM(DATE(GR05C.DATE:"%Yl"))
1520 IF TY<>CY AND CY THEN GOSUB NEWYR
1530 LET CY=TY,YTOTS=YTOTS+GR05C.SCORE,YRNDS=YRNDS+1
1540 IF GR05C.PUTTS THEN LET YTOTP=YTOTP+GR05C.PUTTS,YRNDP=YRNDP+1
1550 IF GR05C.HC THEN LET YTOTH=YTOTH+GR05C.HC,YRNDH=YRNDH+1
1560 GOTO 1490
1570 CLOSE (GR05C)
1580 GOSUB NEWYR
1590 LET R$=L$; GOSUB ADJ; LET L$=R$
1600 LET R$=YY$; GOSUB ADJ; LET YY$=R$
1610 IF L.SRND
:       THEN
:          LET AVGV=ROUND(L.STOT/L.SRND,1),TIT$="Scores",MINV=L.SMIN,MAXV=L.SMAX,DELTA=5,VAL$=L.S$,CLR$="3072F3";
:          GOSUB DOCHL
1620 IF YY.SRND
:       THEN
:          LET AVGV=ROUND(YY.STOT/YY.SRND,1),TIT$="Scores",MINV=YY.SMIN,MAXV=YY.SMAX,DELTA=5,VAL$=YY.S$,CLR$="3072F3";
:          GOSUB DOCHY
1630 IF L.SRND OR YY.SRND THEN CALL "CDW000",TAB$,"r style='padding:0px'",ROW$
1640 IF L.PRND
:       THEN
:          LET AVGV=ROUND(L.PTOT/L.PRND,1),TIT$="Putts",MINV=L.PMIN-1,MAXV=L.PMAX+1,DELTA=1,VAL$=L.P$,CLR$="33cc33";
:          GOSUB DOCHL
1650 IF YY.PRND
:       THEN
:          LET AVGV=ROUND(YY.PTOT/YY.PRND,1),TIT$="Putts",MINV=YY.PMIN-1,MAXV=YY.PMAX+1,DELTA=1,VAL$=YY.P$,CLR$="33cc33";
:          GOSUB DOCHY
1660 IF L.PRND OR YY.PRND THEN CALL "CDW000",TAB$,"r style='padding:0px'",ROW$
1670 IF L.HRND
:       THEN
:          LET AVGV=ROUND(L.HTOT/L.HRND,1),TIT$="Handicap",MINV=L.HMIN-1,MAXV=L.HMAX+1,DELTA=1,VAL$=L.H$,CLR$="ff9900";
:          GOSUB DOCHL
1680 IF YY.HRND
:       THEN
:          LET AVGV=ROUND(YY.HTOT/YY.HRND,1),TIT$="Handicap",MINV=YY.HMIN-1,MAXV=YY.HMAX+1,DELTA=1,VAL$=YY.H$,CLR$="ff9900";
:          GOSUB DOCHY
1690 IF L.HRND OR YY.HRND THEN CALL "CDW000",TAB$,"r style='padding:0px'",ROW$
1700 CALL "CDW000",HTML$,"t style='border:0px'",TAB$
1710 RETURN

1800 ADJ:
1810 IF R.SRND THEN LET R.S$="_"+R.S$+",_",R.P$="_"+R.P$+",_",R.H$="_"+R.H$+",_"
1820 LET R.SMIN=R.SMIN-MOD(R.SMIN,5)
1830 LET R.SMAX=R.SMAX-MOD(R.SMAX,5)+5
1840 LET R.PMIN=R.PMIN-MOD(R.PMIN,1)-0
1850 LET R.PMAX=R.PMAX-MOD(R.PMAX,1)+0
1860 LET R.HMIN=R.HMIN-MOD(R.HMIN,1)-0
1870 LET R.HMAX=R.HMAX-MOD(R.HMAX,1)+0
1880 RETURN

1900 LATEST:
1910 LET CHXL0$="|"+DATE(GR05C.DATE:"%Mz")+CHXL0$
1920 LET CHXL1$="|"+DATE(GR05C.DATE:"%Dz")+CHXL1$
1930 LET CHXL2$="|"+DATE(GR05C.DATE:"%Yz")+CHXL2$
1940 LET R$=L$,S=GR05C.SCORE,P=GR05C.PUTTS,H=GR05C.HC; GOSUB TOTS; LET L$=R$
1950 RETURN

2000 TOTS:
2010 LET R.S$=","+STR(S)+R.S$
2020 LET R.STOT=R.STOT+S
2030 LET R.SRND=R.SRND+1
2040 LET R.SMIN=MIN(R.SMIN,S)
2050 LET R.SMAX=MAX(R.SMAX,S)
2060 IF P
:       THEN
:          LET R.P$=","+STR(P)+R.P$;
:          LET R.PTOT=R.PTOT+P;
:          LET R.PRND=R.PRND+1;
:          LET R.PMIN=MIN(R.PMIN,P);
:          LET R.PMAX=MAX(R.PMAX,P)
:       ELSE
:          LET R.P$=",_"+R.P$
2070 IF H
:       THEN
:          LET R.H$=","+STR(H)+R.H$;
:          LET R.HTOT=R.HTOT+H;
:          LET R.HRND=R.HRND+1;
:          LET R.HMIN=MIN(R.HMIN,H);
:          LET R.HMAX=MAX(R.HMAX,H)
:       ELSE
:          LET R.H$=",_"+R.H$
2080 RETURN

2100 NEWYR:
2110 IF YRNDS=0 OR YRNDP=0 OR YRNDH=0 THEN RETURN
2120 LET CHXL0Y$="|"+STR(CY)+CHXL0Y$
2130 LET R$=YY$,S=ROUND(YTOTS/YRNDS,1),P=ROUND(YTOTP/YRNDP,1),H=ROUND(YTOTH/YRNDH,1);
:    GOSUB TOTS;
:    LET YY$=R$
2140 LET YTOTS=0,YRNDS=0,YTOTP=0,YRNDP=0,YTOTH=0,YRNDH=0
2150 LET YRS=YRS+1
2160 RETURN

2200 DOCHL:
2210 LET CH$="<img src='http://chart.apis.google.com/chart"
2220 LET CH$=CH$+"?chf=bg,s,FFFF99"
2230 LET CH$=CH$+"&chxl=0:|"+CHXL0$+"||1:|"+CHXL1$+"||2:|"+CHXL2$+"||4:|Avg+"+STR(AVGV)
2240 LET CH$=CH$+"&chxp=4,"+STR(AVGV)
2250 LET CH$=CH$+"&chxr=3,"+STR(MINV)+","+STR(MAXV)+","+STR(DELTA)+"|4,"+STR(MINV)+","+STR(MAXV)
2260 LET CH$=CH$+"&chxs=3,676767,11.5,0,lt,676767|4,FF0000,11.5,0,lt,FF0000"
2270 LET CH$=CH$+"&chxt=x,x,x,y,r"
2280 LET CH$=CH$+"&chxtc=3,-600|4,-600"
2290 LET CH$=CH$+"&chs=500x250"
2300 LET CH$=CH$+"&cht=lxy"
2310 LET CH$=CH$+"&chco="+CLR$
2320 LET CH$=CH$+"&chds="+STR(MINV)+","+STR(MAXV)+","+STR(MINV)+","+STR(MAXV)
2330 LET CH$=CH$+"&chd=t:-1|"+VAL$
2340 LET CH$=CH$+"&chls=3"
2350 LET CH$=CH$+"&chma=25,60,0,50"
2360 LET CH$=CH$+"&chm=s,"+CLR$+",0,-1,5"
2370 LET CH$=CH$+"&chtt="+TIT$+"+-+Latest+Rounds'"
2380 GOSUB REP
2390 LET CH$=CH$+" style='border:1px solid #c0c0c0' width=500 height=250>"
2400 CALL "CDW000",ROW$,"d style='padding:0px'",CH$
2410 RETURN

2500 DOCHY:
2505 IF YRS<2 THEN RETURN
2510 LET WID=230+(MAX(0,YRS-3))*40
2520 LET CH$="<img src='http://chart.apis.google.com/chart"
2530 LET CH$=CH$+"?chf=bg,s,FFFF99"
2540 LET CH$=CH$+"&chxl=0:|"+CHXL0Y$+"||2:|Avg+"+STR(AVGV)
2550 LET CH$=CH$+"&chxp=2,"+STR(AVGV)
2560 LET CH$=CH$+"&chxr=1,"+STR(MINV)+","+STR(MAXV)+","+STR(DELTA)+"|2,"+STR(MINV)+","+STR(MAXV)
2570 LET CH$=CH$+"&chxs=1,676767,11.5,0,lt,676767|2,FF0000,11.5,0,lt,FF0000"
2580 LET CH$=CH$+"&chxt=x,y,r"
2590 LET CH$=CH$+"&chxtc=1,-600|2,-600"
2600 LET CH$=CH$+"&chs="+STR(WID)+"x250"
2610 LET CH$=CH$+"&cht=lxy"
2620 LET CH$=CH$+"&chco="+CLR$
2630 LET CH$=CH$+"&chds="+STR(MINV)+","+STR(MAXV)+","+STR(MINV)+","+STR(MAXV)
2640 LET CH$=CH$+"&chd=t:-1|"+VAL$
2650 LET CH$=CH$+"&chls=3"
2660 LET CH$=CH$+"&chma=25,60,0,50"
2670 LET CH$=CH$+"&chm=s,"+CLR$+",0,-1,5"
2680 LET CH$=CH$+"&chtt="+TIT$+"+-+Year+by+Year'"
2690 GOSUB REP
2700 LET CH$=CH$+" style='border:1px solid #c0c0c0' width="+STR(WID)+" height=250>"
2710 CALL "CDW000",ROW$,"d style='padding:0px'",CH$
2720 RETURN

2800 REP:
2810 CALL "SW002",CH$,"|","%7c"
2820 IF 0 THEN CALL "SW002",CH$,":","%3a"
2830 RETURN

2900 TEST:
2910 BEGIN
2920 CALL "CDS091",Y$
2930 DIM CGI$:"C:C(2),L:C(3),DATE:N(7)"
2940 LET CGI.C$="LK",CGI.L$="SAW",CGI.DATE=JUL(2011,7,13)
2950 GOTO 0100

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9005 LET HTML$="Job Completed, "+STR(SENT)+" emails sent."
9010 RUN "CDW999"
