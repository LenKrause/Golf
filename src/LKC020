0100 REM "LKC020 - 06/23/98 Copy SMS & FMS files from Unix to PC
0110 BEGIN
0115 SETESC DT_ERR; SETERR DT_ERR
0120 CALL "CDS069",1,1,0,0,"Copy SMS & FMS to PC directory (Y/N)","AYN",0,Z$
0130 IF Z$="N" THEN GOTO EOJ
0135 LET BATFIL$="tmp\update.bat"
0136 OPEN (1,MODE="O_TRUNC,O_CREATE")BATFIL$
0140 LET FROM$="//cdi/CDI/",TO$="C:\CDI\"
0150 LET FLIST$="SMS"; GOSUB XFER
0160 LET FLIST$="FMS"; GOSUB XFER
0165 LET FLIST$="DOS"; GOSUB XFER
0166 CLOSE (1)
0167 LET A=SCALL("COMMAND.COM /C "+BATFIL$)
0168 ERASE BATFIL$
0170 CALL "CDS069",0,0,0,0,"Job completed-"
0180 GOTO EOJ

1000 XFER:
1010 CALL "CDS095",FLIST,FROM$+"FLIST/pkg"+FLIST$,"YNX"
1020 READ (FLIST,END=1090)FIL$
1025 PRINT FIL$
1030 WRITE (1)FNS$("COPY "+FROM$+FIL$+" "+TO$+FIL$+$0D$)
1040 GOTO 1020
1090 CLOSE (FLIST)
1095 RETURN
7000 DEF FNS$(TXT$)
7010 LET H=1
7020 LET Z=POS("/"=TXT$(H))
7030 IF Z THEN LET H=H+Z,TXT$(H-1,1)="\"; GOTO 7020
7040 RETURN TXT$
7050 FNEND

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 RUN "CDS001"
