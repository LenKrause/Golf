0100 rem "LKC100 - 07/26/99 Serial device report
0110 setesc 8000; seterr 8000
0115 if pos(y.sysclass$ = "12") = 0 then
:      call "CDS069", 0, 0, 0, 0, "This program is only functional on IBM Risc System 6000 computers-";
:      goto eoj
:    fi
0120 print @(8,23), "Getting device list...", 
0130 dim dev$:"LOCATION:C(11*=32),CLASS:C(12*=32),NAME:C(12*=32),STATUS:C(12*=)"
0140 devi$ = "location class name status "; call "LKC101", devi$, devflds
0150 devh$ = "location:class:name:status:"; call "LKC101", devh$, devflds
0160 devf$ = "location|class|name|status|";
:    call "LKC101", devf$, devflds;
:    dim dev[devflds]
0169 if y.sysclass$ = "1" then
:      ttyf$ = "autoconfig|altpin|speed|rts|xon|forcedcd|tbc|term|login|ttyprog_action|logmodes|"
:    else
:      ttyf$ = "autoconfig|altpin|speed|flow_disp|forcedcd|term|login|logmodes|"
:    fi
0170 call "LKC101", ttyf$, ttyflds; dim tty[ttyflds]
0179 if y.sysclass$ = "1" then
:      ptrf$ = "autoconfig|altpin|speed|dtr|xon|tbc|line|col|plot|"
:    else
:      ptrf$ = "autoconfig|altpin|speed|flow_disp|line|col|plot|"
:    fi
0180 call "LKC101", ptrf$, ptrflds; dim ptr[ptrflds]
0190 call "CDS064", y$, work, 0, 12, 0, 384, "SKY", filnam$
0200 open (1)"|lsdev -C -s rs232 -Sa -F'location class name status' | sort"
0210 read (1, err=0360)dev$
0220 dev$ = cvs(dev$, 32), devi$ = dev$ + " "
0230 call "LKC102", devh$, devi$, devflds, devf$, devflds + 0, dev[all], recdev$
0240 close (2); open (2)"|lsattr -E -O -l " + dev.name$
0250 read (2)head$
0260 read (2)info$
0270 head$ = head$(2) + ":"
0280 info$ = info$ + ":"
0290 call "LKC101", head$, flds
0300 call "LKC101", info$, flds
0310 if dev.class$ = "tty" then
:      call "LKC102", head$, info$, flds, ttyf$, ttyflds, tty[all], rec$
:    fi
0320 if dev.class$ = "printer" then
:      call "LKC102", head$, info$, flds, ptrf$, ptrflds, ptr[all], rec$
:    fi
0330 write (work, key=dev.location$)recdev$ + rec$
0340 print @(8,23), dev.location$, 'cl', 
0350 goto 0210
0360 tpl$ = ""
0370 call "CDS084", err=eoj, y$, y5$, u0, "N", y6, y6$
0380 call "LKC103", y$, y6$, "Devices", filnam$, devf$, devflds, dev[all], "", ""
0390 call "LKC103", y$, y6$, "tty Devices", filnam$, ttyf$, ttyflds, tty[all], "REC.CLASS$=""tty""", "REC.NAME$(1,3)+STR(NUM(REC.NAME$(4)):""00000"")"
0400 call "LKC103", y$, y6$, "Serial Printers", filnam$, ptrf$, ptrflds, ptr[all], "REC.CLASS$=""printer""", "REC.NAME$(1,2)+STR(NUM(REC.NAME$(3)):""00000"")"
0410 goto eoj

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 if work then call "CDS075", work
9020 run "CDS001"
