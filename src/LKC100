0100 REM "LKC100 - 07/26/99 Serial device report
0110 SETESC DT_ERR; SETERR DT_ERR
0115 IF POS(Y.SYSCLASS$="12")=0
:       THEN
:          CALL "CDS069",0,0,0,0,"This program is only functional on IBM Risc System 6000 computers-";
:          GOTO EOJ
0120 PRINT @(8,23),"Getting device list...",
0130 DIM DEV$:"LOCATION:C(11*=32),CLASS:C(12*=32),NAME:C(12*=32),STATUS:C(12*=)"
0140 LET DEVI$="location class name status "; CALL "LKC101",DEVI$,DEVFLDS
0150 LET DEVH$="location:class:name:status:"; CALL "LKC101",DEVH$,DEVFLDS
0160 LET DEVF$="location|class|name|status|";
:    CALL "LKC101",DEVF$,DEVFLDS;
:    DIM DEV[DEVFLDS]
0169 IF Y.SYSCLASS$="1"
:       THEN
:          LET TTYF$="autoconfig|altpin|speed|rts|xon|forcedcd|tbc|term|login|ttyprog_action|logmodes|"
:       ELSE
:          LET TTYF$="autoconfig|altpin|speed|flow_disp|forcedcd|term|login|logmodes|"
0170 CALL "LKC101",TTYF$,TTYFLDS; DIM TTY[TTYFLDS]
0179 IF Y.SYSCLASS$="1"
:       THEN
:          LET PTRF$="autoconfig|altpin|speed|dtr|xon|tbc|line|col|plot|"
:       ELSE
:          LET PTRF$="autoconfig|altpin|speed|flow_disp|line|col|plot|"
0180 CALL "LKC101",PTRF$,PTRFLDS; DIM PTR[PTRFLDS]
0190 CALL "CDS064",Y$,WORK,0,12,0,384,"SKY",FILNAM$
0200 OPEN (1)"|lsdev -C -s rs232 -Sa -F'location class name status' | sort"
0210 READ (1,ERR=0360)DEV$
0220 LET DEV$=CVS(DEV$,32),DEVI$=DEV$+" "
0230 CALL "LKC102",DEVH$,DEVI$,DEVFLDS,DEVF$,DEVFLDS+0,DEV[ALL],RECDEV$
0240 CLOSE (2); OPEN (2)"|lsattr -E -O -l "+DEV.NAME$
0250 READ (2)HEAD$
0260 READ (2)INFO$
0270 LET HEAD$=HEAD$(2)+":"
0280 LET INFO$=INFO$+":"
0290 CALL "LKC101",HEAD$,FLDS
0300 CALL "LKC101",INFO$,FLDS
0310 IF DEV.CLASS$="tty"
:       THEN
:          CALL "LKC102",HEAD$,INFO$,FLDS,TTYF$,TTYFLDS,TTY[ALL],REC$
0320 IF DEV.CLASS$="printer"
:       THEN
:          CALL "LKC102",HEAD$,INFO$,FLDS,PTRF$,PTRFLDS,PTR[ALL],REC$
0330 WRITE (WORK,KEY=DEV.LOCATION$)RECDEV$+REC$
0340 PRINT @(8,23),DEV.LOCATION$,'CL',
0350 GOTO 0210
0360 LET TPL$=""
0370 CALL "CDS084",ERR=EOJ,Y$,Y5$,U0,"N",Y6,Y6$
0380 CALL "LKC103",Y$,Y6$,"Devices",FILNAM$,DEVF$,DEVFLDS,DEV[ALL],"",""
0390 CALL "LKC103",Y$,Y6$,"tty Devices",FILNAM$,TTYF$,TTYFLDS,TTY[ALL],"REC.CLASS$=""tty""","REC.NAME$(1,3)+STR(NUM(REC.NAME$(4)):""00000"")"
0400 CALL "LKC103",Y$,Y6$,"Serial Printers",FILNAM$,PTRF$,PTRFLDS,PTR[ALL],"REC.CLASS$=""printer""","REC.NAME$(1,2)+STR(NUM(REC.NAME$(3)):""00000"")"
0410 GOTO EOJ

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 IF WORK THEN CALL "CDS075",WORK
9020 RUN "CDS001"
