0100 rem "GW013 - 05/30/17 Dynaweb Play Date Maintenance
0110 setesc 8000; seterr 8000
0120 call "CDS095", gr03, "XXGR03", "YUY", gr03$
0130 weeks = num(field(cgi$, "WEEKS", err=0140)); if weeks then gosub store
0140 wks$ = field(cgi$, "WKS", err=0150); gosub addwks
0150 gosub setup
0160 goto eoj

1000 setup:
1010 if len(msg$) then html$ = "<p class='msg'>" + msg$
1020 call "CDW000", row$, "dcs4", wases.leaguename$
1030 call "CDW000", tab$, "r class='top'", row$
1040 call "CDW000", row$, "dcs4", "Play Date Maintenance"
1050 call "CDW000", tab$, "r class='title'", row$
1060 year$ = "", year$ = field(cgi$, "YEAR", err=1070)
1070 on len(year$) = 4 gosub yearinput, anydates
1080 call "CDW000", row$, "dcs4", "[HOME] <input type='submit' value='Next'>"
1090 call "CDW000", tab$, "r class='foot'", row$
1100 call "CDW000", html$, "tc", "<form method='post' action='[DSN]?p=GW013'>" + tab$ + "</form>"
1110 if datescript then gosub datescript
1120 return

1200 yearinput:
1210 year = num(date(0:"%Yl")), year$ = "<select name='year'>" + $0a$
1220 year$ = fno$(year)
1230 if num(date(0:"%M")) > 9 then year$ = fno$(year + 1)
1240 year = year - 1
1250 read (gr03, knum=0, key=wases.league$ + bin(jul(year, 12, 31), 3), dir = 0, dom=1260)
1260 read record(gr03, end=1310)gr03$
1270 if gr03.league$ <> wases.league$ then goto 1310
1280 y = num(date(gr03.date:"%Yl"))
1290 if y <= year then year$ = fno$(y), year = y - 1
1300 goto 1260
1310 year$ = year$ + "</select>"
1320 call "CDW000", row$, "dr", "Year"
1330 call "CDW000", row$, "ds3", year$
1340 call "CDW000", tab$, "r", row$
1350 return

1400 anydates:
1410 weeks = 0
1420 read (gr03, knum=1, key=wases.league$ + bin(jul(num(year$), 1, 1), 3), dir = 0, dom=1430)
1430 k$ = key(gr03, end=1580)
1435 if k$(1,3) <> wases.league$ then goto 1580
1440 read record(gr03)gr03$
1450 if date(gr03.date:"%Yl") <> year$ then goto 1580
1460 weeks = weeks + 1
1470 if weeks = 1 then
:      call "CDW000", row$, "dc", "Date";
:      call "CDW000", row$, "dc", "Nine";
:      call "CDW000", row$, "dc", "Half";
:      call "CDW000", row$, "dc", "Scores";
:      call "CDW000", tab$, "r class='foot'", row$
:    fi
1480 call "CDW000", row$, "dc", date(gr03.date:"%Ds %Mz/%Dz/%Yz") + "<input type='hidden' name='DATE" + str(weeks) + "' value='" + str(gr03.date) + "'>"
1490 if gr03.nine$ = "F" then
:      front$ = "Checked", back$ = ""
:    else
:      front$ = "", back$ = "Checked"
:    fi
1500 call "CDW000", row$, "dc", "<input type='radio' name='NINE" + str(weeks) + "' value='F' " + front$ + ">Front &nbsp;&nbsp;<input type='radio' name='NINE" + str(weeks) + "' value='B' " + back$ + ">Back"
1505 first$ = "", second$ = "", tourn$ = "", rain$ = ""
1510 if gr03.half = 1 then
:      first$ = "Checked"
:    else
:      if gr03.half = 2 then
:        second$ = "Checked"
:      else
:        if gr03.half = 0 then
:          tourn$ = "Checked"
:        else
:          rain$ = "Checked"
:        fi
:      fi
:    fi
1520 call "CDW000", row$, "dc", "<input type='radio' name='HALF" + str(weeks) + "' value='1' " + first$ + ">First &nbsp;&nbsp;<input type='radio' name='HALF" + str(weeks) + "' value='2' " + second$ + ">Second &nbsp;&nbsp;<input type='radio' name ='HALF" + str(weeks) + "' value='0' " + tourn$ + ">Tournament &nbsp;&nbsp;<input type='radio' name ='HALF" + str(weeks) + "' value='3' " + rain$ + ">Rain Out"
1530 call "SW404", y$, "XXGR05", gr05$, "GR05.LEAGUE$=""" + gr03.league$ + """ AND GR05.DATE$=$" + hta(gr03.date$) + "$ AND GR05.SCORE", scores;
:    rem call "SW004", "XXGR05", 1, gr03.league$ + gr03.date$, scores
1540 if scores then
:      scores$ = str(scores)
:    else
:      scores$ = "<input type='checkbox' name='DEL" + str(weeks) + "' value='D'>Delete"
:    fi
1550 call "CDW000", row$, "dc", scores$
1560 call "CDW000", tab$, "r", row$
1570 goto 1430
1580 on weeks gosub newyear, newweek
1590 tab$ = tab$ + "<input type='hidden' name='weeks' value='" + str(weeks) + "'>"
1600 return

1700 newweek:
1710 call "CDS265", s265$
1720 weeks = weeks + 1, exc$ = "", dow$ = date(gr03.date:"%Ds")
1730 for i = 0 to 6
1740   if s265.days$[i + 1] <> dow$ then exc$ = exc$ + "," + str(i)
1750 next i
1760 call "CDW000", row$, "dc", fnd$("DATE" + str(weeks), "", exc$(2))
1770 call "CDW000", row$, "dc", "<input type='radio' name='NINE" + str(weeks) + "' value='F'>Front &nbsp;&nbsp;<input type='radio' name='NINE" + str(weeks) + "' value='B'>Back"
1780 call "CDW000", row$, "dc", "<input type='radio' name='HALF" + str(weeks) + "' value='1'>First &nbsp;&nbsp;<input type='radio' name='HALF" + str(weeks) + "' value='2'>Second &nbsp;&nbsp;<input type ='radio' name='HALF" + str(weeks) + "' value='0'>Tournament"
1790 call "CDW000", row$, "d", ""
1800 call "CDW000", tab$, "r", row$
1810 return

1900 newyear:
1910 call "CDW000", row$, "dr", "Year"
1920 call "CDW000", row$, "ds3", year$
1930 call "CDW000", tab$, "r", row$
1940 call "CDW000", row$, "dr", "Day of Week"
1950 call "CDS265", s265$
1960 dow = 0, dow = num(field(cgi$, "DOW", err=1970))
1970 if dow then
:      dow$ = s265.dayl$[dow] + "<input type='hidden' name='DOW' value='" + str(dow) + "'>"
:    else
:      gosub dowinput
:    fi
1980 dow$ = dow$ + "<input type='hidden' name='YEAR' value='" + year$ + "'>"
1990 call "CDW000", row$, "d", dow$
2000 call "CDW000", tab$, "r", row$
2010 if dow then gosub firstplay
2020 return

2100 dowinput:
2110 dim dow$[1:7]
2120 read (gr03, knum=0, key=wases.league$ + $ff$, dom=2130)
2130 read record(gr03, end=2150)gr03$
2140 if gr03.league$ = wases.league$ then
:      ldow$ = date(gr03.date:"%Ds");
:      i = 1;
:      while ldow$ <> s265.days$[i];
:        i = i + 1;
:      wend;
:      dow$[i] = " checked"
:    fi
2150 for i = 1 to 7
2160   dow$ = dow$ + "<br><input type='radio' name='DOW' value='" + str(i) + "'" + dow$[i] + ">" + s265.dayl$[i] + $0a$
2170 next i
2180 return

2200 firstplay:
2210 call "CDW000", row$, "dr", "First Date of Play"
2220 exc$ = ""
2230 for i = 0 to 6
2240   if i + 1 <> dow then exc$ = exc$ + "," + str(i)
2250 next i
2260 call "CDW000", row$, "d", fnd$("FIRSTPLAY", "", exc$(2))
2270 call "CDW000", tab$, "r", row$
2280 call "CDW000", row$, "dr", "Number of Weeks"
2290 call "CDW000", row$, "d", "<input type='text' name='WKS' value='20' size='2' maxlength='2'>"
2300 call "CDW000", tab$, "r", row$
2310 call "CDW000", row$, "dr", "Nine"
2320 call "CDW000", row$, "d", "<input type='radio' name='NINE' value='F'>Front<br><input type='radio' name='NINE' value='B'>Back"
2330 call "CDW000", tab$, "r", row$
2340 return

2400 addwks:
2410 wks = 0, wks = num(wks$, err=2420)
2420 if wks > 52 or fpt(wks) or wks < 2 then
:      msg$ = "Invalid number of weeks.";
:      goto msg
:    fi
2430 fpd$ = "", fpd$ = field(cgi$, "FIRSTPLAY", err=2440)
2440 if len(fpd$) = 0 then msg$ = "Specify First Play Date."; goto msg
2450 call "CDW055", fpd$, dat$[all]
2455 if len(dat$[2]) = 0 then msg$ = "Invalid First Play Date."; goto msg
2460 if date(num(dat$[2]):"%Yl") <> cgi.year$ then
:      msg$ = "First Play Date is not in " + cgi.year$ + ".";
:      goto msg
:    fi
2461 dow = num(cgi.dow$, err=2462)
2462 dows$ = "SunMonTueWedThuFriSat"
2463 dow$ = date(num(dat$[2]):"%Ds")
2464 if dow$ <> dows$(3 * (dow - 1) + 1,3) then
:      msg$ = "First Play Date is a " + dow$ + ".";
:      goto msg
:    fi
2465 nine$ = "", nine$ = field(cgi$, "NINE", err=2466)
2466 if pos(nine$ = "FB") = 0 then
:      msg$ = "Please indicate if you will be playing the Front or Back nine in the first week";
:      goto msg
:    fi
2470 half = 1, date = num(dat$[2])
2480 for wk = 1 to wks
2490   dim gr03$:fattr(gr03$)
2500   gr03.league$ = wases.league$
2510   gr03.date = date
2520   gr03.nine$ = cgi.nine$
2530   gr03.half = half
2540   write record(gr03)gr03$
2550   if wk >= wks / 2 then half = 2
2560   if cgi.nine$ = "F" then cgi.nine$ = "B" else cgi.nine$ = "F"
2570   date = date + 7
2580 next wk
2590 return

2600 store:
2610 for week = 1 to weeks
2620   wk$ = str(week)
2630   dim gr03$:fattr(gr03$)
2640   gr03.league$ = wases.league$
2650   date$ = field(cgi$, "DATE" + wk$)
2655   if len(date$) = 0 then continue; rem "Row for new date not added
2660   if len(date$) = 8 then call "CDS055", date$, dat$[all]; date$ = dat$[2]
2670   gr03.date = num(date$)
2680   del$ = "", del$ = field(cgi$, "DEL" + wk$, err=2690)
2690   if del$ = "D" then
:        call "CDS273", y$, "XXGR05", 1, gr03.league$ + gr03.date$;
:        call "CDS273", y$, "XXGR03", 0, gr03.league$ + gr03.date$;
:        continue
:      fi
2700   gr03.nine$ = "", gr03.nine$ = field(cgi$, "NINE" + wk$, err=2710)
2710   if pos(gr03.nine$ = "FB") = 0 then
:        msg$ = "Specify Nine for " + date(gr03.date) + ".";
:        goto msg
:      fi
2720   gr03.half$ = "", gr03.half$ = field(cgi$, "HALF" + wk$, err=2730)
2730   if pos(gr03.half$ = "1203") = 0 then
:        msg$ = "Specify Half for " + date(gr03.date) + ".";
:        goto msg
:      fi
2740   write record(gr03)gr03$
2750 next week
2760 return

2800 datescript:
2810 head$ = "", head$ = stbl("$otherhead", err=2820)
2820 head$ = fnt$("<script type='text/JavaScript' src='/scripts/datepicker/jquery.js'></script>")
2830 head$ = fnt$("<script type='text/JavaScript' src='/scripts/datepicker/zebra_datepicker.js'></script>")
2840 head$ = fnt$("<link rel='stylesheet' href='/scripts/datepicker/zebra_datepicker.css' type='text/css'>")
2850 tmp$ = stbl("$otherhead", head$)
2860 return

2900 functions:
2910 def fno$(yr) = year$ + "<option value='" + str(yr) + "'>" + str(yr) + "</option>" + $0a$
2920 def fnd$(fldnam$, dopts$, disabled$)
2930 datescript = 1
2940 return "<input class='datepicker' type='text' size='8' id='" + fldnam$ + "' name='" + fldnam$ + "'><script>$(document).ready(function() {$('#" + fldnam$ + "').Zebra_DatePicker({" + dopts$ + "inside:false,disabled_dates:['* * * " + disabled$ + "'],format:'m/d/y',first_day_of_week:0});});</script>"
2950 fnend

2960 def fnt$(tx$) = head$ + tx$ + $0a$
2970 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

8100 msg:
8110 html$ = "<script>alert('" + msg$ + "');window.history.back(-1)</script>"
8120 goto eoj

9000 eoj:
9010 if tcb(13) then exit
9020 run "CDW999"
