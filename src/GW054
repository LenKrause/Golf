0100 REM "GW054 - 09/13/14 Weekly Golf Point Ranking
0110 SETESC DT_ERR; SETERR DT_ERR
0120 ENTER Y$,WASES$,JDATE,FLTS$,HTML$[ALL]
0130 LET HTML$="",FLTS$=""
0140 DIM HTML$[1:4]
0150 CALL "CDS095",GR03,"XXGR03","YNY",GR03$
0160 READ RECORD(GR03,KNUM=0,KEY=WASES.LEAGUE$+BIN(JDATE-7,3),DOM=EOJ)GR03$;
:    REM "Need to know what half the last round this year was played in
0170 LET HALF=GR03.HALF,JDATE=GR03.DATE
0175 IF HALF
:       THEN
:          LET SDATE=JUL(NUM(DATE(JDATE:"%Yl")),1,1)
:       ELSE
:          LET SDATE=JDATE-7
0180 CALL "CDS095",GR04,"XXGR04","YNY",GR04$
0190 LET GR03S=UNT
0200 SELECT (GR03S)GR03$ FROM Y.COMPCODE$+"GR03" WHERE GR03.LEAGUE$=WASES.LEAGUE$ AND GR03.DATE>=SDATE AND GR03.DATE<=JDATE AND GR03.HALF=HALF
0210 READ RECORD(GR03S,END=0240)GR03$
0220 LET DATES$=DATES$+GR03.DATE$
0230 GOTO 0210
0240 CLOSE (GR03S)
0245 IF LEN(DATES$)=0 THEN EXIT
0246 LET DATES$=SSORT(DATES$,3)
0250 CALL "CDS095",GR05,"XXGR05","YNY",GR05$
0260 IF HALF
:       THEN
:          DIM G0$:"ID:C(4),FLT:C(1),POINTS:N(5),ROUNDS:N(2),RANK:N(2),TIE:C(1)",G0A$:FATTR(G0$)
:       ELSE
:          DIM G0$:"ID:C(4),FLT:C(1),ROUNDS:N(1),SORT:N(3),RANK:N(2),TIE:C(1),GROSS[3]:N(3),HC[3]:N(2),NET[3]:N(3)",G0A$:FATTR(G0$)
0270 LET GR05S=UNT
0280 SELECT (GR05S)GR05$ FROM Y.COMPCODE$+"GR05" WHERE GR05.LEAGUE$=WASES.LEAGUE$ AND POS(GR05.DATE$=DATES$,3) AND GR05.SCORE AND GR05.FLT$<>" "
0290 READ RECORD(GR05S,END=0360)GR05$
0300 LET Z=POS(GR05.ID$=G$,LEN(G0$))
0310 IF Z
:       THEN
:          LET G0$=G$(Z,LEN(G0$))
:       ELSE
:          DIM G0$:FATTR(G0$);
:          LET Z=LEN(G$)+1,G0.ID$=GR05.ID$,G0.FLT$=GR05.FLT$,G$=G$+G0$
0320 IF HALF
:       THEN
:          LET G0.ROUNDS=G0.ROUNDS+1;
:          LET G0.POINTS$=STR(G0.POINTS+GR05.POINTS:"###.0")
:       ELSE
:          GOSUB TOURNEY
0340 LET G$(Z,LEN(G0$))=G0$
0350 GOTO 0290
0360 CLOSE (GR05S)
0365 IF LEN(G$)=0 THEN EXIT
0370 IF HALF
:       THEN
:          LET SORTKEY$=$0000$+BIN(4,2)+CHR(1)+$00$+$0000$+$0000$+BIN(5,2)+CHR(5)+$01$+$0000$+$FF$
:       ELSE
:          LET SORTKEY$=$0000$+BIN(4,2)+CHR(1)+$00$+$0000$+$0000$+BIN(5,2)+CHR(1)+$01$+$0000$+$0000$+BIN(6,2)+CHR(3)+$00$+$0000$+$FF$
0380 LET G$=SSORT(G$,LEN(G0$),SORTKEY$,0)
0390 LET LASTFLT$=""
0400 FOR I=1 TO LEN(G$) STEP LEN(G0$)
0410    LET G0$=G$(I,LEN(G0$))
0420    IF G0.FLT$<>LASTFLT$ THEN LET LASTFLT$=G0.FLT$,LASTSRT=-1,RANK=0,TIE=0
0425    IF HALF THEN LET SRT=G0.POINTS ELSE LET SRT=G0.SORT
0430    IF SRT<>LASTSRT
:          THEN
:             LET RANK=RANK+TIE+1,TIE=0
:          ELSE
:             LET TIE=TIE+1,G0.TIE$="T";
:             IF RANK>0
:                THEN
:                   LET G0A$=G$(I-LEN(G0$),LEN(G0$)),G0A.TIE$="T",G$(I-LEN(G0$),LEN(G0$))=G0A$
0440    LET G0.RANK=RANK,LASTSRT=SRT
0450    LET G$(I,LEN(G0$))=G0$
0460 NEXT I
0470 LET LASTFLT$=""
0480 FOR I=1 TO LEN(G$) STEP LEN(G0$)
0490    LET G0$=G$(I,LEN(G0$))
0500    IF G0.FLT$<>LASTFLT$
:          THEN
:             IF LEN(LASTFLT$)
:                THEN
:                   GOSUB FINISHFLT
:             FI;
:             GOSUB NEWFLT
0510    CALL "CDW000",ROW$,"dc",CVS(G0.RANK$,3)+CVS(G0.TIE$,1)
0520    DIM GR04$:FATTR(GR04$)
0530    READ RECORD(GR04,KNUM=0,KEY=WASES.LEAGUE$+G0.ID$,DOM=0540)GR04$
0540    CALL "CDW000",ROW$,"d",CVS(GR04.FIRSTNAME$,3)+" "+CVS(GR04.LASTNAME$,3)
0550    IF HALF
:          THEN
:             CALL "CDW000",ROW$,"dc",G0.POINTS$;
:             CALL "CDW000",ROW$,"dc",STR(G0.ROUNDS)
:          ELSE
:             GOSUB TOURNEY_HTML
0570    CALL "CDW000",TAB$,"r",ROW$
0580 NEXT I
0590 GOSUB FINISHFLT
0600 EXIT

1000 NEWFLT:
1010 LET HTML$="",TAB$="",ROW$="",FLTS$=FLTS$+G0.FLT$,LASTFLT$=G0.FLT$
1012 IF HALF
:       THEN
:          LET H$="Point",SPAN$="s4"
:       ELSE
:          LET H$="Tournament";
:          IF LEN(DATES$)=3
:             THEN
:                LET SPAN$="s5"
:             ELSE
:                LET SPAN$="s11"
1015 LET TXT$=H$+" Ranking"
1016 IF LEN(FLTS$)>1 THEN LET TXT$=TXT$+" - "+G0.FLT$+" Flight"
1017 IF Y.PASSPARM$="E"
:       THEN
:          LET TYPE$="h"
:       ELSE
:          LET TYPE$="d";
:          REM "Use th for email for style
1020 CALL "CDW000",ROW$,TYPE$+"c"+SPAN$,TXT$
1030 CALL "CDW000",TAB$,"r class='title'",ROW$
1035 IF HALF=0 THEN LET RS$=" rowspan=2"
1040 CALL "CDW000",ROW$,TYPE$+"c"+RS$,"Rank"
1050 CALL "CDW000",ROW$,TYPE$+RS$,"Name"
1060 IF HALF
:       THEN
:          CALL "CDW000",ROW$,TYPE$+"c","Points";
:          CALL "CDW000",ROW$,TYPE$+"c","Rounds Played"
:       ELSE
:          GOSUB TOURNEY_HEAD
1080 CALL "CDW000",TAB$,"r class='head'",ROW$
1090 RETURN

1100 FINISHFLT:
1110 CALL "CDW000",HTML$,"t",TAB$
1120 LET HTML$[LEN(FLTS$)]=HTML$
1130 RETURN

1200 TEST:
1210 BEGIN
1220 CALL "CDS091",Y$
1230 CALL PGM(-1),Y$,"SAW",JUL(2013,9,4),FLTS$,HTML$[ALL]
1240 IF LEN(FLTS$) THEN GOSUB TESTH
1250 GOTO 9996

1300 TESTH:
1310 FOR I=1 TO LEN(FLTS$)
1320    CALL "CDW000",HTML$,"t",HTML$[I]
1330 NEXT I
1340 CALL "CDW999",HTML$
1350 RETURN

1400 TOURNEY:
1410 LET IND=2-(GR05.DATE$=DATES$(1,3))
1420 LET G0.GROSS[IND]=GR05.SCORE,G0.GROSS[3]=G0.GROSS[3]+GR05.SCORE
1430 LET G0.HC[IND]=GR05.HC,G0.HC[3]=G0.HC[3]+GR05.HC
1440 LET G0.NET[IND]=GR05.NET,G0.NET[3]=G0.NET[3]+GR05.NET
1450 LET G0.SORT$=STR(G0.NET[3]:"000")
1460 LET G0.ROUNDS=G0.ROUNDS+1
1470 RETURN

1500 TOURNEY_HTML:
1505 FOR IND=1 TO 1+2*(LEN(DATES$)>3)
1509    IF G0.GROSS[IND]
:          THEN
:             LET GR$=STR(G0.GROSS[IND]),HC$=STR(G0.HC[IND]),NT$=STR(G0.NET[IND])
:          ELSE
:             LET GR$="",HC$="",NT$=""
1510    CALL "CDW000",ROW$,"dc",GR$
1520    CALL "CDW000",ROW$,"dc",HC$
1530    CALL "CDW000",ROW$,"dc",NT$
1540 NEXT IND
1590 RETURN

1600 TOURNEY_HEAD:
1602 CALL "CDW000",ROW$,"dcs3","Week 1"
1603 IF LEN(DATES$)>3
:       THEN
:          CALL "CDW000",ROW$,"dcs3","Week 2";
:          CALL "CDW000",ROW$,"dcs3","Total"
1604 CALL "CDW000",TAB$,"r class='head'",ROW$
1605 FOR IND=1 TO 1+2*(LEN(DATES$)>3)
1610    CALL "CDW000",ROW$,"dc","Gross"
1620    CALL "CDW000",ROW$,"dc","HC"
1630    CALL "CDW000",ROW$,"dc","Net"
1640 NEXT IND
1690 RETURN

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 EXIT
