0100 rem "GW054 - 09/13/14 Weekly Golf Point Ranking
0110 setesc 8000; seterr 8000
0120 enter y$, wases$, jdate, flts$, html$[all]
0130 html$ = "", flts$ = ""
0140 dim html$[1:4]
0150 call "CDS095", gr03, "XXGR03", "YNY", gr03$
0160 read record(gr03, knum=0, key=wases.league$ + bin(jdate - 7, 3), dom=eoj)gr03$;
:    rem "Need to know what half the last round this year was played in
0170 half = gr03.half, jdate = gr03.date
0175 if half then
:      sdate = jul(num(date(jdate:"%Yl")), 1, 1)
:    else
:      sdate = jdate - 7
:    fi
0180 call "CDS095", gr04, "XXGR04", "YNY", gr04$
0190 gr03s = unt
0200 select (gr03s)gr03$ from y.compcode$ + "GR03" where gr03.league$ = wases.league$ and gr03.date >= sdate and gr03.date <= jdate and gr03.half = half
0210 read record(gr03s, end=0240)gr03$
0220 dates$ = dates$ + gr03.date$
0230 goto 0210
0240 close (gr03s)
0245 if len(dates$) = 0 then exit
0246 dates$ = ssort(dates$, 3)
0250 call "CDS095", gr05, "XXGR05", "YNY", gr05$
0260 if half then
:      dim g0$:"ID:C(4),FLT:C(1),POINTS:N(5),ROUNDS:N(2),RANK:N(2),TIE:C(1)", g0a$:fattr(g0$)
:    else
:      dim g0$:"ID:C(4),FLT:C(1),ROUNDS:N(1),SORT:N(3),RANK:N(2),TIE:C(1),GROSS[3]:N(3),HC[3]:N(2),NET[3]:N(3)", g0a$:fattr(g0$)
:    fi
0270 gr05s = unt
0280 select (gr05s)gr05$ from y.compcode$ + "GR05" where gr05.league$ = wases.league$ and pos(gr05.date$ = dates$, 3) and gr05.score and gr05.flt$ <> " "
0290 read record(gr05s, end=0360)gr05$
0300 z = pos(gr05.id$ = g$, len(g0$))
0310 if z then
:      g0$ = g$(z, len(g0$))
:    else
:      dim g0$:fattr(g0$);
:      z = len(g$) + 1, g0.id$ = gr05.id$, g0.flt$ = gr05.flt$, g$ = g$ + g0$
:    fi
0320 if half then
:      g0.rounds = g0.rounds + 1;
:      g0.points$ = str(g0.points + gr05.points:"###.0")
:    else
:      gosub tourney
:    fi
0340 g$(z, len(g0$)) = g0$
0350 goto 0290
0360 close (gr05s)
0365 if len(g$) = 0 then exit
0370 if half then
:      sortkey$ = $0000$ + bin(4,2) + chr(1) + $00$ + $0000$ + $0000$ + bin(5,2) + chr(5) + $01$ + $0000$ + $ff$
:    else
:      sortkey$ = $0000$ + bin(4,2) + chr(1) + $00$ + $0000$ + $0000$ + bin(5,2) + chr(1) + $01$ + $0000$ + $0000$ + bin(6,2) + chr(3) + $00$ + $0000$ + $ff$
:    fi
0380 g$ = ssort(g$, len(g0$), sortkey$, 0)
0390 lastflt$ = ""
0400 for i = 1 to len(g$) step len(g0$)
0410   g0$ = g$(i, len(g0$))
0420   if g0.flt$ <> lastflt$ then lastflt$ = g0.flt$, lastsrt = -1, rank = 0, tie = 0
0425   if half then srt = g0.points else srt = g0.sort
0430   if srt <> lastsrt then
:        rank = rank + tie + 1, tie = 0
:      else
:        tie = tie + 1, g0.tie$ = "T";
:        if rank > 0 then
:          g0a$ = g$(i - len(g0$), len(g0$)), g0a.tie$ = "T", g$(i - len(g0$), len(g0$)) = g0a$
:        fi
:      fi
0440   g0.rank = rank, lastsrt = srt
0450   g$(i, len(g0$)) = g0$
0460 next i
0470 lastflt$ = ""
0480 for i = 1 to len(g$) step len(g0$)
0490   g0$ = g$(i, len(g0$))
0500   if g0.flt$ <> lastflt$ then
:        if len(lastflt$) then
:          gosub finishflt
:        fi;
:        gosub newflt
:      fi
0510   call "CDW000", row$, "dc", cvs(g0.rank$, 3) + cvs(g0.tie$, 1)
0520   dim gr04$:fattr(gr04$)
0530   read record(gr04, knum=0, key=wases.league$ + g0.id$, dom=0540)gr04$
0540   call "CDW000", row$, "d", cvs(gr04.firstname$, 3) + " " + cvs(gr04.lastname$, 3)
0550   if half then
:        call "CDW000", row$, "dc", g0.points$;
:        call "CDW000", row$, "dc", str(g0.rounds)
:      else
:        gosub tourney_html
:      fi
0570   call "CDW000", tab$, "r", row$
0580 next i
0590 gosub finishflt
0600 exit

1000 newflt:
1010 html$ = "", tab$ = "", row$ = "", flts$ = flts$ + g0.flt$, lastflt$ = g0.flt$
1012 if half then
:      h$ = "Point", span$ = "s4"
:    else
:      h$ = "Tournament";
:      if len(dates$) = 3 then
:        span$ = "s5"
:      else
:        span$ = "s11"
:      fi
:    fi
1015 txt$ = h$ + " Ranking"
1016 if len(flts$) > 1 then txt$ = txt$ + " - " + g0.flt$ + " Flight"
1017 if y.passparm$ = "E" then
:      type$ = "h"
:    else
:      type$ = "d";
:      rem "Use th for email for style
1020 call "CDW000", row$, type$ + "c" + span$, txt$
1030 call "CDW000", tab$, "r class='title'", row$
1035 if half = 0 then rs$ = " rowspan=2"
1040 call "CDW000", row$, type$ + "c" + rs$, "Rank"
1050 call "CDW000", row$, type$ + rs$, "Name"
1060 if half then
:      call "CDW000", row$, type$ + "c", "Points";
:      call "CDW000", row$, type$ + "c", "Rounds Played"
:    else
:      gosub tourney_head
:    fi
1080 call "CDW000", tab$, "r class='head'", row$
1090 return

1100 finishflt:
1110 call "CDW000", html$, "t", tab$
1120 html$[len(flts$)] = html$
1130 return

1200 test:
1210 begin
1220 call "CDS091", y$
1230 call pgm(-1), y$, "SAW", jul(2013,9,4), flts$, html$[all]
1240 if len(flts$) then gosub testh
1250 goto 9996

1300 testh:
1310 for i = 1 to len(flts$)
1320   call "CDW000", html$, "t", html$[i]
1330 next i
1340 call "CDW999", html$
1350 return

1400 tourney:
1410 ind=2 - (gr05.date$ = dates$(1,3))
1420 g0.gross[ind] = gr05.score, g0.gross[3] = g0.gross[3] + gr05.score
1430 g0.hc[ind] = gr05.hc, g0.hc[3] = g0.hc[3] + gr05.hc
1440 g0.net[ind] = gr05.net, g0.net[3] = g0.net[3] + gr05.net
1450 g0.sort$ = str(g0.net[3]:"000")
1460 g0.rounds = g0.rounds + 1
1470 return

1500 tourney_html:
1505 for ind=1 to 1 + 2 * (len(dates$) > 3)
1509   if g0.gross[ind] then
:        gr$ = str(g0.gross[ind]), hc$ = str(g0.hc[ind]), nt$ = str(g0.net[ind])
:      else
:        gr$ = "", hc$ = "", nt$ = ""
:      fi
1510   call "CDW000", row$, "dc", gr$
1520   call "CDW000", row$, "dc", hc$
1530   call "CDW000", row$, "dc", nt$
1540 next ind
1590 return

1600 tourney_head:
1602 call "CDW000", row$, "dcs3", "Week 1"
1603 if len(dates$) > 3 then
:      call "CDW000", row$, "dcs3", "Week 2";
:      call "CDW000", row$, "dcs3", "Total"
:    fi
1604 call "CDW000", tab$, "r class='head'", row$
1605 for ind=1 to 1 + 2 * (len(dates$) > 3)
1610   call "CDW000", row$, "dc", "Gross"
1620   call "CDW000", row$, "dc", "HC"
1630   call "CDW000", row$, "dc", "Net"
1640 next ind
1690 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 exit
