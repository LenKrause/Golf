0100 rem "GW050 - 07/13/14 Golf Select Year and Half for Point Ranking
0110 setesc 8000; seterr 8000
0120 call "CDS095", gr03, "XXGR03", "YNY", gr03$
0130 call "CDW000", row$, "ds3", wases.leaguename$
0140 call "CDW000", tab$, "r class='top'", row$
0150 call "CDW000", row$, "dcs3", "Point Ranking"
0160 call "CDW000", tab$, "r class='title'", row$
0170 read (gr03, knum=0, key=wases.league$ + $ff$, dom=0180)
0180 read record(gr03, end=0230)gr03$
0190 if gr03.league$ <> wases.league$ then goto 0230
0200 yr$ = date(gr03.date:"%Yl")
0210 if pos(yr$ = yrs$, 4) = 0 then
:      call "SW404", y$, "XXGR05", gr05$, "GR05.LEAGUE$=""" + wases.league$ + """ AND GR05.DATE=" + str(gr03.date) + " AND GR05.SCORE", recs, 1;
:      if recs then
:        yrs$ = yrs$ + yr$
:      fi
:    fi
0220 goto 0180
0230 if len(yrs$) then
:      gosub process
:    else
:      msg$ = "No Data Available";
:      run "GW011"
:    fi
0235 html$ = "<form method='post' action='[DSN]?p=GW051'>"
0240 call "CDW000", row$, "dcs3", "<input type='submit' value='Submit'>[HOME]"
0250 call "CDW000", tab$, "r class='foot'", row$
0260 call "CDW000", html$, "tc", tab$
0270 goto eoj

1000 process:
1010 call "CDW000", row$, "dr", "Period"
1020 call "CDW000", row$, "d", "<input type='radio' name='period' value='1' checked>1st half<br><input type='radio' name='period' value='2'>2nd half<br><input type='radio' name='period' value='3'>Entire year"
1030 call "CDW000", tab$, "r", row$
1040 call "CDW000", row$, "dr", "Format"
1050 call "CDW000", row$, "d", "<input type='radio' name='format' value='P' checked>PDF<br><input type='radio' name='format' value='E'>Email"
1060 call "CDW000", tab$, "r", row$
1070 call "CDW000", row$, "dr", "Drop round<br>with lowest points"
1080 call "CDW000", row$, "d", "<input type='radio' name='drop' value='Y' checked>Yes<br><input type='radio' name='drop' value='N'>No"
1090 call "CDW000", tab$, "r", row$
1100 call "CDW000", row$, "dcs2", "Year"
1110 call "CDW000", tab$, "r class='head'", row$
1115 checked$ = " checked"
1120 for i = 1 to len(yrs$) step 4
1130   call "CDW000", row$, "dcs2", "<input type='radio' name='year' value='" + yrs$(i, 4) + "'" + checked$ + ">" + yrs$(i, 4)
1135   checked$ = ""
1140   call "CDW000", tab$, "r", row$
1150 next i
1160 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 if tcb(13) then exit
9020 run "CDW999"
