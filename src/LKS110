0100 REM "LKS110 - 03/14/93 TERMCAP COMPARISON
0110 BEGIN
0111 DIM SEP$[1:2]; LET SEP$[1]=":",SEP$[2]=","
0115 CALL "CDS084",ERR=9000,Y$,Y5$,6,"N",Y6,Y6$
0120 LET F1$="/u/CDI/bin/termcap"
0121 CALL "CDS069",7,30,0,1,"+first file+","A",0,F1$
0122 IF CTL=4 THEN GOTO 9000
0130 LET F2$="/u/bbx/termcap"
0131 CALL "CDS069",7,30,0,2,"+second file+","A",0,F2$
0132 IF CTL=4 THEN GOTO 0120
0133 IF POS("termcap"=F1$+" "+F2$)
:       THEN
:          LET TYPE=1
:       ELSE
:          IF POS("terminfo"=F1$+" "+F2$)
:             THEN
:                LET TYPE=2
:             ELSE
:                CALL "CDS069",1,2,0,3,"+file type 1=termcap, 2=terminfo","0",TYPE
0140 CALL "CDS069",1,10,0,4,"+terminal id+","A",0,T$
0150 IF CTL=4 THEN GOTO 9000
0160 LET F$=F1$;
:    GOSUB 1000;
:    IF CNT=0
:       THEN
:          GOTO 0140
:       ELSE
:          LET C1=CNT;
:          DIM C1$[CNT];
:          FOR I=1 TO CNT;
:             LET C1$[I]=Z$[I];
:          NEXT I
0170 LET F$=F2$;
:    GOSUB 1000;
:    IF CNT=0
:       THEN
:          GOTO 0140
:       ELSE
:          LET C2=CNT;
:          DIM C2$[CNT];
:          FOR I=1 TO CNT;
:             LET C2$[I]=Z$[I];
:          NEXT I
0180 LET I1=1,I2=1
0190 IF L<1 THEN GOSUB NEWPAGE
0191 IF I1>C1 AND I2>C2 THEN CALL "CDS094",Y6$,"E"; GOTO 0100
0200 IF I2>C2 THEN PRINT (6)C1$[I1]; LET I1=I1+1; GOTO 0270
0210 IF I1>C1 THEN PRINT (6)@(20),C2$[I2]; LET I2=I2+1; GOTO 0270
0220 IF C1$[I1]=C2$[I2] THEN LET I1=I1+1,I2=I2+1; GOTO 0191
0230 LET Z1=POS("="=C1$[I1]),Z2=POS("="=C2$[I2])
0240 IF Z1 AND Z2 AND C1$[I1](1,Z1)=C2$[I2](1,Z2)
:       THEN
:          PRINT (6)C1$[I1],@(20),C2$[I2];
:          LET I1=I1+1,I2=I2+1;
:          GOTO 0270
0242 LET Z1=POS("#"=C1$[I1]),Z2=POS("#"=C2$[I2])
0244 IF Z1 AND Z2 AND C1$[I1](1,Z1)=C2$[I2](1,Z2)
:       THEN
:          PRINT (6)C1$[I1],@(20),C2$[I2];
:          LET I1=I1+1,I2=I2+1;
:          GOTO 0270
0250 IF C1$[I1]<C2$[I2] THEN PRINT (6)C1$[I1]; LET I1=I1+1; GOTO 0270
0260 PRINT (6)@(20),C2$[I2]; LET I2=I2+1
0270 LET L=L-1
0280 GOTO 0190

0900 NEWPAGE:
0910 CALL "CDS094",ERR=0110,Y6$
0920 LET L=Y6
0930 RETURN

1000 REM "LOCATE CAP ENTRY
1010 LET Z$="",T0$=T$,CNT=0
1011 GOSUB COPY
1012 GOSUB SORT
1013 RETURN

1019 COPY:
1020 CALL "CDS095",ERR=1140,1,F$
1030 READ (1,END=1140)L$
1040 IF LEN(L$)<LEN(T0$) OR L$(1,1+LEN(T0$))<>T0$+"|" THEN GOTO 1030
1050 READ (1,END=1140)L$
1055 LET L$=CVS(L$,3)
1060 IF LEN(L$)=0 THEN GOTO 1050
1070 IF L$(1,1)="#" THEN GOTO 1050; REM "SKIP REMARKS
1080 IF L$(1,1)=$09$ THEN LET L$=L$(2); GOTO 1060; REM "STRIP LEADING TAB'S
1090 IF TYPE=1 THEN IF L$(1,1)=":" THEN LET L$=L$(2) ELSE ESCAPE
1091 IF TYPE=1 THEN IF L$(LEN(L$))="\" THEN LET L$=L$(1,LEN(L$)-1)
1105 IF POS("|"=L$)=0 THEN LET Z$=Z$+L$; GOTO 1050
1140 RETURN

1200 SORT:
1205 LET SORT=UNT
1210 CLOSE (SORT)
1220 OPEN (SORT)"|sort"
1230 LET H=1,CNT=0
1240 WHILE H<LEN(Z$)
1250    LET Z=POS(SEP$[TYPE]=Z$(H))
1255    LET ENT$=CVS(Z$(H,Z-1),3)
1260    IF (LEN(ENT$)>3 AND TYPE=1 AND ENT$(1,3)="tc=") OR (LEN(ENT$)>4 AND TYPE=2 AND ENT$(1,4)="use=")
:          THEN
:             LET T0$=ENT$(3+TYPE);
:             GOSUB COPY
:          ELSE
:             WRITE (SORT)ENT$
1270    LET H=H+Z,CNT=CNT+1
1280 WEND
1290 WRITE (SORT,IND=0)
1300 DIM Z$[1:CNT]
1310 LET I=1
1320 READ (SORT,END=1350)Z$[I]
1330 LET I=I+1
1340 GOTO 1320
1360 RETURN

9000 EOJ:
9010 RUN "CDS001"
