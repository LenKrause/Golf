0100 rem "GW046 - 05/04/14 HTML for Putts and Points Entry
0110 setesc 8000; seterr 8000
0120 enter y$, gr05$, gw046$
0130 if gr05.score then score$ = str(gr05.score)
0140 gw046.score$ = "<input type='text' size='2' maxlength='2' value='" + score$ + "' " + fnstyle$("score", "", "if (document.form1.score" + gr05.id$ + ".value.length == 0) {document.form1.net" + gr05.id$ + ".value=""""; document.form1.putts" + gr05.id$ + ".value=""""; document.form1.points" + gr05.id$ + ".value="""";} else {if (numrange(this,30,79)) {document.form1.net" + gr05.id$ + ".value=eval(document.form1.score" + gr05.id$ + ".value-" + str(gr05.hc) + ")}}")
0150 if gr05.net then net$ = str(gr05.net)
0160 gw046.net$ = "<input type='text' tabindex='-1' name='net" + gr05.id$ + "' size='2' maxlength='2' value='" + net$ + "'>"
0170 out$ = fns$("putts")
0175 out$ = out$ + "<option></option>"
0180 for putts = 9 to 27
0185   if gr05.putts = putts then sel$ = " selected" else sel$ = ""
0190   out$ = out$ + "<option" + sel$ + ">" + str(putts) + "</option>"
0200 next putts
0210 out$ = out$ + "</select>"
0220 gw046.putts$ = out$
0230 out$ = fns$("points")
0235 out$ = out$ + "<option></option>"
0240 for points = 0 to num(stbl("MAXPTS")) step .5
0245   if gr05.points > 0 and gr05.points = points then
:        sel$ = " selected"
:      else
:        sel$ = ""
:      fi
0250   out$ = out$ + "<option" + sel$ + ">" + str(points) + "</option>"
0260 next points
0270 out$ = out$ + "</select>"
0280 gw046.points$ = out$
0290 exit

7000 functions:
7010 def fns$(fld$) = "<select" + fnstyle$(fld$, "if (document.form1.score" + gr05.id$ + ".value == """") {adv(this)} else ", "") + ">"
7020 def fnstyle$(fld$, focus$, blur$) = " name='" + fld$ + gr05.id$ + "' onFocus='" + focus$ + "{document.form1." + fld$ + gr05.id$ + ".style.cssText = ""background-color:#ffff99""}' onBlur='document.form1." + fld$ + gr05.id$ + ".style.cssText = ""background-color:#fff"";" + blur$ + "'"

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 if tcb(13) then exit
9020 run "CDS001"
