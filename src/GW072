0100 rem "GW072 - 08/27/14 Send Post Season Email
0110 setesc 8000; seterr 8000
0120 call "CDS041", "CDS084", s084$, "YY"
0130 sent = 0
0140 s084.mode$ = "E", s084.name$ = "MAIL", s084.emailtype$ = "H"
0150 s084.fromname$ = "ScoresAndCharts.com", s084.fromemail$ = "scoresandcharts@gmail.com"
0160 tmp$ = stbl("WMS_CDW000", "D")
0170 call "CDS095", gr04, "XXGR04", "YNY", gr04$
0180 call "CDS095", gr06, "XXGR06", "YNY", gr06$
0190 call "CDS095", gr08, "XXGR08", "YNY", gr08$
0200 call "SW001", "XXGR01", 0, wases.league$, gr01$, sw001$
0210 s084.subject$ = cvs(gr01.name$, 3) + " Post Season"
0220 gr04s = unt, eml$ = ""
0230 select (gr04s)gr04$ from y.compcode$ + "GR04" where gr04.league$ = wases.league$ and pos(" " <> gr04.email$)
0240 read record(gr04s, end=0246)gr04$
0242 eml$ = eml$ + cvs(gr04.email$, 3) + ",", sent = sent + 1
0244 goto 0240
0246 if 1 then
:      s084.emailbcc$ = eml$
:    else
:      s084.toemail$ = "len@excellware.com"
:    fi
0247 gosub dates
0430 call "CDS084", y$, y5$, u0, s084$, y6, y6$
0440 note$ = cgi.note$
0445 open (99, mode = "O_CREATE,O_TRUNC")"tmp/lknote.htm";
:    write (99)note$;
:    close (99)
0450 call "SW002", note$, $0d0a$, "<br>"
0455 call "SW002", note$, "video link", "<a href='https://www.facebook.com/photo.php?v=10151575018971088'>this video</a>"
0460 print (y6.ch)note$
0470 call "CDS257", y$, wases.league$ + "p", "", "XXGR02", "W", 0, 0, 0, 0, "", "", "", "", cgi.note$
0580 print (y6.ch)html$
0590 call "CDS094", y6$, "e"
0610 goto eoj

2000 dates:
2010 html$ = "<br><br>", tab$ = "", row$ = ""
2030 call "CDW000", row$, "hcs4b", wases.leaguename$
2040 call "CDW000", tab$, "r", row$
2050 call "CDW000", row$, "hcs4b", "Post Season Schedule"
2060 call "CDW000", tab$, "r", row$
2070 call "CDW000", row$, "hc", "Date"
2080 call "CDW000", row$, "hc", "Time"
2090 call "CDW000", row$, "h", "Course"
2100 call "CDW000", row$, "h", "Address"
2110 call "CDW000", tab$, "r", row$
2220 call "CDS095", gr06, "XXGR06", "YNY", gr06$
2230 read record(gr06, knum=0, key=wases.league$ + y.jdate$, dom=2240)gr06$;
:    goto 2260
2240 read record(gr06, end=2380)gr06$
2250 if gr06.league$ <> wases.league$ then goto 2380
2260 call "CDW000", row$, "dc", date(gr06.date:"%Ds %Mz/%Dz/%Yz")
2270 call "CDW000", row$, "dc", gr06.time$
2275 dim gr08$:fattr(gr08$); gr08.name$ = "Course ID=" + gr06.id$
2276 read record(gr08, knum=0, key=gr06.id$, dom=2277)gr08$
2280 course$ = cvs(gr08.name$, 3), url$ = cvs(gr08.url$, 3)
2290 if len(url$) and pos("http://" = cvs(url$, 8)) = 0 then url$ = "http://" + url$
2300 if len(url$) then course$ = "<a href='" + url$ + "'>" + course$ + "</a>"
2310 call "CDW000", row$, "d", course$
2320 address$ = cvs(cvs(gr08.address$, 3) + " " + cvs(gr08.city$, 3) + " " + gr08.state$ + " " + gr08.zip$, 3)
2330 if len(address$) then
:      map$ = address$;
:      call "SW002::URL", map$;
:      address$ = "<a href='http://maps.google.com?q=" + map$ + "'>" + address$ + "</a>"
:    fi
2340 call "CDW000", row$, "d", address$
2350 call "CDW000", tab$, "r", row$
2360 goto 2240
2380 call "CDW000", html$, "t", tab$
2390 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9003 tmp$ = stbl("!CLEAR", "WMS_CDW000")
9004 html$ = ""
9005 msg$ = "Job Completed, " + str(sent) + " emails sent."
9010 run "GW011"
