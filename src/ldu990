0100 REM "LDU990 - 10/31/91 D.O.T. LABEL PRINTING
0110 REM "FILE 1 - XXSD00 SYSTEM CONTROL FILE
0120 REM "FILE 6 - PRINTER
0130 SETERR DT_ERR; SETESC DT_ERR
0140 LET D$=DAY,W9=32,D9=8,W=28,D=8
0150 PRINT 'CF',@(0,5),'CE','SB',@(20,10),"LABELS ACROSS:",@(20,12),"No. OF LABELS:",@(20,14),"D.O.T. DATE:",@(20,16),'SF',
0160 CALL "CDS069",1,4,40,10,"+NUMBER OF LABELS ACROSS (1-4)+","0",A
0170 IF CTL=4 THEN GOTO 9000
0180 CALL "CDS069",1,99999,40,12,"+NUMBER OF LABELS TO PRINT","##,##0",N
0190 CALL "CDS069",0,0,40,14,"+D.O.T. DATE","D",0,D$
0200 LET Z$="Y";
:    CALL "CDS069",1,1,0,0,"           IS DATA CORRECT (Y/N)","ANY",0,Z$
0210 IF Z$="N" THEN GOTO 0150
0220 LET Z$="Y"
0230 LET Z$="N";
:    CALL "CDS069",1,1,0,0,"          PRINT TEST PATTERN (Y/N)","ANY",0,Z$
0240 IF Z$="Y" THEN GOSUB 1000; LET Z$="N"; GOTO 0230
0250 DIM X0$(28);
:    LET X0$(1)=Y.COMPNAME$;
:    IF Y.COMPCODE$="LD"
:       THEN
:          LET X$="(216) 229-1200",NUM$="1319"
:       ELSE
:          LET X$="(216) 721-9900",NUM$="1513"
0260 DIM L$(W*D);
:    LET L$(1,28)=X0$,L$(29,28)="2800 East 90th Street",L$(57,28)="Cleveland, Ohio  44104",L$(85,28)=X$,L$(113,28)="TESTED D.O.T. R-"+NUM$,L$(141,28)="DATE "+D$
0265 LET Z$="Y";
:    CALL "CDS069",1,1,0,0,"      PRINT COMPANY NAME ON LABELS  (Y/N)","ANY",0,Z$
0266 IF Z$="N" THEN GOSUB 7000
0268 LET Z$="Y";
:    CALL "CDS069",1,1,0,0,"      PRINT D.O.T. NUMBER & DATE  (Y/N)","ANY",0,Z$
0269 IF Z$="N" THEN GOSUB 7500
0270 GOSUB 1100
0280 LET Z$="N";
:    CALL "CDS069",1,1,0,0,"       DO YOU WANT ANOTHER COPY (Y/N)","ANY",0,Z$
0290 IF Z$="Y" THEN GOTO 0270
0300 PRINT @(0,5),'CE',
0310 GOTO 0150

1000 REM "PRINT TEST PATTERN
1010 LET L1$=L$
1020 DIM L$(D*W,"X")
1030 GOSUB 1200
1040 PRINT (6)Q$
1050 LET L$=L1$,L1$=""
1060 RETURN

1100 REM "PRINT LABELS
1110 LET M=MOD(N,A)
1120 IF M>0 THEN LET N=N+A-M
1130 PRINT @(8,22),"PRINTING LABEL ",
1140 GOSUB 1200
1150 FOR I=1 TO N/A
1160    PRINT @(23,22),I*A,"...",
1170    PRINT (6)Q$
1180 NEXT I
1190 RETURN

1200 REM "GENERATE Q$
1210 LET Q=1,W2=W+2
1220 DIM Q$(133*D+1),P$(W2*A+1)
1230 FOR J=1 TO D
1240    LET Z$=$1F$+L$((J-1)*W+1,W),P=1
1250    FOR K=1 TO A
1260       LET P$(P,W2)=Z$,P=P+W2
1270    NEXT K
1280    LET Q$(Q,132)=P$,Q$(Q+132,1)=AND("*",$DF$),Q=Q+133
1290 NEXT J
1300 RETURN

7000 REM PRINT SETUP FOR LABELS WITHOUT COMPANY NAME
7010 IF Y.COMPCODE$="LD"
:       THEN
:          LET L$(1,28)=" ",L$(29,28)="17E/17H TESTED",L$(57,28)=" ",L$(85,28)="D.O.T. R-"+NUM$,L$(113,28)=" ",L$(141,28)="DATE "+D$
:       ELSE
:          LET L$(1,28)=" ",L$(29,28)="TESTED",L$(57,28)=" ",L$(85,28)="D.O.T. R-"+NUM$,L$(113,28)=" ",L$(141,28)="DATE "+D$
7030 RETURN

7500 REM PRINT ONLY COMPANY NAME & ADDRESS
7510 DIM L$(W*D);
:    LET L$(1,28)=X0$,L$(29,28)=" ",L$(57,28)="2800 East 90th Street",L$(85,28)=" ",L$(113,28)="Cleveland, Ohio 44104",L$(141,28)=" ",L$(169,28)=X$
7530 RETURN

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9005 CLEAR
9010 RUN "CDS001"
