0100 REM "GW010 - 11/18/18 Dynaweb Golf
0110 SETESC 8000; SETERR 8000
0120 GOSUB INIT
0130 IF PROG$="GW999" THEN RUN PROG$; REM "Need to do this even if not yet logged in
0140 IF POS(" "<>WASES.LEAGUE$)=0 OR (POS(" "<>WASES.CID$)=0 AND WASES.ADMIN$<>"F") THEN GOSUB LOGIN
0150 IF POS("|"+PROG$+"|"="|CDW100|GW011|GW012|GW013|GW014|GW015|GW016|GW020|GW030|GW031|GW040|GW041|GW042|GW043|GW044|GW045|GW050|GW051|GW053|GW061|GW062|GW070|GW071|GW072|GW080|GW090|GW100|GW101|")=0 THEN LET MSG$="Not authorized to run program "+PROG$,PROG$="GW011"
0160 GOSUB RUN_
0170 LET WASES.LEAGUE$=""
0180 GOTO 0140

1000 RUN_:
1010 GOSUB PROGINFO
1020 LET TMP$=STBL("$otherhead",STBL("$otherhead")+$0A$+"<!-- Dynaweb Program = "+PROG$+" -->"+$0A$)
1030 IF POS("t"=DEBUG$) THEN GOSUB DOTRACE; SETTRACE (32701)
1040 RUN PROG$,ERR=1050
1050 LET MSG$="Unable to run program "+PROG$
1060 RETURN

1100 LOGIN:
1110 CALL "CDS095",GR04,"XXGR04","YUY",GR04$
1120 LET EMAIL$="",PASSWORD$="",PWD=0,MULTI=0,WASES.LEAGUE$="",WASES.LEAGUENAME$="",WASES.CID$=""
1130 LET MLS$=ATH(FIELD(CGI$,"mls",ERR=1180),ERR=1180)
1140 CALL "SW001",ERR=1180,"XXGR01",0,MLS$(1,3),GR01$
1150 LET WASES.LEAGUE$=GR01.LEAGUE$,WASES.LEAGUENAME$=GR01.NAME$
1160 READ RECORD(GR04,KNUM=0,KEY=WASES.LEAGUE$+MLS$(4,4),DOM=1180)GR04$
1170 LET WASES.CID$=GR04.ID$,WASES.ADMIN$=GR04.ADMIN$
1171 CALL "utses.wbb",1,WASES$,""
1173 RETURN
1180 LET EMAIL$=FIELD(CGI$,"EMAIL",ERR=1190); IF POS(" "<>EMAIL$)=0 THEN LET LOGINMSG$="Email Address is required"; GOTO 1370
1190 IF LEN(EMAIL$)=0 THEN GOTO 1370
1200 LET PWD=FIELD(CGI$,"pwd",ERR=1210)="send"
1210 LET PASSWORD$=PAD(FIELD(CGI$,"PASSWORD",ERR=1220),8)
1220 GOSUB GETGR04S
1230 SWITCH 1
1240 CASE RECS=0
1250 CALL "CDS275::VALIDATE",ERR=1260,EMAIL$,LOGINMSG$
1260 IF LEN(LOGINMSG$)=0 THEN LET LOGINMSG$="Email Address is not on file, please contact your league secretary."
1270 LET GR04.EMAIL$=EMAIL$,EMAIL$=""
1280 BREAK
1285 case recs=1 and gr04.LEAGUE$="TTM"; gosub sendttmlink; break
1290 CASE LEN(PWDL$)=0 OR PWD; GOSUB SENDPWD; BREAK
1300 CASE LEN(PASSWORD$) AND POS(PASSWORD$=PWDL$,8)=0; LET LOGINMSG$="Incorrect Password"; BREAK
1310 CASE LEN(PASSWORD$) AND POS(PASSWORD$=PWDL$,8)
1320 IF LEN(GR04L$)=LEN(GR04$) THEN LET WASES.LEAGUE$=GR04.LEAGUE$,WASES.CID$=GR04.ID$,WASES.ADMIN$=GR04.ADMIN$; CALL "SW001","XXGR01",0,GR04.LEAGUE$,GR01$,SW001$; LET WASES.LEAGUENAME$=GR01.NAME$ ELSE LET MULTI=1
1330 BREAK
1340 SWEND
1350 CALL "utses.wbb",1,WASES$,""
1360 IF POS(" "<>WASES.CID$) THEN RETURN
1370 ON MULTI GOSUB SINGLE,MULTI
1380 GOTO EOJ

1400 SINGLE:
1410 CALL "CDS041","CDW550",W550$,"YP"
1420 LET W550.FILNAM$="GR04",W550.SUBMITPROG$="GW011"
1430 IF POS(" "<>EMAIL$) THEN LET GR04.EMAIL$=EMAIL$,W550.VIEW$="M",W550.INCPIPE$="N",W550.EXTRAFOOT$=" <input type='button' onclick='window.location=""[DSN]?p=GW011&pwd=send&email="+CVS(EMAIL$,3)+"""' value='Forgot Password'>" ELSE LET W550.VIEW$="L",W550.INCBACK$="N"
1440 LET W550.INCCONAME$="Y",W550.INCHOME$="N",W550.INCCLOSE$="N"
1445 IF 0 THEN LET W550.EXTRAFOOT$="<input type='button' onclick='window.open(""http://sc.excellware.com:8040"")' value='Remote Control'>"
1450 CALL "CDW550",Y$,GR04$,W550$,HTML$
1460 IF LEN(LOGINMSG$) THEN LET HTML$="<p class='msg'>"+LOGINMSG$+"</p>"+HTML$
1470 IF POS(" "<>WA00.LOGINMSG$) THEN LET HTML$="<p class='msg'>"+WA00.LOGINMSG$+"</p>"+HTML$
1480 RETURN

1500 MULTI:
1510 CALL "CDW000",ROW$,"dc",Y.COMPNAME$
1520 CALL "CDW000",TAB$,"r class='top'",ROW$
1530 CALL "CDW000",ROW$,"dc","League Selection"
1540 CALL "CDW000",TAB$,"r class='title'",ROW$
1550 CALL "CDW000",ROW$,"dc",EMAIL$
1560 CALL "CDW000",TAB$,"r",ROW$
1570 FOR I=1 TO LEN(GR04L$) STEP LEN(GR04$)
1580 LET GR04$=GR04L$(I,LEN(GR04$))
1590 CALL "SW001","XXGR01",0,GR04.LEAGUE$,GR01$,SW001$
1600 CALL "CDW000",ROW$,"d","<a href='[DSN]?p=GW011&mls="+HTA(GR04.LEAGUE$+GR04.ID$)+"'>"+CVS(GR01.NAME$,2)+"</a>"
1620 CALL "CDW000",TAB$,"r",ROW$
1630 NEXT I
1640 CALL "CDW000",ROW$,"dc","<input type='button' onclick='window.location=""[DSN]?p=GW999""' value='Logout'"
1650 CALL "CDW000",TAB$,"r class='foot'",ROW$
1660 CALL "CDW000",HTML$,"tc class='menu'",TAB$
1670 RETURN

1700 GETGR04S:
1710 LET RECS=0,PWDL$="",GR04L$="",GR04S=UNT
1720 SELECT (GR04S)GR04$ FROM Y.COMPCODE$+"GR04" WHERE GR04.EMAIL$=PAD(CVS(EMAIL$,8),50)
1730 READ RECORD(GR04S,END=1770)GR04$
1740 LET GR04L$=GR04L$+GR04$,RECS=RECS+1
1750 IF POS(" "<>GR04.PASSWORD$) THEN LET PWDL$=PWDL$+GR04.PASSWORD$
1760 GOTO 1730
1770 CLOSE (GR04S)
1780 RETURN

1800 SENDPWD:
1810 IF LEN(PWDL$)=0 THEN LET PWD$=STR(INT(FPT(TIM)*10000)),TYPE$="Temporary" ELSE LET PWD$=CVS(PWDL$(1,8),2),TYPE$="Your"
1820 FOR I=1 TO LEN(GR04L$) STEP LEN(GR04$)
1830 LET GR04$=GR04L$(I,LEN(GR04$))
1840 LET GR04.PASSWORD$=PWD$
1850 WRITE RECORD(GR04)GR04$
1860 NEXT I
1870 CALL "CDS041","CDS084",S084$,"YP"
1880 LET S084.NAME$="MAIL",S084.METHOD$="N",S084.SUBJECT$="ScoresAndCharts.com Golfer Password"
1890 LET S084.TOEMAIL$=EMAIL$,S084.EMAILBCC$="webmaster@scoresandcharts.com"
1900 CALL "SW001","XXGR01",0,GR04.LEAGUE$,GR01$,SW001$
1910 LET S084.FROMNAME$="ScoresAndCharts.com",S084.FROMEMAIL$="scoresandcharts@gmail.com"
1920 CALL "CDS084",Y$,Y5$,U0,S084$,Y6,Y6$
1930 PRINT (Y6.CH)CVS(GR01.NAME$,2)
1940 PRINT (Y6.CH)TYPE$+" Password is "+PWD$
1950 CALL "CDS094",Y6$,"E"
1960 LET LOGINMSG$="Check your email (and spam folder) for "+TYPE$+" Password"
1970 RETURN

2000 HOME:
2010 DIM CGI$:"M:C(3)"; LET CGI.M$="SYS",PROG$="CDW100"; GOTO RUN_

2100 SEND:
2110 IF 0 THEN CALL "WA800",Y$,WASES$,WA800$; LET HTML$=WA800.TOPHTML$+HTML$+WA800.BOTHTML$
2120 CALL "utfrmout.wbb",Y$,HTML$+"",HTML$
2130 CALL "utfrmout.wbb",ENV$,HTML$+"",HTML$
2140 CALL "utfrmout.wbb",WA00$,HTML$+"",HTML$
2150 CALL "utfrmout.wbb",GR01$,HTML$+"",HTML$
2160 GOTO EOJ

2200 FUNCTIONS:
2210 DEF FNDIF(XIN1$,XIN2$)=LEN(XIN2$)<LEN(XIN1$) OR XIN2$(1,LEN(XIN1$))<>XIN1$
2220 DEF FNIS_BLANK(XIN$)=!(LEN(CVS(XIN$,3)))
2230 DEF FNLINK(TEXT$,HREF$,VBL$)
2240 IF LEN(VBL$)=0 THEN LET VBL$=CVS(TEXT$,4)
2250 IF VBL$="LEAGUESEL" THEN LET TMP$=" | " ELSE LET TMP$=""
2260 LET TMP$=STBL(VBL$,TMP$+"<input type='button' onclick='"+HREF$+"' value='"+TEXT$+"'>")
2270 RETURN 0
2280 FNEND

2300 INIT:
2310 LET Y.COMPNAME$="ScoresAndCharts.com"
2320 CALL "CDS091","","",SM08$
2330 CALL "CDS041","WASES",WASES$,"YP"
2340 LET TMP$=STBL("$sestpl",FATTR(WASES$))
2350 CALL "SW005",Y$,"WA",WA00$
2360 GOSUB BBWEB
2370 GOSUB SESSION_SETS
2380 LET Y5$=FIELD(CGI$,"Y5",ERR=2390),Y5=1,SM08.JOBTITLE$=Y5$,TMP$=STBL("$title",Y5$)
2390 LET FIL$=FIELD(CGI$,"f",ERR=2400)
2400 LET PROG$="GW011",PROG$=FIELD(CGI$,"p",ERR=2410)
2410 LET TMP$=STBL("SMS_CDS084.SYMBOL","E29BB3")
2420 RETURN

2500 PROGINFO:
2510 CALL "CDS095",ERR=2540,PROGCHAN,PROG$,"NNX"
2520 IF !(Y5) THEN CALL "CDS095",ERR=2530,PROGCHAN,PROG$,"NNX"; CALL "CDS187",ERR=2530,PROGCHAN,S187$; LET SM08.JOBTITLE$=S187.DESC$
2530 IF PROGCHAN THEN CLOSE (PROGCHAN)
2540 LET SM08.APPID$=PROG$(MAX(1,LEN(PROG$)-LEN(SM08.APPID$)+1))
2550 IF POS(" "<>WASES.LEAGUENAME$) THEN LET SM08.COMPNAME$=WASES.LEAGUENAME$,Y.COMPNAME$=CVS(WASES.LEAGUENAME$,2)
2560 CALL "CDS095",SM08,"CDSM08","NNX"
2570 WRITE RECORD(SM08)SM08$
2580 CLOSE (SM08)
2590 LET TMP$=STBL("SMS_SM08",SM08$)
2600 RETURN

2700 BBWEB:
2710 GOSUB BBWEB_STBLS
2720 IF CVS(SYS,2)="LEVEL 5" THEN LET BWU$=STBL("BBXDIR")+"bwu/" ELSE LET BWU$=STBL("BBXDIR")+"../utils/bwu/"
2730 PREFIX PFX+" "+BWU$
2740 CALL "utaddr.wbb"; REM "ADDR frequently used pgms
2750 CALL "utcgi.wbb",ENV$,CGI$,ERRMSG$; IF LEN(ERRMSG$) THEN GOTO ERR_SESSION
2760 CALL "utses.wbb",0,WASES$,ERRMSG$; IF LEN(ERRMSG$) THEN GOTO ERR_SESSION
2770 LET GLB$="SMSDIR"; GOSUB SLASH
2780 LET GLB$="SMSAPP"; GOSUB SLASH
2790 LET GLB$="SMSTMP"; GOSUB SLASH
2800 LET GLB$="SMSLOG"; GOSUB SLASH
2810 LET GLB$="BBXDIR"; GOSUB SLASH
2820 RETURN

2900 BBWEB_STBLS:
2910 IF 0 THEN LET TMP$=STBL("DOCTYPE","<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01 Frameset//EN"" ""http://www.w3.org/TR/html4/frameset.dtd"">")
2920 LET TMP$=STBL("$title","Golfer Login"); REM "$title global must be set to send tags before HTML$
2930 IF POS(" "=WA00.C_BODY$)=0 THEN LET TMP$=STBL("$bodyopt","bgcolor='#"+WA00.C_BODY$+"'")
2940 IF POS(" "<>WA00.WEBLOGO$) THEN LET TMP$=STBL("$pagetop","<p align='center'><img border='0' src='"+CVS(WA00.WEBLOGO$,2)+"'></p>")
2950 LET TMP$=STBL("$menubar","#e0e0e0")
2960 LET TMP$=STBL("$mailerror",CVS(WA00.ERROREMAIL$,2))
2970 LET TMP$=STBL("SMS_MODE","web")
2980 LET OTHHEAD$="",OTHHEAD$=STBL("$otherhead",ERR=2981)
2985 LET OTHHEAD$=OTHHEAD$+$0A$+"<link rel='import' href='/import/icons.html'>"+$0A$
2986 LET OTHHEAD$=OTHHEAD$+$0A$+"<meta name='viewport' content='width=device-width, initial-scale=1'>"+$0A$
2990 IF POS(" "<>WA00.STYLESHEET$) THEN LET OTHHEAD$=OTHHEAD$+"<link rel=stylesheet type=""text/css"" href=""/"+y.COMPCODE$+"/dynaweb/"+CVS(WA00.STYLESHEET$,3)+""">"+$0A$
3000 IF 0 THEN LET OTHHEAD$=OTHHEAD$+"<script type=""Text/JavaScript"" src=""/scripts/dwLib.js""></script>"+$0A$
3010 IF 0 THEN LET OTHHEAD$=OTHHEAD$+"<script type=""Text/JavaScript"" src=""/scripts/"+Y.COMPCODE$+".js""></script>"+$0A$
3020 LET TMP$=STBL("$otherhead",OTHHEAD$)
3030 LET TMP$=STBL("WMS_tblparms"," border=1 cellpadding=3 cellspacing=1")
3040 RETURN

3100 SESSION_SETS:
3110 LET TMP$=STBL("DSN",STBL("SCRIPT_NAME")); REM "Set Dynaweb Script Name
3120 LET TMP$=STBL("HOVER","onMouseOver=""HoverOn(this)"" onMouseOut=""HoverOff(this)""")
3130 LET TMP$=STBL("WMS_STARTTIME",STR(TIM*100))
3140 LET WASES.SESSIONID$=STBL("$sesid")
3150 LET DEBUG$=FIELD(CGI$,"debug",ERR=3160); GOSUB DEBUG
3160 LET TMP=FNLINK("Back","JavaScript:window.history.back(-1)","")
3170 LET TMP=FNLINK("Print","JavaScript:window.print(1)","")
3180 LET TMP=FNLINK("Home","Javascript:window.location=""[DSN]""","")
3190 LET TMP=FNLINK("Cancel","Javascript:window.location=""[DSN]""","")
3200 LET TMP$=STBL("LEAGUESEL","")
3210 LET TMP=FNLINK("Logout","JavaScript:window.location=""[DSN]?p=GW999""","")
3220 LET TMP=FNLINK("Close","JavaScript:window.open(""/close.htm"",""_top"");","")
3230 RETURN

3300 SLASH:
3310 LET TMP$=STBL(GLB$)
3320 IF TMP$(LEN(TMP$))<>"/" THEN LET TMP$=STBL(GLB$,TMP$+"/")
3330 RETURN

3400 FIL:
3410 LET FIL=UNT
3420 LET HTML$="Requested page has expired and is no longer available."
3430 OPEN (FIL,ERR=SEND)FIL$
3440 LET FIN$=FIN(FIL),SIZ=DEC(FIN$(1,4))
3450 READ RECORD(FIL,SIZ=SIZ)HTML$
3460 CLOSE (FIL)
3470 IF FIELD(CGI$,"fmt",ERR=3480)="e" THEN CALL "CDW003",HTML$,"e"; LET HTML$="<pre style=""text-align:left"">"+HTML$+"</pre>"
3480 GOTO SEND

3500 DEBUG:
3510 IF POS("d"=DEBUG$) THEN CALL "CDS173"; CALL "SW002",DEBUG$,"d",""
3520 IF POS("e"=DEBUG$) THEN CALL "CDW279",ENV$,HTML$,"ENV"
3530 IF POS("g"=DEBUG$) THEN CALL "CDW279","!LIST",HTML$,"Global Variables"
3540 IF POS("c"=DEBUG$) THEN CALL "CDW279",CGI$,HTML$,"CGI"
3550 IF POS("v"=DEBUG$) THEN CALL "CDW279",WASES$,HTML$,"Session Variable WASES"; CALL "CDW279",WA00$,HTML$,"Web Control Variable WA00"
3560 IF POS("t"<>DEBUG$) THEN GOTO SEND
3570 RETURN

3600 DOTRACE:
3610 LET TRFILNAM$="tmp/,txt"; CALL "CDS064",Y$,32701,0,0,0,0,"STR",TRFILNAM$
3620 LET TMP$="",TMP$=STBL("$otherhead",ERR=3630)
3630 LET TMP$=STBL("$otherhead",TMP$+"<script type='text/javascript'>window.open('/"+TRFILNAM$+"','trace','status=0,menubar=0,scrollbars=1,toolbar=0,location=0,direction=0')</script>"+$0A$)
3640 RETURN

3700 SENDTTMLINK:
3770 CALL "CDS041","CDS084",S084$,"YP"
3780 LET S084.NAME$="MAIL",S084.METHOD$="N",S084.SUBJECT$="Tee Time Manager Access Link"
3790 LET S084.TOEMAIL$=EMAIL$,S084.EMAILBCC$="webmaster@scoresandcharts.com"
3800 CALL "SW001","XXGR01",0,GR04.LEAGUE$,GR01$,SW001$
3810 LET S084.FROMNAME$="Tee Time Manager",S084.FROMEMAIL$="webmaster@scoresandcharts.com"
3820 CALL "CDS084",Y$,Y5$,U0,S084$,Y6,Y6$
3830 PRINT (Y6.CH)CVS(GR01.NAME$,2)
3840 PRINT (Y6.CH)"Tee Time Manager Access Link: http://www.scoresandcharts.com/cgi-bin/ttm.cgi"
3850 CALL "CDS094",Y6$,"E"
3860 LET LOGINMSG$="Check your email (and spam folder) for Tee Time Manager Access Link"
3870 RETURN

8000 REM "Call Error/Escape Routine
8010 CALL "CDS063",STR(TCB(5)),Y$,PGM(-2)
8020 ON Y.ERRSTS GOTO 8030,8040,8050,8060
8030 SETERR 0
8040 RETRY
8050 RETURN
8060 RETRY

8200 ERR_SESSION:
8210 LET HTML$="GW010: Error getting proper Session information. <br> Please contact Excellware. <br> Error Message: "+ERRMSG$
8220 GOTO SEND

9000 EOJ:
9006 RUN "CDW999"

9996 REM EDITOR
9997 SETERR 9999; SETESC 9998; CALL "CDS140",O$,O1$; SETERR 9997; EXECUTE O$
9998 DIM O$(0); GOTO 9997
9999 END
