0100 rem "GW010 - 11/18/18 Dynaweb Golf
0110 setesc 8000; seterr 8000
0120 gosub init
0130 if prog$ = "GW999" then
:      run prog$;
:      rem "Need to do this even if not yet logged in
0140 if pos(" " <> wases.league$) = 0 or (pos(" " <> wases.cid$) = 0 and wases.admin$ <> "F") then
:      gosub login
:    fi
0150 if pos("|" + prog$ + "|" = "|CDW100|GW011|GW012|GW013|GW014|GW015|GW016|GW020|GW030|GW031|GW040|GW041|GW042|GW043|GW044|GW045|GW050|GW051|GW053|GW061|GW062|GW070|GW071|GW072|GW080|GW090|GW100|GW101|") = 0 then
:      msg$ = "Not authorized to run program " + prog$, prog$ = "GW011"
:    fi
0160 gosub run_
0170 wases.league$ = ""
0180 goto 0140

1000 run_:
1010 gosub proginfo
1020 tmp$ = stbl("$otherhead", stbl("$otherhead") + $0a$ + "<!-- Dynaweb Program = " + prog$ + " -->" + $0a$)
1030 if pos("t" = debug$) then gosub dotrace; settrace (32701)
1040 run prog$, err=1050
1050 msg$ = "Unable to run program " + prog$
1060 return

1100 login:
1110 call "CDS095", gr04, "XXGR04", "YUY", gr04$
1120 email$ = "", password$ = "", pwd = 0, multi = 0, wases.league$ = "", wases.leaguename$ = "", wases.cid$ = ""
1130 mls$ = ath(field(cgi$, "mls", err=1180), err=1180)
1140 call "SW001", err=1180, "XXGR01", 0, mls$(1,3), gr01$
1150 wases.league$ = gr01.league$, wases.leaguename$ = gr01.name$
1160 read record(gr04, knum=0, key=wases.league$ + mls$(4,4), dom=1180)gr04$
1170 wases.cid$ = gr04.id$, wases.admin$ = gr04.admin$
1171 call "utses.wbb", 1, wases$, ""
1173 return
1180 email$ = field(cgi$, "EMAIL", err=1190);
:    if pos(" " <> email$) = 0 then
:      loginmsg$ = "Email Address is required";
:      goto 1370
:    fi
1190 if len(email$) = 0 then goto 1370
1200 pwd = field(cgi$, "pwd", err=1210) = "send"
1210 password$ = pad(field(cgi$, "PASSWORD", err=1220), 8)
1220 gosub getgr04s
1230 switch 1
1240   case recs = 0
1250     call "CDS275::VALIDATE", err=1260, email$, loginmsg$
1260     if len(loginmsg$) = 0 then
:          loginmsg$ = "Email Address is not on file, please contact your league secretary."
:        fi
1270     gr04.email$ = email$, email$ = ""
1280     break
1285   case recs = 1 and gr04.league$ = "TTM"; gosub sendttmlink; break
1290   case len(pwdl$) = 0 or pwd; gosub sendpwd; break
1300   case len(password$) and pos(password$ = pwdl$, 8) = 0;
:      loginmsg$ = "Incorrect Password";
:      break
1310   case len(password$) and pos(password$ = pwdl$, 8)
1320     if len(gr04l$) = len(gr04$) then
:          wases.league$ = gr04.league$, wases.cid$ = gr04.id$, wases.admin$ = gr04.admin$;
:          call "SW001", "XXGR01", 0, gr04.league$, gr01$, sw001$;
:          wases.leaguename$ = gr01.name$
:        else
:          multi = 1
:        fi
1330     break
1340 swend
1350 call "utses.wbb", 1, wases$, ""
1360 if pos(" " <> wases.cid$) then return
1370 on multi gosub single, multi
1380 goto eoj

1400 single:
1410 call "CDS041", "CDW550", w550$, "YP"
1420 w550.filnam$ = "GR04", w550.submitprog$ = "GW011"
1430 if pos(" " <> email$) then
:      gr04.email$ = email$, w550.view$ = "M", w550.incpipe$ = "N", w550.extrafoot$ = " <input type='button' onclick='window.location=""[DSN]?p=GW011&pwd=send&email=" + cvs(email$, 3) + """' value='Forgot Password'>"
:    else
:      w550.view$ = "L", w550.incback$ = "N"
:    fi
1440 w550.incconame$ = "Y", w550.inchome$ = "N", w550.incclose$ = "N"
1445 if 0 then
:      w550.extrafoot$ = "<input type='button' onclick='window.open(""http://sc.excellware.com:8040"")' value='Remote Control'>"
:    fi
1450 call "CDW550", y$, gr04$, w550$, html$
1460 if len(loginmsg$) then html$ = "<p class='msg'>" + loginmsg$ + "</p>" + html$
1470 if pos(" " <> wa00.loginmsg$) then
:      html$ = "<p class='msg'>" + wa00.loginmsg$ + "</p>" + html$
:    fi
1480 return

1500 multi:
1510 call "CDW000", row$, "dc", y.compname$
1520 call "CDW000", tab$, "r class='top'", row$
1530 call "CDW000", row$, "dc", "League Selection"
1540 call "CDW000", tab$, "r class='title'", row$
1550 call "CDW000", row$, "dc", email$
1560 call "CDW000", tab$, "r", row$
1570 for i = 1 to len(gr04l$) step len(gr04$)
1580   gr04$ = gr04l$(i, len(gr04$))
1590   call "SW001", "XXGR01", 0, gr04.league$, gr01$, sw001$
1600   call "CDW000", row$, "d", "<a href='[DSN]?p=GW011&mls=" + hta(gr04.league$ + gr04.id$) + "'>" + cvs(gr01.name$, 2) + "</a>"
1620   call "CDW000", tab$, "r", row$
1630 next i
1640 call "CDW000", row$, "dc", "<input type='button' onclick='window.location=""[DSN]?p=GW999""' value='Logout'"
1650 call "CDW000", tab$, "r class='foot'", row$
1660 call "CDW000", html$, "tc class='menu'", tab$
1670 return

1700 getgr04s:
1710 recs = 0, pwdl$ = "", gr04l$ = "", gr04s = unt
1720 select (gr04s)gr04$ from y.compcode$ + "GR04" where gr04.email$ = pad(cvs(email$, 8), 50)
1730 read record(gr04s, end=1770)gr04$
1740 gr04l$ = gr04l$ + gr04$, recs = recs + 1
1750 if pos(" " <> gr04.password$) then pwdl$ = pwdl$ + gr04.password$
1760 goto 1730
1770 close (gr04s)
1780 return

1800 sendpwd:
1810 if len(pwdl$) = 0 then
:      pwd$ = str(int(fpt(tim) * 10000)), type$ = "Temporary"
:    else
:      pwd$ = cvs(pwdl$(1,8), 2), type$ = "Your"
:    fi
1820 for i = 1 to len(gr04l$) step len(gr04$)
1830   gr04$ = gr04l$(i, len(gr04$))
1840   gr04.password$ = pwd$
1850   write record(gr04)gr04$
1860 next i
1870 call "CDS041", "CDS084", s084$, "YP"
1880 s084.name$ = "MAIL", s084.method$ = "N", s084.subject$ = "ScoresAndCharts.com Golfer Password"
1890 s084.toemail$ = email$, s084.emailbcc$ = "webmaster@scoresandcharts.com"
1900 call "SW001", "XXGR01", 0, gr04.league$, gr01$, sw001$
1910 s084.fromname$ = "ScoresAndCharts.com", s084.fromemail$ = "webmaster@scoresandcharts.com"
1920 call "CDS084", y$, y5$, u0, s084$, y6, y6$
1930 print (y6.ch)cvs(gr01.name$, 2)
1940 print (y6.ch)type$ + " Password is " + pwd$
1950 call "CDS094", y6$, "E"
1960 loginmsg$ = "Check your email (and spam folder) for " + type$ + " Password"
1970 return

2000 home:
2010 dim cgi$:"M:C(3)"; cgi.m$ = "SYS", prog$ = "CDW100"; goto run_

2100 send:
2110 if 0 then
:      call "WA800", y$, wases$, wa800$;
:      html$ = wa800.tophtml$ + html$ + wa800.bothtml$
:    fi
2120 call "utfrmout.wbb", y$, html$ + "", html$
2130 call "utfrmout.wbb", env$, html$ + "", html$
2140 call "utfrmout.wbb", wa00$, html$ + "", html$
2150 call "utfrmout.wbb", gr01$, html$ + "", html$
2160 goto eoj

2200 functions:
2210 def fndif(xin1$, xin2$) = len(xin2$) < len(xin1$) or xin2$(1, len(xin1$)) <> xin1$
2220 def fnis_blank(xin$) = !(len(cvs(xin$, 3)))
2230 def fnlink(text$, href$, vbl$)
2240 if len(vbl$) = 0 then vbl$ = cvs(text$, 4)
2250 if vbl$ = "LEAGUESEL" then tmp$ = " | " else tmp$ = ""
2260 tmp$ = stbl(vbl$, tmp$ + "<input type='button' onclick='" + href$ + "' value='" + text$ + "'>")
2270 return 0
2280 fnend

2300 init:
2310 y.compname$ = "ScoresAndCharts.com"
2320 call "CDS091", "", "", sm08$
2330 call "CDS041", "WASES", wases$, "YP"
2340 tmp$ = stbl("$sestpl", fattr(wases$))
2350 call "SW005", y$, "WA", wa00$
2360 gosub bbweb
2370 gosub session_sets
2380 y5$ = field(cgi$, "Y5", err=2390), y5 = 1, sm08.jobtitle$ = y5$, tmp$ = stbl("$title", y5$)
2390 fil$ = field(cgi$, "f", err=2400)
2400 prog$ = "GW011", prog$ = field(cgi$, "p", err=2410)
2410 tmp$ = stbl("SMS_CDS084.SYMBOL", "E29BB3")
2420 return

2500 proginfo:
2510 call "CDS095", err=2540, progchan, prog$, "NNX"
2520 if !(y5) then
:      call "CDS095", err=2530, progchan, prog$, "NNX";
:      call "CDS187", err=2530, progchan, s187$;
:      sm08.jobtitle$ = s187.desc$
:    fi
2530 if progchan then close (progchan)
2540 sm08.appid$ = prog$(max(1, len(prog$) - len(sm08.appid$) + 1))
2550 if pos(" " <> wases.leaguename$) then
:      sm08.compname$ = wases.leaguename$, y.compname$ = cvs(wases.leaguename$, 2)
:    fi
2560 call "CDS095", sm08, "CDSM08", "NNX"
2570 write record(sm08)sm08$
2580 close (sm08)
2590 tmp$ = stbl("SMS_SM08", sm08$)
2600 return

2700 bbweb:
2710 gosub bbweb_stbls
2720 if cvs(sys, 2) = "LEVEL 5" then
:      bwu$ = stbl("BBXDIR") + "bwu/"
:    else
:      bwu$ = stbl("BBXDIR") + "../utils/bwu/"
:    fi
2730 prefix pfx + " " + bwu$
2740 call "utaddr.wbb"; rem "ADDR frequently used pgms
2750 call "utcgi.wbb", env$, cgi$, errmsg$; if len(errmsg$) then goto err_session
2760 call "utses.wbb", 0, wases$, errmsg$; if len(errmsg$) then goto err_session
2770 glb$ = "SMSDIR"; gosub slash
2780 glb$ = "SMSAPP"; gosub slash
2790 glb$ = "SMSTMP"; gosub slash
2800 glb$ = "SMSLOG"; gosub slash
2810 glb$ = "BBXDIR"; gosub slash
2820 return

2900 bbweb_stbls:
2910 if 0 then
:      tmp$ = stbl("DOCTYPE", "<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01 Frameset//EN"" ""http://www.w3.org/TR/html4/frameset.dtd"">")
:    fi
2920 tmp$ = stbl("$title", "Golfer Login");
:    rem "$title global must be set to send tags before HTML$
2930 if pos(" " = wa00.c_body$) = 0 then
:      tmp$ = stbl("$bodyopt", "bgcolor='#" + wa00.c_body$ + "'")
:    fi
2940 if pos(" " <> wa00.weblogo$) then
:      tmp$ = stbl("$pagetop", "<p align='center'><img border='0' src='" + cvs(wa00.weblogo$, 2) + "'></p>")
:    fi
2950 tmp$ = stbl("$menubar", "#e0e0e0")
2960 tmp$ = stbl("$mailerror", cvs(wa00.erroremail$, 2))
2970 tmp$ = stbl("SMS_MODE", "web")
2980 othhead$ = "", othhead$ = stbl("$otherhead", err=2981)
2985 othhead$ = othhead$ + $0a$ + "<link rel='import' href='/import/icons.html'>" + $0a$
2986 othhead$ = othhead$ + $0a$ + "<meta name='viewport' content='width=device-width, initial-scale=1'>" + $0a$
2990 if pos(" " <> wa00.stylesheet$) then
:      othhead$ = othhead$ + "<link rel=stylesheet type=""text/css"" href=""/" + y.compcode$ + "/dynaweb/" + cvs(wa00.stylesheet$, 3) + """>" + $0a$
:    fi
3000 if 0 then
:      othhead$ = othhead$ + "<script type=""Text/JavaScript"" src=""/scripts/dwLib.js""></script>" + $0a$
:    fi
3010 if 0 then
:      othhead$ = othhead$ + "<script type=""Text/JavaScript"" src=""/scripts/" + y.compcode$ + ".js""></script>" + $0a$
:    fi
3020 tmp$ = stbl("$otherhead", othhead$)
3030 tmp$ = stbl("WMS_tblparms", " border=1 cellpadding=3 cellspacing=1")
3040 return

3100 session_sets:
3110 tmp$ = stbl("DSN", stbl("SCRIPT_NAME")); rem "Set Dynaweb Script Name
3120 tmp$ = stbl("HOVER", "onMouseOver=""HoverOn(this)"" onMouseOut=""HoverOff(this)""")
3130 tmp$ = stbl("WMS_STARTTIME", str(tim * 100))
3140 wases.sessionid$ = stbl("$sesid")
3150 debug$ = field(cgi$, "debug", err=3160); gosub debug
3160 tmp = fnlink("Back", "JavaScript:window.history.back(-1)", "")
3170 tmp = fnlink("Print", "JavaScript:window.print(1)", "")
3180 tmp = fnlink("Home", "Javascript:window.location=""[DSN]""", "")
3190 tmp = fnlink("Cancel", "Javascript:window.location=""[DSN]""", "")
3200 tmp$ = stbl("LEAGUESEL", "")
3210 tmp = fnlink("Logout", "JavaScript:window.location=""[DSN]?p=GW999""", "")
3220 tmp = fnlink("Close", "JavaScript:window.open(""/close.htm"",""_top"");", "")
3230 return

3300 slash:
3310 tmp$ = stbl(glb$)
3320 if tmp$(len(tmp$)) <> "/" then tmp$ = stbl(glb$, tmp$ + "/")
3330 return

3400 fil:
3410 fil = unt
3420 html$ = "Requested page has expired and is no longer available."
3430 open (fil, err=send)fil$
3440 fin$ = fin(fil), siz=dec(fin$(1,4))
3450 read record(fil, siz=siz)html$
3460 close (fil)
3470 if field(cgi$, "fmt", err=3480) = "e" then
:      call "CDW003", html$, "e";
:      html$ = "<pre style=""text-align:left"">" + html$ + "</pre>"
:    fi
3480 goto send

3500 debug:
3510 if pos("d" = debug$) then call "CDS173"; call "SW002", debug$, "d", ""
3520 if pos("e" = debug$) then call "CDW279", env$, html$, "ENV"
3530 if pos("g" = debug$) then call "CDW279", "!LIST", html$, "Global Variables"
3540 if pos("c" = debug$) then call "CDW279", cgi$, html$, "CGI"
3550 if pos("v" = debug$) then
:      call "CDW279", wases$, html$, "Session Variable WASES";
:      call "CDW279", wa00$, html$, "Web Control Variable WA00"
:    fi
3560 if pos("t" <> debug$) then goto send
3570 return

3600 dotrace:
3610 trfilnam$ = "tmp/,txt"; call "CDS064", y$, 32701, 0, 0, 0, 0, "STR", trfilnam$
3620 tmp$ = "", tmp$ = stbl("$otherhead", err=3630)
3630 tmp$ = stbl("$otherhead", tmp$ + "<script type='text/javascript'>window.open('/" + trfilnam$ + "','trace','status=0,menubar=0,scrollbars=1,toolbar=0,location=0,direction=0')</script>" + $0a$)
3640 return

3700 sendttmlink:
3770 call "CDS041", "CDS084", s084$, "YP"
3780 s084.name$ = "MAIL", s084.method$ = "N", s084.subject$ = "Tee Time Manager Access Link"
3790 s084.toemail$ = email$, s084.emailbcc$ = "webmaster@scoresandcharts.com"
3800 call "SW001", "XXGR01", 0, gr04.league$, gr01$, sw001$
3810 s084.fromname$ = "Tee Time Manager", s084.fromemail$ = "webmaster@scoresandcharts.com"
3820 call "CDS084", y$, y5$, u0, s084$, y6, y6$
3830 print (y6.ch)cvs(gr01.name$, 2)
3840 print (y6.ch)"Tee Time Manager Access Link: http://www.scoresandcharts.com/cgi-bin/ttm.cgi"
3850 call "CDS094", y6$, "E"
3860 loginmsg$ = "Check your email (and spam folder) for Tee Time Manager Access Link"
3870 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

8200 err_session:
8210 html$ = "GW010: Error getting proper Session information. <br> Please contact Excellware. <br> Error Message: " + errmsg$
8220 goto send

9000 eoj:
9006 run "CDW999"
