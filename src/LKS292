0100 REM "LKS292 - 08/31/98 Convert SKY file to tab-delimited format
0110 SETESC DT_ERR; SETERR DT_ERR
0120 TABLE FF 00 01 02 03 04 05 06 07 08 09 09
0130 TABLE FF 00 01 02 03 04 05 06 07 08 09 7E
0140 PRINT 'SB',@(1,2),"Input File Name:",'LF',"Output File Name:",'SF',
0150 CALL "CDS069",1,25,18,2,"+SKY input file name+","A",0,FILNAM$
0160 IF CTL=4 THEN GOTO EOJ
0170 CALL "CDS095",ERR=0150,1,FILNAM$,"YNN",REC$
0180 CALL "CDS087",1,"",F2$,F3$,"",F[ALL]
0190 IF F3$<>"SKY"
:       THEN
:          CALL "CDS069",0,0,0,0,"This program only works on SKY type files-";
:          GOTO 0150
0200 LET OUTFIL$="export/"+F2$+".txt";
:    REM "Don't use csv cause Excel will truncate at comma
0210 CALL "CDS069",1,50,18,3,"+output file name+","A",0,OUTFIL$
0220 IF CTL=4 THEN GOTO 0150
0230 LET Z$=""; CALL "CDS069",1,1,0,0,"Is information correct (Y/N)+","AYN",0,Z$
0240 IF CTL=4 THEN GOTO EOJ
0250 IF Z$="N" THEN GOTO 0150
0260 CALL "CDS095",ERR=0320,2,OUTFIL$,"DNX"
0270 CALL "CDS087",2,"","",G3$
0280 IF G3$<>"STR"
:       THEN
:          CLOSE (2);
:          CALL "CDS069",0,0,0,0,"Output file exists, and is not an STR type file-";
:          GOTO 0170
0290 CALL "CDS075",2
0300 LET Z$="";
:    CALL "CDS069",1,1,0,0,"OK to replace existing file "+OUTFIL$+" (Y/N)","AYN",0,Z$
0310 IF Z$="N" THEN GOTO 0210
0320 CALL "CDS064",ERR=EOJ,Y$,2,0,0,0,0,"STR",OUTFIL$
0330 CALL "CDS070",1,RECS
0340 LET DELTA=MAX(1,INT(RECS/100)),REC=0
0350 LET K$=KEY(1,END=0430)
0360 IF POS(" ">K$) OR POS("~"<K$)
:       THEN
:          CALL "CDS069",0,0,0,0,"This program does not work with binary data in the key-";
:          GOTO EOJ
0370 IF F[3] THEN GOSUB DATA
0380 LET K$=K$+$0D0A$
0390 WRITE RECORD(2)K$
0400 LET REC=REC+1
0410 IF MOD(REC,DELTA)=0 THEN PRINT @(8,23),100*REC/RECS:"###.0",
0420 GOTO 0350
0430 CLOSE (1)
0440 CLOSE (2)
0450 CALL "CDS069",0,0,0,0,"File export completed-"
0460 PRINT 'CF',
0470 GOTO 0150

1000 DATA:
1010 READ RECORD(1)REC$
1020 LET REC$=REC$(1,POS($00$<>REC$,-1))
1030 LET CHK$=TBL(REC$,TBL=0130)
1040 IF POS(" ">CHK$) OR POS("~"<CHK$)
:       THEN
:          CALL "CDS069",0,0,0,0,"This program does not work with binary data-";
:          GOTO EOJ
1050 LET K$=K$+$09$+TBL(REC$,TBL=0120)
1060 RETURN

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 RUN "CDS001"
