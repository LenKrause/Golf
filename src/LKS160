0100 REM "LKS160 - 09/30/96 Copy & Downsize Data Files
0110 REM "FILE 1 - FILELIST
0120 BEGIN
0130 DEF FNC$(TXT$,ROW)=@(INT((80-LEN(TXT$))/2),ROW)+TXT$
0140 SETESC DT_ERR; SETERR DT_ERR
0150 PRINT 'CS','SB',FNC$("Copy & Downsize Files",0),'LF',"Target Directory:",'LF',"Number of records to copy:",'SF',
0160 LET DRY$="";
:    CALL "CDS069",3,10,18,1,"+directory where files are to be copied+","A",0,DRY$
0170 IF CTL=4 THEN GOTO EOJ
0180 LET DRY$=CVS(DRY$,3)
0190 IF LEN(DRY$)<3 THEN GOTO 0160
0200 LET Z=POS("\"=DRY$); IF Z THEN LET DRY$(Z,1)="/"; GOTO 0200
0210 IF DRY$(LEN(DRY$))<>"/" THEN LET DRY$=DRY$+"/"
0220 MKDIR DRY$,ERR=0230
0230 CALL "CDS095",ERR=0160,DRY,DRY$,"YNX"
0240 READ RECORD(DRY,END=0270)FIL$
0250 IF FIL$<>"." AND FIL$<>".." THEN LET DRYFIL=DRYFIL+1
0260 GOTO 0240
0270 IF DRYFIL<>0
:       THEN
:          LET Z$="";
:          CALL "CDS069",1,1,0,0,"Note: target directory already contains "+STR(DRYFIL)+" files which will not be replaced, proceed with copy (Y/N)","AYN",0,Z$;
:          IF Z$="N"
:             THEN
:                GOTO 0160
0275 CALL "CDS069",10,999999,27,2,"+number of records to copy from each file+","###,###",NORECS
0276 IF CTL=4 THEN GOTO 0160
0280 CALL "CDS091",Y$
0290 CALL "CDS065",ERR=EOJ,Y$,"","DIRSRTSKYMKYINDSER",3,1
0300 PRINT @(0,3),'CE',
0310 LET L=2
0320 READ (1,END=0440)F$
0330 CALL "CDS095",ERR=0320,2,F$,"Y"
0340 CALL "CDS087",2,"",F2$,F3$,F4$,F[ALL],F5$,F6$
0350 CALL "CDS095",ERR=0390,3,DRY$+F2$,"DNX"; CLOSE (3)
0360 CALL "CDS069",0,0,0,0,"File "+DRY$+F2$+" already exists-"
0370 LET STS$="Not copied"
0380 GOTO 0410
0390 GOSUB COPY
0400 LET FILES=FILES+1
0410 IF L<20 THEN LET L=L+1 ELSE PRINT @(0,2),'LD',
0420 PRINT @(0,L),F5$,@(40,L),STS$
0430 GOTO 0320
0440 CALL "CDS069",0,0,0,0,STR(FILES)+" files copied-"
0450 GOTO EOJ

1000 COPY:
1010 IF F[6]<=100 OR (LEN(F2$)=6 AND POS(F2$(3,2)="SISDSKSMDD",2))
:       THEN
:          LET COPYRECS=F[6]
:       ELSE
:          LET COPYRECS=NORECS
1020 IF POS(F3$="SKYMKYINDSER",3) THEN LET RECS=0 ELSE LET RECS=COPYRECS+5
1030 CALL "CDS064",Y$,3,0,F[1],RECS,F[3],F3$+F6$,DRY$+F2$
1040 FOR I=1 TO COPYRECS
1050    IF POS(F3$="DIRSRTSKY",3) THEN LET K$=KEY(2,END=1080)
1060    IF F[3] OR F3$="SER" THEN READ RECORD(2,END=1080)REC$ ELSE READ (2)
1070    IF POS(F3$="MKYINDSER",3)
:          THEN
:             WRITE RECORD(3)REC$
:          ELSE
:             IF F[3]
:                THEN
:                   WRITE RECORD(3,KEY=K$)REC$
:                ELSE
:                   WRITE (3,KEY=K$)
1080 NEXT I
1090 LET STS$="Copied"
1100 RETURN

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 CLEAR
9015 CALL "CDS075",ERR=9020,1
9020 RUN "CDS001"
