0100 REM "GW051 - 07/13/14 Golf Point Ranking - PDF
0110 SETESC DT_ERR; SETERR DT_ERR
0120 IF CGI.FORMAT$="E" THEN RUN "GW053"; REM "Email format
0170 CALL "CDS095",GR03,"XXGR03","YNY",GR03$
0180 CALL "CDS095",GR04,"XXGR04","YNY",GR04$
0190 LET HALF=NUM(CGI.PERIOD$); REM "1/2/3=entire year
0200 LET GR03S=UNT
0210 SELECT (GR03S)GR03$ FROM Y.COMPCODE$+"GR03" WHERE GR03.LEAGUE$=WASES.LEAGUE$ AND DATE(GR03.DATE:"%Yl")=CGI.YEAR$
0220 READ RECORD(GR03S,END=0250)GR03$
0230 IF HALF=3 OR GR03.HALF=HALF THEN LET DATES$=DATES$+GR03.DATE$
0240 GOTO 0220
0250 CLOSE (GR03S)
0255 LET DATES=LEN(DATES$)/3
0256 IF DATES=0 THEN LET MSG$="No Rounds played"; RUN "GW011"
0260 LET DATES$=SSORT(DATES$,3)
0270 DIM DATES$[1:DATES]
0280 CALL "CDS095",GR05,"XXGR05","YNY",GR05$
0290 DIM GS$[1:100]
0300 LET GR05S=UNT
0310 SELECT (GR05S)GR05$ FROM Y.COMPCODE$+"GR05" WHERE GR05.LEAGUE$=WASES.LEAGUE$ AND POS(GR05.DATE$=DATES$,3) SORTBY GR05.ID$+GR05.DATE$
0320 READ RECORD(GR05S,END=0380)GR05$
0330 IF LEN(CG$) AND CG$(1,4)<>GR05.ID$
:       THEN
:          IF LEN(CG$)
:             THEN
:                LET IDS=IDS+1,GS$[IDS]=LASTFLT$+STR(LASTHC:"##")+CG$
:          FI;
:          LET CG$="",LASTHC=0
0334 LET LASTFLT$=GR05.FLT$,LASTHC=GR05.HC
0335 IF GR05.SCORE=0
:       THEN
:          GOTO 0320;
:          REM "Need lasthc from last week in year even if golfer did not play
0340 IF LEN(CG$)=0 THEN LET CG$=GR05.ID$
0350 LET Z=POS(STR(GR05.DATE)=DATL$,7);
:    IF Z=0
:       THEN
:          LET DATL$=DATL$+STR(GR05.DATE)
0360 LET CG$=CG$+STR(GR05.DATE)+STR(GR05.POINTS:"##.0")
0370 GOTO 0320
0380 CLOSE (GR05S)
0390 IF LEN(CG$) THEN LET IDS=IDS+1,GS$[IDS]=LASTFLT$+STR(LASTHC:"##")+CG$
0392 IF IDS=0 THEN LET MSG$="No Rounds Played"; RUN "GW011"
0393 CALL "CDS041","CDS084",S084$,"YY"
0394 LET S084.MODE$="E",S084.NAME$="APDF"
0395 CALL "CDS084",ERR=EOJ,Y$,Y5$,U0,S084$,Y6,Y6$
0399 LET CG$=""
0400 LET DATL$=SSORT(DATL$,7)
0410 LET WKS=LEN(DATL$)/7
0420 DIM PTS[1:IDS,1:WKS+5];
:    REM "+1 weeks, +2 total pts, +3 min pts, +4 min wk#, +5 HC
0430 FOR ID=1 TO IDS
0440    LET CG$=GS$[ID],TIMES=0,TOT=0,MINPTS=99
0450    LET PTS[ID,WKS+5]=NUM(CG$(2,2))
0460    FOR J=8 TO LEN(CG$) STEP 11
0470       LET WK=(POS(CG$(J,7)=DATL$,7)-1)/7+1
0480       LET PTS[ID,WK]=NUM(CG$(J+7,4)),TIMES=TIMES+1,TOT=TOT+PTS[ID,WK]
0490       IF PTS[ID,WK]<MINPTS THEN LET MINPTS=PTS[ID,WK],MINWKNO=WK
0500    NEXT J
0510    LET PTS[ID,WKS+1]=TIMES
0520    LET PTS[ID,WKS+2]=TOT
0530    IF TIMES=WKS AND CGI.DROP$="Y"
:          THEN
:             LET PTS[ID,WKS+3]=MINPTS,PTS[ID,WKS+4]=MINWKNO
0540    LET SEQ$=SEQ$+CG$(1,1)+STR(200-(PTS[ID,WKS+2]-PTS[ID,WKS+3]):"###.0")+STR(ID:"##0")
0550 NEXT ID
0560 LET SEQ$=SSORT(SEQ$,9)
0570 LET ROW=150
0580 CALL "CDS254",Y6$,"T1200C,"+STR(ROW)+",3,1,14,,4148",WASES.LEAGUENAME$
0590 LET ROW=ROW+60;
:    CALL "CDS254",Y6$,"T1200C,"+STR(ROW)+",3,1,14,,4148","Point Ranking - "+FNH$(HALF)
0600 CALL "CDS454::SETUP",150,300,"450,59,"+FILL((WKS+2)*4,"102,"),FILL(3*(4+LEN(SEQ$)/9),"50,")
0610 FOR SEQP=1 TO LEN(SEQ$) STEP 9
0620    IF SEQP=1 OR SEQ$(SEQP,1)<>FLT$ THEN GOSUB NEWFLT
0630    LET ID=NUM(SEQ$(SEQP+6,3))
0640    READ RECORD(GR04,KNUM=0,KEY=WASES.LEAGUE$+GS$[ID](4,4))GR04$
0650    LET COLNO=1
0660    LET ROWNO=ROWNO+1;
:       CALL "CDS454",Y6$,COLNO,ROWNO,"L",CVS(GR04.FIRSTNAME$,2)+" "+CVS(GR04.LASTNAME$,2)
0670    LET COLNO=COLNO+1; CALL "CDS454",Y6$,COLNO,ROWNO,"C",STR(PTS[ID,WKS+5])
0680    FOR WK=1 TO WKS
0685       IF WK=PTS[ID,WKS+4]
:             THEN
:                LET SHADE=1,COLOR$="red"
:             ELSE
:                LET SHADE=0,COLOR$=""
0690       LET COLNO=COLNO+1;
:          CALL "CDS454",Y6$,COLNO,ROWNO,"R",STR(PTS[ID,WK]:"###.0"),SHADE,0,0,0,0,0,COLOR$
0700    NEXT WK
0710    LET COLNO=COLNO+1; CALL "CDS454",Y6$,COLNO,ROWNO,"C",STR(PTS[ID,WKS+1])
0720    LET COLNO=COLNO+1;
:       CALL "CDS454",Y6$,COLNO,ROWNO,"R",STR(PTS[ID,WKS+2]-PTS[ID,WKS+3]:"###.0")
0730 NEXT SEQP
0740 CALL "CDS094",Y6$,"E"
0745 WAIT 2
0750 LET HTML$="<script>window.open('"+S084.FILEOUT$(POS("/tmp/"=S084.FILEOUT$))+"');</script>"
0760 RUN "GW011"

1000 NEWFLT:
1010 LET ROWNO=ROWNO+1
1020 LET FLT$=SEQ$(SEQP,1)
1030 LET TXT$=FLT$+" Flight"
1040 CALL "CDS454",Y6$,1,ROWNO,"C",TXT$,1,2+WKS+2,1
1050 LET ROWNO=ROWNO+1,COLNO=0
1060 LET COLNO=COLNO+1; CALL "CDS454",Y6$,COLNO,ROWNO,"L","Name",1
1070 LET COLNO=COLNO+1; CALL "CDS454",Y6$,COLNO,ROWNO,"C","HC",1
1080 FOR DATP=1 TO LEN(DATL$) STEP 7
1090    LET TXT$=DATE(NUM(DATL$(DATP,7)):" %M/%Dz")
1100    LET COLNO=COLNO+1; CALL "CDS454",Y6$,COLNO,ROWNO,"R",TXT$,1
1110 NEXT DATP
1120 LET COLNO=COLNO+1; CALL "CDS454",Y6$,COLNO,ROWNO,"C","Wks",1
1130 LET COLNO=COLNO+1; CALL "CDS454",Y6$,COLNO,ROWNO,"R","Pts",1
1140 RETURN
7010 DEF FNH$(HALF)
7020 SWITCH HALF
7030 CASE 1; LET OUT$="1st half "; BREAK
7040 CASE 2; LET OUT$="2nd half "; BREAK
7045 CASE 3; LET OUT$=""; BREAK
7050 SWEND
7060 RETURN OUT$+CGI.YEAR$
7070 FNEND

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 RUN "CDW999"
