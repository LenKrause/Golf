0100 rem "GW051 - 07/13/14 Golf Point Ranking - PDF
0110 setesc 8000; seterr 8000
0120 if cgi.format$ = "E" then run "GW053"; rem "Email format
0170 call "CDS095", gr03, "XXGR03", "YNY", gr03$
0180 call "CDS095", gr04, "XXGR04", "YNY", gr04$
0190 half = num(cgi.period$); rem "1/2/3=entire year
0200 gr03s = unt
0210 select (gr03s)gr03$ from y.compcode$ + "GR03" where gr03.league$ = wases.league$ and date(gr03.date:"%Yl") = cgi.year$
0220 read record(gr03s, end=0250)gr03$
0230 if half = 3 or gr03.half = half then dates$ = dates$ + gr03.date$
0240 goto 0220
0250 close (gr03s)
0255 dates = len(dates$) / 3
0256 if dates = 0 then msg$ = "No Rounds played"; run "GW011"
0260 dates$ = ssort(dates$, 3)
0270 dim dates$[1:dates]
0280 call "CDS095", gr05, "XXGR05", "YNY", gr05$
0290 dim gs$[1:100]
0300 gr05s = unt
0310 select (gr05s)gr05$ from y.compcode$ + "GR05" where gr05.league$ = wases.league$ and pos(gr05.date$ = dates$, 3) sortby gr05.id$ + gr05.date$
0320 read record(gr05s, end=0380)gr05$
0330 if len(cg$) and cg$(1,4) <> gr05.id$ then
:      if len(cg$) then
:        ids = ids + 1, gs$[ids] = lastflt$ + str(lasthc:"##") + cg$
:      fi;
:      cg$ = "", lasthc = 0
:    fi
0334 lastflt$ = gr05.flt$, lasthc = gr05.hc
0335 if gr05.score = 0 then
:      goto 0320;
:      rem "Need lasthc from last week in year even if golfer did not play
0340 if len(cg$) = 0 then cg$ = gr05.id$
0350 z = pos(str(gr05.date) = datl$, 7);
:    if z = 0 then
:      datl$ = datl$ + str(gr05.date)
:    fi
0360 cg$ = cg$ + str(gr05.date) + str(gr05.points:"##.0")
0370 goto 0320
0380 close (gr05s)
0390 if len(cg$) then ids = ids + 1, gs$[ids] = lastflt$ + str(lasthc:"##") + cg$
0392 if ids = 0 then msg$ = "No Rounds Played"; run "GW011"
0393 call "CDS041", "CDS084", s084$, "YY"
0394 s084.mode$ = "E", s084.name$ = "APDF"
0395 call "CDS084", err=eoj, y$, y5$, u0, s084$, y6, y6$
0399 cg$ = ""
0400 datl$ = ssort(datl$, 7)
0410 wks = len(datl$) / 7
0420 dim pts[1:ids, 1:wks + 5];
:    rem "+1 weeks, +2 total pts, +3 min pts, +4 min wk#, +5 HC
0430 for id = 1 to ids
0440   cg$ = gs$[id], times = 0, tot = 0, minpts = 99
0450   pts[id, wks + 5] = num(cg$(2,2))
0460   for j = 8 to len(cg$) step 11
0470     wk = (pos(cg$(j, 7) = datl$, 7) - 1) / 7 + 1
0480     pts[id, wk] = num(cg$(j + 7, 4)), times = times + 1, tot = tot + pts[id, wk]
0490     if pts[id, wk] < minpts then minpts = pts[id, wk], minwkno = wk
0500   next j
0510   pts[id, wks + 1] = times
0520   pts[id, wks + 2] = tot
0530   if times = wks and cgi.drop$ = "Y" then
:        pts[id, wks + 3] = minpts, pts[id, wks + 4] = minwkno
:      fi
0540   seq$ = seq$ + cg$(1,1) + str(200 - (pts[id, wks + 2] - pts[id, wks + 3]):"###.0") + str(id:"##0")
0550 next id
0560 seq$ = ssort(seq$, 9)
0570 row = 150
0580 call "CDS254", y6$, "T1200C," + str(row) + ",3,1,14,,4148", wases.leaguename$
0590 row = row + 60;
:    call "CDS254", y6$, "T1200C," + str(row) + ",3,1,14,,4148", "Point Ranking - " + fnh$(half)
0600 call "CDS454::SETUP", 150, 300, "450,59," + fill((wks + 2) * 4,"102,"), fill(3 * (4 + len(seq$) / 9), "50,")
0610 for seqp = 1 to len(seq$) step 9
0620   if seqp = 1 or seq$(seqp, 1) <> flt$ then gosub newflt
0630   id = num(seq$(seqp + 6, 3))
0640   read record(gr04, knum=0, key=wases.league$ + gs$[id](4,4))gr04$
0650   colno = 1
0660   rowno = rowno + 1;
:      call "CDS454", y6$, colno, rowno, "L", cvs(gr04.firstname$, 2) + " " + cvs(gr04.lastname$, 2)
0670   colno = colno + 1; call "CDS454", y6$, colno, rowno, "C", str(pts[id, wks + 5])
0680   for wk = 1 to wks
0685     if wk = pts[id, wks + 4] then
:          shade = 1, color$ = "red"
:        else
:          shade = 0, color$ = ""
:        fi
0690     colno = colno + 1;
:        call "CDS454", y6$, colno, rowno, "R", str(pts[id, wk]:"###.0"), shade, 0, 0, 0, 0, 0, color$
0700   next wk
0710   colno = colno + 1; call "CDS454", y6$, colno, rowno, "C", str(pts[id, wks + 1])
0720   colno = colno + 1;
:      call "CDS454", y6$, colno, rowno, "R", str(pts[id, wks + 2] - pts[id, wks + 3]:"###.0")
0730 next seqp
0740 call "CDS094", y6$, "E"
0745 wait 2
0750 html$ = "<script>window.open('" + s084.fileout$(pos("/tmp/" = s084.fileout$)) + "');</script>"
0760 run "GW011"

1000 newflt:
1010 rowno = rowno + 1
1020 flt$ = seq$(seqp, 1)
1030 txt$ = flt$ + " Flight"
1040 call "CDS454", y6$, 1, rowno, "C", txt$, 1, 2 + wks + 2, 1
1050 rowno = rowno + 1, colno = 0
1060 colno = colno + 1; call "CDS454", y6$, colno, rowno, "L", "Name", 1
1070 colno = colno + 1; call "CDS454", y6$, colno, rowno, "C", "HC", 1
1080 for datp = 1 to len(datl$) step 7
1090   txt$ = date(num(datl$(datp, 7)):" %M/%Dz")
1100   colno = colno + 1; call "CDS454", y6$, colno, rowno, "R", txt$, 1
1110 next datp
1120 colno = colno + 1; call "CDS454", y6$, colno, rowno, "C", "Wks", 1
1130 colno = colno + 1; call "CDS454", y6$, colno, rowno, "R", "Pts", 1
1140 return
7010 def fnh$(half)
7020 switch half
7030   case 1; out$ = "1st half "; break
7040   case 2; out$ = "2nd half "; break
7045   case 3; out$ = ""; break
7050 swend
7060 return out$ + cgi.year$
7070 fnend

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 run "CDW999"
