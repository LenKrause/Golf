rem "GW062.bbj - Golf Weekly Email - cron version

use ::ttm.bbj::ttm
use ::DTHtmlTable.bbj::DTHtmlTable
use java.util.HashMap

declare HashMap emails!

setesc DT_ERR; seterr DT_ERR
REM  playdate = 0 for final year-end results
enter y$, league$, playdate, hcListPdf$, skinPdf$, msg$, err=*next

if len(y$) = 0 then
  call "CDS091", y$
  league$ = "SAW"
  playdate = 0 
  hcListPdf$ = ""
  skinPdf$   = ""
  msg$ = ""
fi

if league$ = "SAW" then aid$ = "lk01"

if playdate then
  emails! = new HashMap()
  rem  Include all in group
  call "SW010", "XXGR09", 0, "", "email", emails$, "", "", "rec.aid$ = """ + aid$ + """ and rec.active$ = ""Y"""
  if len(emails$) then
    for i = 1 to len(emails$) step 50
      emails!.put(cvs(emails$(i, 50), 2), "")
    next i
  fi
fi

tmp$ = stbl("WMS_CDW000", "D")
y.passparm$ = "E"

rem  Get League Points Ranking by Flight
call "GW054.bbj", y$, league$, playdate + 0, flts$, html$[all]

call "CDS041", "CDS084", s084$, "YP"
sent = 0
s084.mode$      = "E"
s084.name$      = "MAIL"
s084.emailtype$ = "H"
s084.fromname$  = "ScoresAndCharts.com"
s084.fromemail$ = "webmaster@scoresandcharts.com"
call "CDS095", gr03, "XXGR03", "YNY", gr03$
call "CDS095", gr04, "XXGR04", "YNY", gr04$
call "CDS095", gr05, "XXGR05", "YNY", gr05$, gr05k$
call "SW001", "XXGR01", 0, league$, gr01$, sw001$
if playdate then call "SW001", "XXGR03", 0, league$ + bin(playdate, 3), gr03$, sw001$
call "SW001", "XXGR08", 0, gr01.id$, gr08$, sw001$
s084.subject$ = gr01.name$

declare DTHtmlTable table!
declare BBjString   style!

gr05s = unt
select (gr05s)gr05$ from y.compcode$ + "GR05" where gr05.league$ = league$ and gr05.date = playdate

repeat
  read record(gr05s, end=*break)gr05$
  read record(gr04, knum=0, key=gr05.league$ + gr05.id$)gr04$
  if pos(" " <> gr04.email$) = 0 then continue
  table! = new DTHtmlTable(BBjAPI.TRUE)  
  html$ = "", tab$ = "", row$ = ""
  if gr03.half = 1 or gr03.half = 2 then
    table!.addHeaderCell(gr01.name$, "cs3")
    table!.addRow()
  fi
  dim gr05a$:fattr(gr05$)
  gr05a = unt
  open (gr05a)y.compcode$ + "GR05"
  read (gr05a, knum=0, key=gr05.league$ + gr05.id$ + bin(-1, 3), dom=*next)
  repeat
    readrecord (gr05a, end=*next)gr05a$
    if gr05a.league$ <> gr05.league$ or gr05a.id$ <> gr05.id$ then break
    if gr05a.score = 0 then continue
    call "SW001", "XXGR03", 0, gr05a.league$ + bin(gr05a.date, 3), gr03a$, sw001$
    if ! sw001.onfile then break
    if gr03a.half = 1 or gr03a.half = 2 then break
  until 0
  if gr05a.league$ = gr05.league$ and gr05a.id$ = gr05.id$ and (gr03a.half = 1 or gr03a.half = 2) then
    wk = 1
    tit$ = "Last Round"
    gosub week
  fi
  if gr03.half = 1 or gr03.half = 2 then
    gr05a$ = gr05$
    wk = 2
    tit$ = "Next Round - " + iff(gr03.NINE$ = "F", "FRONT", "BACK") +  " NINE"
    gosub week
  fi
  table!.addTable()
  html$ = table!.getHtml()
  
  if len(html$[5]) then html$ = html$ + "<br>" + html$[5] + "<br>"; rem  Weekly Prizes
  
  z = pos(gr05.flt$ = flts$)
  if playdate and z and len(html$[z]) then html$ = html$ + html$[z] + "<br>"; rem  Flight Point Ranking
  
  if len(html$[9]) then html$ = html$ + html$[9] + "<br>"; rem  Year Long Skins Game
   
  html$ = "<div style='font-family:verdana,tahoma,sans-serif'>" + html$ + "</div>"
  html$ = html$ + "<div style='font-size:0;height:5px;'></div>"
  gosub chart
  gosub bestrounds
  s084.toname$  = fnn$(gr04$)
  s084.toemail$ = cvs(gr04.email$, 2)
  if playdate then emails!.remove(s084.toemail$, err=*next); rem  Remove anyone with a match
  if 1 then s084.toemail$ = "len@excellware.com"
  if 0 then call "CDW999", html$; run "GW011"
  call "CDS084", y$, y5$, u0, s084$, y6, y6$
  style! = table!.getStyle()
  style! = style!.replace("font-size: 15px;", "font-size:10pt;")
  print (y6.ch)"<head>" + style! + "</head>" + $0a$
  if 0 then note$ = cgi.note$
  if 0 then call "SW002", note$, $e2809d$, """"
  if 0 then call "SW002", note$, $e28099$, "'"
  if 0 then cgi.note$ = note$
  if 0 then call "SW002", note$, $0d0a$, "<br>"
  if jul(0,0,0) < jul(2023,8,2) then note$ = "<p>Cookout after golf this week, Wednesday August 2</p>"
  if len(note$) then print (y6.ch)note$
  if 0 then call "CDS257", y$, league$ + "e", "", "XXGR02", "W", 0, 0, 0, 0, "", "", "", "", cgi.note$
  link$ = ttm.getLink(gr04.EMAIL$)
  if len(link$) then print (y6.ch)"<p><strong>Click <a href='" + link$ + "'>here</a> to select your tee time.</p>"
  if 0 then print (y6.ch)"<p><strong>Please call or email your opponent if you can't play this week.</strong></p>"
  if 0 then if pos(" " <> gr08.name$) and pos(" " <> gr08.phone$) then print (y6.ch)"<p><strong>Call " + cvs(gr08.name$, 2) + " at " + gr08.phone$ + " if you can't play and did not get in touch with your opponent.</strong></p>"
  print (y6.ch)"<p>You can login to <a href='http://scoresandcharts.com'>ScoresAndCharts.com</a> anytime to:</p>"
  print (y6.ch)"<ul>"
  print (y6.ch)"<li>Enter scorecard,</li>"
  print (y6.ch)"<li>View results of last round,</li>"
  print (y6.ch)"<li>View next opponent info,</li>"
  print (y6.ch)"<li>View point ranking,</li>"
  print (y6.ch)"<li>Change information including your email address &amp; phone#, and</li>"
  print (y6.ch)"<li>View latest charts and your best 10 rounds!</li>"
  print (y6.ch)"</ul>"
  print (y6.ch)html$
  if len(hcListPdf$) then call "CDS184", y$, y6$, hcListPdf$, "HandicapListing.pdf"
  if playdate and len(skinPdf$) and pos(gr04.id$ = "KRALSTEJ", 4) then call "CDS184", y$, y6$, skinPdf$, "Skins Posting Sheet.pdf"
  call "CDS094", y6$, "e"
  sent = sent + 1
  if 1 then ts = ts + 1; if ts = 2 then goto eoj
until 0

rem  Send any guest emails
if playdate and emails!.size() then
  declare BBjVector emv!
  emv! = DT.getMapKeys(emails!)
  for i = 0 to emv!.size() - 1
    email$ = emv!.get(i).toString()
    link$ = ttm.getLink(email$)
    if len(link$) then
      s084.toemail$  = email$
      if 0 s084.emailbcc$ = "len@excellware.com"
      call "CDS084", y$, y5$, u0, s084$, y6, y6$
      print (y6.ch)"<p>Although you do not have a match scheduled for this week"
      print (y6.ch)"<br>You can join us as a guest by clicking this <a href='" + link$ + "'>link</a> to select a tee time"
      print (y6.ch)"<br>If you plan to play 18 holes, then select both an early and a late tee time."
      call "CDS094", y6$, "e"
      guests = guests + 1
    fi
  next i
fi

goto eoj

week:
  dim gr04a$:fattr(gr04$)
  read record(gr04, knum=0, key=gr05.league$ + gr05a.opp$, dom=*next)gr04a$
  dim gr05b$:fattr(gr05$)
  read record(gr05, knum=0, key=gr05.league$ + gr05a.opp$ + gr05a.date$, dom=*next)gr05b$
  table!.addHeaderCell(tit$, "cs3")
  table!.addRow()
  table!.addCell(date(gr05a.date:"%Ds %Mz/%Dz/%Yz"), "r")
  table!.addCell(fnn$(gr04$), "c")
  table!.addCell(fnn$(gr04a$), "c")
  table!.addRow()
  if wk = 1 then
    table!.addCell("Gross", "r")
    table!.addCell(fns$(gr05a.score), "c")
    table!.addCell(fns$(gr05b.score), "c")
    table!.addRow()
  fi
  table!.addCell("Handicap", "r")
  table!.addCell(fns$(gr05a.hc), "c")
  table!.addCell(fns$(gr05b.hc), "c")
  table!.addRow()
  if wk = 1 then
    table!.addCell("Net", "r")
    table!.addCell(fns$(gr05a.net), "c")
    table!.addCell(fns$(gr05b.net), "c")
    table!.addRow()
  fi
  if wk = 1 then
    table!.addCell("Putts", "r")
    table!.addCell(fns$(gr05a.putts), "c")
    table!.addCell(fns$(gr05b.putts), "c")
    table!.addRow()
  fi
  if wk = 1 then
    table!.addCell("Points", "r")
    table!.addCell(fns$(gr05a.points), "c")
    table!.addCell(fns$(gr05b.points), "c")
    table!.addRow()
  fi
  if wk = 2 then
    table!.addCell("Email", "r")
    table!.addCell(gr04.email$, "c")
    table!.addCell("<a href='mailto:" + cvs(gr04a.email$, 3) + "?subject=Golf'>" + gr04a.email$ + "</a>", "c&")
    table!.addRow()
  fi
  if wk = 2 then
    table!.addCell("Phone#", "r")
    table!.addCell(gr04.phone$, "c")
    table!.addCell(gr04a.phone$, "c")
    table!.addRow()
  fi
return

functions:
def fnn$(rec$)
  dim rec1$:fattr(gr04$); rec1$ = rec$
  nam$ = cvs(cvs(rec1.firstname$, 2) + " " + cvs(rec1.lastname$, 2), 1)
  return nam$
fnend

def fns$(val)
  if val then out$ = str(val) else out$ = ""
  return out$
fnend

def fnb$(txt$)
  while pos(" " = txt$)
    txt$(pos(" " = txt$), 1) = "+"
  wend
  return txt$
fnend

chart:
tmp$ = stbl("WMS_CDW000_RC", ""); rem "Disable odd/even row background
dim r$:"S:C(2*),STOT:B,SRND:B,SMIN:B,SMAX:B,P:C(2*),PTOT:B,PRND:B,PMIN:B,PMAX:B,H:C(2*),HTOT:B,HRND:B,HMIN:B,HMAX:B"
r.smin = 99, r.pmin = 99, r.hmin = 99
dim l$:fattr(r$); l$ = r$
dim yy$:fattr(r$); yy$ = r$
chxl0$ = "", chxl1$ = "", chxl2$ = "", chxl0y$ = "", yscores$ = "", yputts$ = "", yhc$ = "", gr05c = unt, tots = 0, totp = 0, toth = 0, rndp = 0, rndh = 0, mins = 99, maxs = 0, minp = 99, maxp = 0, minh = 99, maxh = 0, cy = 0, ytots = 0, ytotp = 0, ytoth = 0, yrnds = 0, yrndp = 0, yrndh = 0, ys$ = "", yp$ = "", yh$ = "", yrs = 0
dim gr05c$:fattr(gr05$)
select (gr05c)gr05c$ from y.compcode$ + "GR05" where gr05c.league$ = gr05.league$ and gr05c.id$ = gr04.id$ and gr05c.score
repeat
  read record(gr05c, end=*break)gr05c$
  if l.srnd < 20 then gosub latest
  ty = num(date(gr05c.date:"%Yl"))
  if ty <> cy and cy then gosub newyr
  cy = ty, ytots = ytots + gr05c.score, yrnds = yrnds + 1
  if gr05c.putts then ytotp = ytotp + gr05c.putts, yrndp = yrndp + 1
  if gr05c.hc then ytoth = ytoth + gr05c.hc, yrndh = yrndh + 1
until 0
close (gr05c)
gosub newyr
r$ = l$;  gosub adj;  l$ = r$
r$ = yy$; gosub adj; yy$ = r$
if l.srnd then
  avgv = round(l.stot / l.srnd, 1)
  tit$ = "Scores"
  minv = l.smin
  maxv = l.smax
  delta = 5
  val$ = l.s$
  clr$ = "3072F3"
  gosub dochl
fi
if yy.srnd then
  avgv = round(yy.stot / yy.srnd, 1)
  tit$ = "Scores"
  minv = yy.smin
  maxv = yy.smax
  delta = 1
  val$ = yy.s$
  clr$ = "3072F3"
  gosub dochy
fi
if l.srnd or yy.srnd then call "CDW000", tab$, "r style='padding:0px'", row$
if l.prnd then
  avgv = round(l.ptot / l.prnd, 1)
  tit$ = "Putts"
  minv = l.pmin - 1
  maxv = l.pmax + 1
  delta = 1
  val$ = l.p$
  clr$ = "33cc33"
  gosub dochl
fi
if yy.prnd then
  avgv = round(yy.ptot / yy.prnd, 1)
  tit$ = "Putts"
  minv = yy.pmin - 1
  maxv = yy.pmax + 1
  delta = 1
  val$ = yy.p$
  clr$ = "33cc33"
  gosub dochy
fi
if l.prnd or yy.prnd then call "CDW000", tab$, "r style='padding:0px'", row$
if l.hrnd then
  avgv = round(l.htot / l.hrnd, 1)
  tit$ = "Handicap"
  minv = l.hmin - 1
  maxv = l.hmax + 1
  delta = 1
  val$ = l.h$
  clr$ = "ff9900"
  gosub dochl
fi
if yy.hrnd then
  avgv = round(yy.htot / yy.hrnd, 1)
  tit$ = "Handicap"
  minv = yy.hmin - 1
  maxv = yy.hmax + 1
  delta = 1
  val$ = yy.h$
  clr$ = "ff9900"
  gosub dochy
fi
if l.hrnd or yy.hrnd then call "CDW000", tab$, "r style='padding:0px'", row$
call "CDW000", html$, "t style='border:0px'", tab$
return

adj:
  if r.srnd then r.s$ = "_" + r.s$ + ",_", r.p$ = "_" + r.p$ + ",_", r.h$ = "_" + r.h$ + ",_"
  r.smin = r.smin - mod(r.smin, 5)
  r.smax = r.smax - mod(r.smax, 5) + 5
  r.pmin = r.pmin - mod(r.pmin, 1) - 0
  r.pmax = r.pmax - mod(r.pmax, 1) + 0
  r.hmin = r.hmin - mod(r.hmin, 1) - 0
  r.hmax = r.hmax - mod(r.hmax, 1) + 0
return

latest:
  chxl0$ = "|" + date(gr05c.date:"%Mz") + chxl0$
  chxl1$ = "|" + date(gr05c.date:"%Dz") + chxl1$
  chxl2$ = "|" + date(gr05c.date:"%Yz") + chxl2$
  r$ = l$, s = gr05c.score, p = gr05c.putts, h = gr05c.hc; gosub tots; l$ = r$
return

tots:
  r.s$ = "," + str(s) + r.s$
  r.stot = r.stot + s
  r.srnd = r.srnd + 1
  r.smin = min(r.smin, s)
  r.smax = max(r.smax, s)
  if p then r.p$ = "," + str(p) + r.p$; r.ptot = r.ptot + p; r.prnd = r.prnd + 1; r.pmin = min(r.pmin, p); r.pmax = max(r.pmax, p) else r.p$ = ",_" + r.p$
  if h then r.h$ = "," + str(h) + r.h$; r.htot = r.htot + h; r.hrnd = r.hrnd + 1; r.hmin = min(r.hmin, h); r.hmax = max(r.hmax, h) else r.h$ = ",_" + r.h$
return

newyr:
  if yrnds = 0 then return
  chxl0y$ = "|" + str(cy) + chxl0y$
  r$ = yy$, s = round(ytots / yrnds, 1), p = round(ytotp / max(1, yrndp), 1), h = round(ytoth / max(1, yrndh), 1); gosub tots; yy$ = r$
  ytots = 0, yrnds = 0, ytotp = 0, yrndp = 0, ytoth = 0, yrndh = 0
  yrs = yrs + 1
return

dochl:
  ch$ = "<img src='http://chart.apis.google.com/chart"
  ch$ = ch$ + "?chf=bg,s,FFFF99"
  ch$ = ch$ + "&chxl=0:|" + chxl0$ + "||1:|" + chxl1$ + "||2:|" + chxl2$ + "||4:|Avg+" + str(avgv)
  ch$ = ch$ + "&chxp=4," + str(avgv)
  ch$ = ch$ + "&chxr=3," + str(minv) + "," + str(maxv) + "," + str(delta) + "|4," + str(minv) + "," + str(maxv)
  ch$ = ch$ + "&chxs=3,676767,11.5,0,lt,676767|4,FF0000,11.5,0,lt,FF0000"
  ch$ = ch$ + "&chxt=x,x,x,y,r"
  ch$ = ch$ + "&chxtc=3,-600|4,-600"
  ch$ = ch$ + "&chs=500x250"
  ch$ = ch$ + "&cht=lxy"
  ch$ = ch$ + "&chco=" + clr$
  ch$ = ch$ + "&chds=" + str(minv) + "," + str(maxv) + "," + str(minv) + "," + str(maxv)
  ch$ = ch$ + "&chd=t:-1|" + val$
  ch$ = ch$ + "&chls=3"
  ch$ = ch$ + "&chma=25,60,0,50"
  ch$ = ch$ + "&chm=s," + clr$ + ",0,-1,5"
  ch$ = ch$ + "&chtt=" + tit$ + "+-+Latest+Rounds'"
  gosub rep
  ch$ = ch$ + " style='border:1px solid #c0c0c0' width=500 height=250>"
  call "CDW000", row$, "d style='padding:0px'", ch$
return

dochy:
  wid = 230 + (max(0, yrs - 3)) * 40
  ch$ = "<img src='http://chart.apis.google.com/chart"
  ch$ = ch$ + "?chf=bg,s,FFFF99"
  ch$ = ch$ + "&chxl=0:|" + chxl0y$ + "||2:|Avg+" + str(avgv)
  ch$ = ch$ + "&chxp=2," + str(avgv)
  ch$ = ch$ + "&chxr=1," + str(minv) + "," + str(maxv) + "," + str(delta) + "|2," + str(minv) + "," + str(maxv)
  ch$ = ch$ + "&chxs=1,676767,11.5,0,lt,676767|2,FF0000,11.5,0,lt,FF0000"
  ch$ = ch$ + "&chxt=x,y,r"
  ch$ = ch$ + "&chxtc=1,-600|2,-600"
  ch$ = ch$ + "&chs=" + str(wid) + "x250"
  ch$ = ch$ + "&cht=lxy"
  ch$ = ch$ + "&chco=" + clr$
  ch$ = ch$ + "&chds=" + str(minv) + "," + str(maxv) + "," + str(minv) + "," + str(maxv)
  ch$ = ch$ + "&chd=t:-1|" + val$
  ch$ = ch$ + "&chls=3"
  ch$ = ch$ + "&chma=25,60,0,50"
  ch$ = ch$ + "&chm=s," + clr$ + ",0,-1,5"
  ch$ = ch$ + "&chtt=" + tit$ + "+-+Year+by+Year'"
  gosub rep
  ch$ = ch$ + " style='border:1px solid #c0c0c0' width=" + str(wid) + " height=250>"
  call "CDW000", row$, "d style='padding:0px'", ch$
return

rep:
  call "SW002", ch$, "|", "%7c"
  if 0 then call "SW002", ch$, ":", "%3a"
return

bestrounds:
  call "CDS041", "CDW038", w038$, "YY"
  w038.useclass$ = "N"
  y.compname$ = ""
  call "CDW038", y$, "Best Rounds", y5a$, htmlbest$, w038$, "XXGR05", 0, "", "", "GR05.DATE|SCORE|HC|NET|PUTTS", "GR05.LEAGUE$=""" + league$ + """ AND GR05.ID$=""" + gr04.id$ + """ and GR05.NET", "REC.SCORE$+NOT(REC.DATE$)", "", 10
  html$ = html$ + "<br>" + htmlbest$
return

dt_err:
call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
retry

eoj:
tmp$ = stbl("!CLEAR", "WMS_CDW000")
html$ = ""
if sent then
  msg$ = "Job Completed, Sent emails to " + str(sent) + " players with a match"
  if guests then msg$ = msg$ + " and " + str(guests) + " guests"
else
  msg$ = "Select 'Golf Score Posting Sheets' before 'Weekly Email'"
fi
if tcb(13) then exit
run "CDS001"