0100 REM "GW062.bbj - 04/03/21 Golf Weekly Email - cron version
0110 SETESC 8000; SETERR 8000
0112 enter y$, league$, playdate, pdf$, msg$, err=*next
0113 if len(y$) = 0 then call "CDS091", y$; league$ = "SAW", playdate=jul(2021,4,7), pdf$="", msg$=""
0115 use ::ttm.bbj::ttm
0120 LET TMP$=STBL("WMS_CDW000","D")
0125 LET Y.PASSPARM$="E"
0130 CALL "GW054.bbj", Y$, league$, playdate + 0, FLTS$, HTML$[ALL]
0140 CALL "CDS041","CDS084",S084$,"YY"
0150 LET SENT=0
0160 LET S084.MODE$="E",S084.NAME$="MAIL",S084.EMAILTYPE$="H"
0170 LET S084.FROMNAME$="ScoresAndCharts.com",S084.FROMEMAIL$="webmaster@scoresandcharts.com"
0180 CALL "CDS095",GR03,"XXGR03","YNY",GR03$
0190 CALL "CDS095",GR04,"XXGR04","YNY",GR04$
0200 CALL "CDS095",GR05,"XXGR05","YNY",GR05$,GR05K$
0210 CALL "SW001","XXGR01",0,league$,GR01$,SW001$
0212 CALL "SW001","XXGR03",0,league$+BIN(playdate,3),GR03$,SW001$
0213 CALL "SW001","XXGR03",0,league$+BIN(playdate-7,3),GR03A$,SW001$
0215 CALL "SW001","XXGR08",0,GR01.ID$,GR08$,SW001$
0220 LET S084.SUBJECT$=GR01.NAME$
0225 if gr03.half <> 1 and gr03.half <> 2 then goto 440; rem  Practice round 
0230 LET GR05S=UNT
0240 SELECT (GR05S)GR05$ FROM Y.COMPCODE$+"GR05" WHERE GR05.LEAGUE$=league$ AND GR05.DATE=playdate
0250 if GR05S = 0 goto 440; rem Practice round
0251 READ RECORD(GR05S,END=EOJ)GR05$
0260 READ RECORD(GR04,KNUM=0,KEY=GR05.LEAGUE$+GR05.ID$)GR04$
0270 IF POS(" "<>GR04.EMAIL$)=0 THEN GOTO 0250
0280 LET HTML$="",TAB$="",ROW$=""
0290 IF GR03.HALF=1 OR GR03.HALF=2 OR GR03A.HALF=1 OR GR03A.HALF=2 THEN CALL "CDW000",ROW$,"hbcs3",GR01.NAME$; CALL "CDW000",TAB$,"r",ROW$
0310 DIM GR05A$:FATTR(GR05$)
0320 LET GR05A.LEAGUE$=GR05.LEAGUE$
0330 LET GR05A.ID$=GR05.ID$
0340 LET GR05A.DATE=playdate-7; REM  ToDo - modify to use last round actually played by using SELECT where score is posted sorted by highest data first, limit 1
0350 READ RECORD(GR05,KNUM=0,KEY=KGEN(GR05A$,GR05K$,0),DOM=0360)GR05A$; GOTO 0370
0360 READ RECORD(GR05,END=0380)GR05A$
0370 IF GR05A.LEAGUE$=GR05.LEAGUE$ AND GR05A.ID$=GR05.ID$ AND (GR03A.HALF=1 OR GR03.HALF=2) THEN LET WK=1,TIT$="Last Round"; GOSUB WEEK
0380 IF GR03.HALF=1 OR GR03.HALF=2 THEN LET GR05A$=GR05$,WK=2,TIT$="Next Round - " + iff(gr03.NINE$ = "F", "FRONT", "BACK") +  " NINE"; GOSUB WEEK
0390 CALL "CDW000",HTML$,"t",TAB$
0400 LET Z=POS(GR05.FLT$=FLTS$)
0410 IF Z THEN LET HTML$=HTML$+"<br>"+HTML$[Z]+"<br>"
0420 LET HTML$="<div style='font-family:verdana,tahoma,sans-serif'>"+HTML$+"</div>"
0430 LET HTML$=HTML$+"<div style='font-size:0;height:5px;'></div>"
0440 GOSUB CHART
0445 GOSUB BESTROUNDS
0450 LET S084.TONAME$=FNN$(GR04$),S084.TOEMAIL$=GR04.EMAIL$
0460 IF 1 THEN LET S084.TOEMAIL$="len@excellware.com"
0470 IF 0 THEN CALL "CDW999",HTML$; RUN "GW011"
0480 CALL "CDS084",Y$,Y5$,U0,S084$,Y6,Y6$
0490 if 0 LET NOTE$=CGI.NOTE$
0491 if 0 CALL "SW002",NOTE$,$E2809D$,""""
0492 if 0 CALL "SW002",NOTE$,$E28099$,"'"
0494 if 0 LET CGI.NOTE$=NOTE$
0500 if 0 CALL "SW002",NOTE$,$0D0A$,"<br>"
0505 if jul(0,0,0)=jul(2020,8,8) then note$="<p>No early rounds this week due to high school tournament at Pine Hills.</p>"
0510 if len(note$) PRINT (Y6.CH)NOTE$
0520 if 0 CALL "CDS257",Y$,league$+"e","","XXGR02","W",0,0,0,0,"","","","",CGI.NOTE$
0524 link$ = ttm.getLink(gr04.EMAIL$)
0525 if len(link$) then print (y6.ch)"<p><strong>Click <a href='" + link$ + "'>here</a> to select your tee time.</p>"
0530 if 0 PRINT (Y6.CH)"<p><strong>Please call or email your opponent if you can't play this week.</strong></p>"
0540 if 0 IF POS(" "<>GR08.NAME$) AND POS(" "<>GR08.PHONE$) THEN PRINT (Y6.CH)"<p><strong>Call "+CVS(GR08.NAME$,2)+" at "+GR08.PHONE$+" if you can't play and did not get in touch with your opponent.</strong></p>"
0550 PRINT (Y6.CH)"<p>You can login to <a href='http://scoresandcharts.com'>ScoresAndCharts.com</a> anytime to:</p>"
0560 PRINT (Y6.CH)"<ul>"
0565 PRINT (Y6.CH)"<li>Enter scorecard,</li>"
0570 PRINT (Y6.CH)"<li>View results of last round,</li>"
0580 PRINT (Y6.CH)"<li>View next opponent info,</li>"
0585 PRINT (Y6.CH)"<li>View point ranking,</li>"
0590 PRINT (Y6.CH)"<li>Change information including your email address &amp; phone#, and</li>"
0600 PRINT (Y6.CH)"<li>View latest charts and your best 10 rounds!</li>"
0610 PRINT (Y6.CH)"</ul>"
0620 PRINT (Y6.CH)HTML$
0625 if len(pdf$) call "CDS184", Y$, y6$, pdf$, "HandicapListing.pdf"
0630 CALL "CDS094",Y6$,"e"
0640 LET SENT=SENT+1
0650 IF 1 THEN LET TS=TS+1; IF TS=2 THEN GOTO EOJ
0660 GOTO 0250

1000 WEEK:
1010 DIM GR04A$:FATTR(GR04$)
1020 READ RECORD(GR04,KNUM=0,KEY=GR05.LEAGUE$+GR05A.OPP$,DOM=1030)GR04A$
1030 DIM GR05B$:FATTR(GR05$)
1040 READ RECORD(GR05,KNUM=0,KEY=GR05.LEAGUE$+GR05A.OPP$+GR05A.DATE$,DOM=1050)GR05B$
1050 CALL "CDW000",ROW$,"hcs3",TIT$
1060 CALL "CDW000",TAB$,"r",ROW$
1070 CALL "CDW000",ROW$,"dr",DATE(GR05A.DATE:"%Ds %Mz/%Dz/%Yz")
1080 CALL "CDW000",ROW$,"dc",FNN$(GR04$)
1090 CALL "CDW000",ROW$,"dc",FNN$(GR04A$)
1100 CALL "CDW000",TAB$,"r",ROW$
1110 IF WK=1 THEN CALL "CDW000",ROW$,"dr","Gross"; CALL "CDW000",ROW$,"dc",FNS$(GR05A.SCORE); CALL "CDW000",ROW$,"dc",FNS$(GR05B.SCORE); CALL "CDW000",TAB$,"r",ROW$
1120 CALL "CDW000",ROW$,"dr","Handicap"; CALL "CDW000",ROW$,"dc",FNS$(GR05A.HC); CALL "CDW000",ROW$,"dc",FNS$(GR05B.HC); CALL "CDW000",TAB$,"r",ROW$
1130 IF WK=1 THEN CALL "CDW000",ROW$,"dr","Net"; CALL "CDW000",ROW$,"dc",FNS$(GR05A.NET); CALL "CDW000",ROW$,"dc",FNS$(GR05B.NET); CALL "CDW000",TAB$,"r",ROW$
1140 IF WK=1 THEN CALL "CDW000",ROW$,"dr","Putts"; CALL "CDW000",ROW$,"dc",FNS$(GR05A.PUTTS); CALL "CDW000",ROW$,"dc",FNS$(GR05B.PUTTS); CALL "CDW000",TAB$,"r",ROW$
1150 IF WK=1 THEN CALL "CDW000",ROW$,"dr","Points"; CALL "CDW000",ROW$,"dc",FNS$(GR05A.POINTS); CALL "CDW000",ROW$,"dc",FNS$(GR05B.POINTS); CALL "CDW000",TAB$,"r",ROW$
1160 IF WK=2 THEN CALL "CDW000",ROW$,"dr","Email"; CALL "CDW000",ROW$,"dc",GR04.EMAIL$; CALL "CDW000",ROW$,"dc","<a href='mailto:"+CVS(GR04A.EMAIL$,3)+"?subject=Golf'>"+GR04A.EMAIL$+"</a>"; CALL "CDW000",TAB$,"r",ROW$
1170 IF WK=2 THEN CALL "CDW000",ROW$,"dr","Phone#"; CALL "CDW000",ROW$,"dc",GR04.PHONE$; CALL "CDW000",ROW$,"dc",GR04A.PHONE$; CALL "CDW000",TAB$,"r",ROW$
1180 RETURN

1200 FUNCTIONS:
1210 DEF FNN$(REC$)
1220 DIM REC1$:FATTR(GR04$); LET REC1$=REC$
1230 LET NAM$=CVS(CVS(REC1.FIRSTNAME$,2)+" "+CVS(REC1.LASTNAME$,2),1)
1240 RETURN NAM$
1250 FNEND

1260 DEF FNS$(VAL)
1270 IF VAL THEN LET OUT$=STR(VAL) ELSE LET OUT$=""
1280 RETURN OUT$
1290 FNEND

1300 DEF FNB$(TXT$)
1310 WHILE POS(" "=TXT$)
1320 LET TXT$(POS(" "=TXT$),1)="+"
1330 WEND
1340 RETURN TXT$
1350 FNEND

1400 CHART:
1410 LET TMP$=STBL("WMS_CDW000_RC",""); REM "Disable odd/even row background
1420 DIM R$:"S:C(2*),STOT:B,SRND:B,SMIN:B,SMAX:B,P:C(2*),PTOT:B,PRND:B,PMIN:B,PMAX:B,H:C(2*),HTOT:B,HRND:B,HMIN:B,HMAX:B"
1430 LET R.SMIN=99,R.PMIN=99,R.HMIN=99
1440 DIM L$:FATTR(R$); LET L$=R$
1450 DIM YY$:FATTR(R$); LET YY$=R$
1460 LET CHXL0$="",CHXL1$="",CHXL2$="",CHXL0Y$="",YSCORES$="",YPUTTS$="",YHC$="",GR05C=UNT,TOTS=0,TOTP=0,TOTH=0,RNDP=0,RNDH=0,MINS=99,MAXS=0,MINP=99,MAXP=0,MINH=99,MAXH=0,CY=0,YTOTS=0,YTOTP=0,YTOTH=0,YRNDS=0,YRNDP=0,YRNDH=0,YS$="",YP$="",YH$="",YRS=0
1470 DIM GR05C$:FATTR(GR05$)
1480 SELECT (GR05C)GR05C$ FROM Y.COMPCODE$+"GR05" WHERE GR05C.LEAGUE$=GR05.LEAGUE$ AND GR05C.ID$=GR04.ID$ AND GR05C.SCORE
1490 READ RECORD(GR05C,END=1570)GR05C$
1500 IF L.SRND<20 THEN GOSUB LATEST
1510 LET TY=NUM(DATE(GR05C.DATE:"%Yl"))
1520 IF TY<>CY AND CY THEN GOSUB NEWYR
1530 LET CY=TY,YTOTS=YTOTS+GR05C.SCORE,YRNDS=YRNDS+1
1540 IF GR05C.PUTTS THEN LET YTOTP=YTOTP+GR05C.PUTTS,YRNDP=YRNDP+1
1550 IF GR05C.HC THEN LET YTOTH=YTOTH+GR05C.HC,YRNDH=YRNDH+1
1560 GOTO 1490
1570 CLOSE (GR05C)
1580 GOSUB NEWYR
1590 LET R$=L$; GOSUB ADJ; LET L$=R$
1600 LET R$=YY$; GOSUB ADJ; LET YY$=R$
1610 IF L.SRND THEN LET AVGV=ROUND(L.STOT/L.SRND,1),TIT$="Scores",MINV=L.SMIN,MAXV=L.SMAX,DELTA=5,VAL$=L.S$,CLR$="3072F3"; GOSUB DOCHL
1620 IF YY.SRND THEN LET AVGV=ROUND(YY.STOT/YY.SRND,1),TIT$="Scores",MINV=YY.SMIN,MAXV=YY.SMAX,DELTA=1,VAL$=YY.S$,CLR$="3072F3"; GOSUB DOCHY
1630 IF L.SRND OR YY.SRND THEN CALL "CDW000",TAB$,"r style='padding:0px'",ROW$
1640 IF L.PRND THEN LET AVGV=ROUND(L.PTOT/L.PRND,1),TIT$="Putts",MINV=L.PMIN-1,MAXV=L.PMAX+1,DELTA=1,VAL$=L.P$,CLR$="33cc33"; GOSUB DOCHL
1650 IF YY.PRND THEN LET AVGV=ROUND(YY.PTOT/YY.PRND,1),TIT$="Putts",MINV=YY.PMIN-1,MAXV=YY.PMAX+1,DELTA=1,VAL$=YY.P$,CLR$="33cc33"; GOSUB DOCHY
1660 IF L.PRND OR YY.PRND THEN CALL "CDW000",TAB$,"r style='padding:0px'",ROW$
1670 IF L.HRND THEN LET AVGV=ROUND(L.HTOT/L.HRND,1),TIT$="Handicap",MINV=L.HMIN-1,MAXV=L.HMAX+1,DELTA=1,VAL$=L.H$,CLR$="ff9900"; GOSUB DOCHL
1680 IF YY.HRND THEN LET AVGV=ROUND(YY.HTOT/YY.HRND,1),TIT$="Handicap",MINV=YY.HMIN-1,MAXV=YY.HMAX+1,DELTA=1,VAL$=YY.H$,CLR$="ff9900"; GOSUB DOCHY
1690 IF L.HRND OR YY.HRND THEN CALL "CDW000",TAB$,"r style='padding:0px'",ROW$
1700 CALL "CDW000",HTML$,"t style='border:0px'",TAB$
1710 RETURN

1800 ADJ:
1810 IF R.SRND THEN LET R.S$="_"+R.S$+",_",R.P$="_"+R.P$+",_",R.H$="_"+R.H$+",_"
1820 LET R.SMIN=R.SMIN-MOD(R.SMIN,5)
1830 LET R.SMAX=R.SMAX-MOD(R.SMAX,5)+5
1840 LET R.PMIN=R.PMIN-MOD(R.PMIN,1)-0
1850 LET R.PMAX=R.PMAX-MOD(R.PMAX,1)+0
1860 LET R.HMIN=R.HMIN-MOD(R.HMIN,1)-0
1870 LET R.HMAX=R.HMAX-MOD(R.HMAX,1)+0
1880 RETURN

1900 LATEST:
1910 LET CHXL0$="|"+DATE(GR05C.DATE:"%Mz")+CHXL0$
1920 LET CHXL1$="|"+DATE(GR05C.DATE:"%Dz")+CHXL1$
1930 LET CHXL2$="|"+DATE(GR05C.DATE:"%Yz")+CHXL2$
1940 LET R$=L$,S=GR05C.SCORE,P=GR05C.PUTTS,H=GR05C.HC; GOSUB TOTS; LET L$=R$
1950 RETURN

2000 TOTS:
2010 LET R.S$=","+STR(S)+R.S$
2020 LET R.STOT=R.STOT+S
2030 LET R.SRND=R.SRND+1
2040 LET R.SMIN=MIN(R.SMIN,S)
2050 LET R.SMAX=MAX(R.SMAX,S)
2060 IF P THEN LET R.P$=","+STR(P)+R.P$; LET R.PTOT=R.PTOT+P; LET R.PRND=R.PRND+1; LET R.PMIN=MIN(R.PMIN,P); LET R.PMAX=MAX(R.PMAX,P) ELSE LET R.P$=",_"+R.P$
2070 IF H THEN LET R.H$=","+STR(H)+R.H$; LET R.HTOT=R.HTOT+H; LET R.HRND=R.HRND+1; LET R.HMIN=MIN(R.HMIN,H); LET R.HMAX=MAX(R.HMAX,H) ELSE LET R.H$=",_"+R.H$
2080 RETURN

2100 NEWYR:
2110 IF YRNDS=0 THEN RETURN
2120 LET CHXL0Y$="|"+STR(CY)+CHXL0Y$
2130 LET R$=YY$,S=ROUND(YTOTS/YRNDS,1),P=ROUND(YTOTP/MAX(1,YRNDP),1),H=ROUND(YTOTH/MAX(1,YRNDH),1); GOSUB TOTS; LET YY$=R$
2140 LET YTOTS=0,YRNDS=0,YTOTP=0,YRNDP=0,YTOTH=0,YRNDH=0
2150 LET YRS=YRS+1
2160 RETURN

2200 DOCHL:
2210 LET CH$="<img src='http://chart.apis.google.com/chart"
2220 LET CH$=CH$+"?chf=bg,s,FFFF99"
2230 LET CH$=CH$+"&chxl=0:|"+CHXL0$+"||1:|"+CHXL1$+"||2:|"+CHXL2$+"||4:|Avg+"+STR(AVGV)
2240 LET CH$=CH$+"&chxp=4,"+STR(AVGV)
2250 LET CH$=CH$+"&chxr=3,"+STR(MINV)+","+STR(MAXV)+","+STR(DELTA)+"|4,"+STR(MINV)+","+STR(MAXV)
2260 LET CH$=CH$+"&chxs=3,676767,11.5,0,lt,676767|4,FF0000,11.5,0,lt,FF0000"
2270 LET CH$=CH$+"&chxt=x,x,x,y,r"
2280 LET CH$=CH$+"&chxtc=3,-600|4,-600"
2290 LET CH$=CH$+"&chs=500x250"
2300 LET CH$=CH$+"&cht=lxy"
2310 LET CH$=CH$+"&chco="+CLR$
2320 LET CH$=CH$+"&chds="+STR(MINV)+","+STR(MAXV)+","+STR(MINV)+","+STR(MAXV)
2330 LET CH$=CH$+"&chd=t:-1|"+VAL$
2340 LET CH$=CH$+"&chls=3"
2350 LET CH$=CH$+"&chma=25,60,0,50"
2360 LET CH$=CH$+"&chm=s,"+CLR$+",0,-1,5"
2370 LET CH$=CH$+"&chtt="+TIT$+"+-+Latest+Rounds'"
2380 GOSUB REP
2390 LET CH$=CH$+" style='border:1px solid #c0c0c0' width=500 height=250>"
2400 CALL "CDW000",ROW$,"d style='padding:0px'",CH$
2410 RETURN

2500 DOCHY:
2510 LET WID=230+(MAX(0,YRS-3))*40
2520 LET CH$="<img src='http://chart.apis.google.com/chart"
2530 LET CH$=CH$+"?chf=bg,s,FFFF99"
2540 LET CH$=CH$+"&chxl=0:|"+CHXL0Y$+"||2:|Avg+"+STR(AVGV)
2550 LET CH$=CH$+"&chxp=2,"+STR(AVGV)
2560 LET CH$=CH$+"&chxr=1,"+STR(MINV)+","+STR(MAXV)+","+STR(DELTA)+"|2,"+STR(MINV)+","+STR(MAXV)
2570 LET CH$=CH$+"&chxs=1,676767,11.5,0,lt,676767|2,FF0000,11.5,0,lt,FF0000"
2580 LET CH$=CH$+"&chxt=x,y,r"
2590 LET CH$=CH$+"&chxtc=1,-600|2,-600"
2600 LET CH$=CH$+"&chs="+STR(WID)+"x250"
2610 LET CH$=CH$+"&cht=lxy"
2620 LET CH$=CH$+"&chco="+CLR$
2630 LET CH$=CH$+"&chds="+STR(MINV)+","+STR(MAXV)+","+STR(MINV)+","+STR(MAXV)
2640 LET CH$=CH$+"&chd=t:-1|"+VAL$
2650 LET CH$=CH$+"&chls=3"
2660 LET CH$=CH$+"&chma=25,60,0,50"
2670 LET CH$=CH$+"&chm=s,"+CLR$+",0,-1,5"
2680 LET CH$=CH$+"&chtt="+TIT$+"+-+Year+by+Year'"
2690 GOSUB REP
2700 LET CH$=CH$+" style='border:1px solid #c0c0c0' width="+STR(WID)+" height=250>"
2710 CALL "CDW000",ROW$,"d style='padding:0px'",CH$
2720 RETURN

2800 REP:
2810 CALL "SW002",CH$,"|","%7c"
2820 IF 0 THEN CALL "SW002",CH$,":","%3a"
2830 RETURN

2900 BESTROUNDS:
2905 CALL "CDS041","CDW038",W038$,"YY"
2906 LET W038.USECLASS$="N"
2907 LET Y.COMPNAME$=""
2910 CALL "CDW038",Y$,"Best 10 Scores",Y5A$,HTMLBEST$,W038$,"XXGR05",0,"","","GR05.DATE|SCORE|HC|NET|PUTTS","GR05.LEAGUE$="""+league$+""" AND GR05.ID$="""+GR04.ID$+""" and GR05.NET","REC.SCORE$+NOT(REC.DATE$)","",10
2920 LET HTML$=HTML$+"<br>"+HTMLBEST$
2990 RETURN

8000 REM "Call Error/Escape Routine
8010 CALL "CDS063",STR(TCB(5)),Y$,PGM(-2)
8020 ON Y.ERRSTS GOTO 8030,8040,8050,8060
8030 SETERR 0
8040 RETRY
8050 RETURN
8060 RETRY

9000 EOJ:
9003 LET TMP$=STBL("!CLEAR","WMS_CDW000")
9004 LET HTML$=""
9005 IF SENT THEN LET MSG$="Job Completed, "+STR(SENT)+" emails sent." ELSE LET MSG$="Select 'Golf Score Posting Sheets' before 'Weekly Email'"
9010 exit
