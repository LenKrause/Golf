0100 rem "GW020 - 09/23/15 Golfer Maintenance
0110 setesc 8000; seterr 8000
0120 call "CDS095", gr04, "XXGR04", "YNY", gr04$, gr04k$
0130 call "CDS095", gr05, "XXGR05", "YNY", gr05$
0140 recs = num(field(cgi$, "RECS", err=0150)); gosub save
0150 call "CDW000", row$, "dcs9", wases.leaguename$
0160 call "CDW000", tab$, "r class='top'", row$
0170 call "CDW000", row$, "dcs9", "Golfer Maintenance"
0180 call "CDW000", tab$, "r class='title'", row$
0190 call "CDW000", row$, "d", "First Name"
0200 call "CDW000", row$, "d", "Last Name"
0210 call "CDW000", row$, "d", "Email"
0220 call "CDW000", row$, "d", "Phone#"
0230 call "CDW000", row$, "dc", "Initial<br>Handicap"
0235 call "CDW000", row$, "dc", "Preferred<br>Flight"
0240 call "CDW000", row$, "dc", "Play in " + date(0:"%Yl")
0245 if 0 then call "CDW000", row$, "dc", "Preferred<br>Flight"
0250 call "CDW000", row$, "dc", "Last Round"
0255 call "CDW000", row$, "dc", "Scores"
0260 call "CDW000", tab$, "r class='head'", row$
0270 ind=0
0280 read (gr04, knum=3, key=wases.league$, dom=0290)
0290 read record(gr04, end=0480)gr04$
0300 if gr04.league$ <> wases.league$ then goto 0480
0310 ind=ind + 1, ind$ = str(ind)
0320 if len(b$) then
:      gr04.id$ = fnf$("ID"), gr04.firstname$ = fnf$("FIRSTNAME"), gr04.lastname$ = fnf$("LASTNAME"), gr04.phone$ = fnf$("PHONE"), gr04.email$ = fnf$("EMAIL"), gr04.playthisyear$ = fnf$("PLAYTHISYEAR")
:    fi
0330 call "CDW000", row$, "d", "<input type='hidden' name='ID" + ind$ + "' value='" + gr04.id$ + "'>" + fni$("FIRSTNAME")
0340 call "CDW000", row$, "d", fni$("LASTNAME")
0350 call "CDW000", row$, "d", fni$("EMAIL")
0360 call "CDW000", row$, "d", fni$("PHONE")
0370 if pos("HC" + ind$ + "|" = b$) then
:      val$ = fnf$("HC"), opt$ = ";background-color:#ffcccc"
:    else
:      val$ = str(gr04.hc), opt$ = "";
:      if val$ = "0" then
:        val$ = ""
:      fi
:    fi
0380 call "CDW000", row$, "dc", "<input style='text-align:center" + opt$ + "' type='text' name='HC" + ind$ + "' value='" + val$ + "' size=2 maxlength=2>"
0385 call "CDW000", row$, "dc", "<input style='text-align:center' type='text' name='PREFLT" + ind$ + "' value='" + cvs(gr04.preflt$, 5) + "' size=1 maxlength=1>"
0390 if gr04.playthisyear$ = "Y" or pos(" " <> gr04.id$) = 0 then
:      ysel$ = "checked", nsel$ = ""
:    else
:      nsel$ = "checked", ysel$ = ""
:    fi
0400 call "CDW000", row$, "d", "<input type='radio' name='PLAYTHISYEAR" + ind$ + "' value='Y' " + ysel$ + ">Yes &nbsp;<input type='radio' name='PLAYTHISYEAR" + ind$ + "' value='N' " + nsel$ + ">No"
0403 if 0 then prea$ = "", preb$ = "", preblk$ = ""
0404 if 0 then
:      if gr04.preflt$ = "A" then
:        prea$ = "checked"
:      else
:        if gr04.preflt$ = "B" then
:          preb$ = "checked"
:        else
:          preblk$ = "checked"
:        fi
:      fi
:    fi
0405 if 0 then
:      call "CDW000", row$, "d", "<input type='radio' name='PREFLT" + ind$ + "' value=' ' " + preblk$ + ">None<br><input type='radio' name='PREFLT" + ind$ + "' value='A' " + prea$ + ">A<br><input type='radio' name='PREFLT" + ind$ + "' value='B' " + preb$ + ">B"
:    fi
0410 read (gr05, knum=0, key=gr04.league$ + gr04.id$ + $ff$, dom=0420)
0420 last$ = ""
0430 read record(gr05, end=0450)gr05$
0440 if gr05.league$ + gr05.id$ = gr04.league$ + gr04.id$ then
:      if gr05.score then
:        last$ = date(gr05.date)
:      else
:        goto 0430
:      fi
:    fi
0450 call "CDW000", row$, "dc", last$
0455 call "CDW000", row$, "dc", "<input type='button' value='Edit' onclick='window.open(""[DSN]?p=GW041&id=" + gr04.id$ + """)'>"
0460 call "CDW000", tab$, "r", row$
0470 goto 0290
0480 if new < 10 then new = new + 1; dim gr04$:fattr(gr04$); goto 0310
0490 call "CDW000", row$, "dcs9", "[CANCEL] <input type='submit' value='Save Changes'>"
0500 call "CDW000", tab$, "r", row$
0510 call "CDW000", html$, "tc", tab$
0520 html$ = html$ + "<input type='hidden' name='recs' value=" + ind$ + ">"
0530 html$ = "<form method='post' action='[DSN]?p=GW020'>" + html$ + "</form>"
0540 if len(msg$) then html$ = "<p class='msg'>" + msg$ + "</p>" + html$
0550 goto eoj
0560 def fni$(vbl$)
0570 val$ = field(gr04$, vbl$), lgth$ = str(len(val$))
0580 if pos(vbl$ + ind$ + "|" = b$) then
:      opt$ = " style='background-color:#ffcccc' value='" + fnf$(vbl$) + "'"
:    else
:      opt$ = " value='" + cvs(val$, 2) + "'"
:    fi
0590 return "<input type='text' name='" + vbl$ + ind$ + "' size=" + lgth$ + " maxlength=" + lgth$ + opt$ + ">"
0600 fnend

1000 save:
1010 b$ = ""
1020 for ind=1 to recs
1030   ind$ = str(ind)
1040   email$ = cvs(fnf$("EMAIL"), 8)
1050   if pos(" " <> email$) then
:        call "CDS275::VALIDATE", err=1060, email$, msg$;
:        goto 1070
:      fi
1060   if pos(" " <> email$) then b$ = fnb$("EMAIL")
1070   phone$ = cvs(fnf$("PHONE"), 3)
1080   if len(phone$) then
:        if len(phone$) <> 12 or pos(phone$(1,1) = "23456789") = 0 or pos(phone$(4,1) = ".-") = 0 or pos(phone$(8,1) = ".-") = 0 or mask(phone$(2,2) + phone$(5,3) + phone$(9,4), "^[0123456789]+$") = 0 then
:          b$ = fnb$("PHONE")
:        fi
:      fi
1090   if pos(" " <> email$ + phone$) then
:        if pos(" " <> fnf$("LASTNAME")) = 0 then
:          b$ = fnb$("LASTNAME")
:        fi;
:        if pos(" " <> fnf$("FIRSTNAME")) = 0 then
:          b$ = fnb$("FIRSTNAME")
:        fi
:      fi
1100   hc = -1, hc = num(fnf$("HC"), err=1110)
1110   if hc < 0 or fpt(hc) then b$ = fnb$("HC")
1120 next ind
1130 if len(b$) then msg$ = "Correct problems shown in red!"; return
1140 for ind=1 to recs
1150   ind$ = str(ind)
1160   dim gr04$:fattr(gr04$)
1170   gr04.league$ = wases.league$, id$ = fnf$("ID"), chg = 0
1180   if pos(" " <> id$) then
:        gr04.id$ = id$;
:        extract record(gr04, knum=0, key=kgen(gr04$, gr04k$, 0))gr04$
:      fi
1190   if pos(" " <> fnf$("FIRSTNAME")) then
:        gr04.firstname$ = fnf$("FIRSTNAME"), chg = 1
:      fi
1200   if pos(" " <> fnf$("LASTNAME")) then
:        gr04.lastname$ = fnf$("LASTNAME"), chg = 1
:      fi
1210   gr04.email$ = cvs(fnf$("EMAIL"), 8)
1220   gr04.phone$ = cvs(fnf$("PHONE"), 8)
1230   if num(fnf$("HC")) then gr04.hc = num(fnf$("HC"))
1231   preflt$ = cvs(pad(fnf$("PREFLT"), 1), 4)
1235   if pos(preflt$ = " ABCD") then gr04.preflt$ = preflt$
1240   if pos(fnf$("PLAYTHISYEAR") = "YN") then
:        gr04.playthisyear$ = fnf$("PLAYTHISYEAR")
:      fi
1250   if pos(" " <> id$) then write record(gr04)gr04$ else if chg then gosub newid
1260 next ind
1270 run "GW011"

1300 newid:
1310 gr04.id$ = cvs(gr04.lastname$(1,3) + gr04.firstname$(1,1), 4)
1320 z = pos(" " = gr04.id$); if z then gr04.id$(z, 1) = "_"; goto 1320
1330 write record(gr04, dom=1340)gr04$; return
1340 gr04.id$(4,1) = chr(1 + asc(gr04.id$(4,1)))
1350 goto 1330

1400 functions:
1410 def fnb$(fld$) = b$ + fld$ + ind$ + "|"
1420 def fnf$(fld$)
1430 val$ = "", val$ = field(cgi$, fld$ + ind$, err=1440)
1440 return val$
1450 fnend

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 run "CDW999"
