0100 rem "GW071 - 09/30/15 Post Season Email Entry
0110 setesc 8000; seterr 8000
0120 call "CDS095", gr06, "XXGR06", "YUY", gr06$
0121 call "CDS095", gr08, "XXGR08", "YUY", gr08$
0130 call "CDW000", row$, "ds4", wases.leaguename$
0140 call "CDW000", tab$, "r class='top'", row$
0150 call "CDW000", row$, "ds4", y5$
0160 call "CDW000", tab$, "r class='title'", row$
0170 call "CDW000", row$, "dc", "Date"
0180 call "CDW000", row$, "dc", "Time"
0190 call "CDW000", row$, "d", "Course"
0200 call "CDW000", row$, "d", "Address"
0210 call "CDW000", tab$, "r class='head'", row$
0220 dates = 0
0230 read record(gr06, knum=0, key=wases.league$ + y.jdate$, dom=0240)gr06$;
:    goto 0253
0240 read record(gr06, end=0380)gr06$
0250 if gr06.league$ <> wases.league$ then goto 0380
0253 dim gr08$:fattr(gr08$)
0254 gr08.name$ = "*** Course ID " + gr06.id$ + " is not on file ***"
0255 read record(gr08, knum=0, key=gr06.id$, dom=0256)gr08$
0260 call "CDW000", row$, "dc", date(gr06.date:"%Ds %Mz/%Dz/%Yz")
0270 call "CDW000", row$, "dc", gr06.time$
0280 course$ = cvs(gr08.name$, 3), url$ = cvs(gr08.url$, 3)
0290 if len(url$) and pos("http://" = cvs(url$, 8)) = 0 then url$ = "http://" + url$
0300 if len(url$) then
:      course$ = "<a href='" + url$ + "' target='_blank'>" + course$ + "</a>"
:    fi
0310 call "CDW000", row$, "d", course$
0320 address$ = cvs(cvs(gr08.address$, 3) + " " + cvs(gr08.city$, 3) + " " + gr08.state$ + " " + gr08.zip$, 3)
0330 if len(address$) then
:      map$ = address$;
:      call "SW002::URL", map$;
:      address$ = "<a href='http://maps.google.com?q=" + map$ + "' target='_blank'>" + address$ + "</a>"
:    fi
0340 call "CDW000", row$, "d", address$
0350 call "CDW000", tab$, "r", row$
0360 dates = dates + 1
0370 goto 0240
0380 if dates = 0 then
:      msg$ = "No Post Season dates scheduled today or later";
:      run "GW011"
:    fi
0390 call "CDW000", row$, "d", "Email Message"
0400 call "CDS261", y$, "XXGR02", wases.league$ + "p", "", 0, "", "", 0, notepad$
0410 call "CDW000", row$, "ds3", "<textarea name='NOTE' cols='80' rows='15'>" + notepad$ + "</textarea>"
0420 call "CDW000", tab$, "r", row$
0430 call "CDW000", row$, "dcs4", "[CANCEL] <input type='submit' value='Send Emails'>"
0440 call "CDW000", tab$, "r class='foot'", row$
0450 call "CDW000", html$, "tc", tab$
0460 html$ = "<form method='post' action='[DSN]?p=GW072'>" + html$ + "</form>"
0470 goto eoj

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 if tcb(13) then exit
9020 run "CDW999"
