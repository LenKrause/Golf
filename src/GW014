0100 REM "GW014 - 05/05/12 Dynaweb Golfer Replacement
0110 SETESC DT_ERR; SETERR DT_ERR
0130 CALL "CDS095",GR04,"XXGR04","YUY",GR04$
0140 LET ID$=FIELD(CGI$,"ID",ERR=0240)
0150 LET NEWID$=FIELD(CGI$,"NEWID",ERR=0240)
0160 EXTRACT RECORD(GR04,KNUM=0,KEY=WASES.LEAGUE$+ID$,DOM=0240)GR04$
0170 LET FLT$=GR04.FLT$,GR04.FLT$="",SEQ=GR04.SEQ,GR04.SEQ=0
0180 WRITE RECORD(GR04)GR04$
0190 EXTRACT RECORD(GR04,KNUM=0,KEY=WASES.LEAGUE$+NEWID$)GR04$
0200 LET GR04.FLT$=FLT$,GR04.SEQ=SEQ
0210 WRITE RECORD(GR04)GR04$
0220 LET MSG$="Golfer replaced"
0230 RUN "GW011"
0240 LET GR04S=UNT,PLAYER$=""
0250 SELECT (GR04S)GR04$ FROM Y.COMPCODE$+"GR04" WHERE GR04.LEAGUE$=WASES.LEAGUE$ AND GR04.PLAYTHISYEAR$="Y" AND GR04.SEQ=0
0260 READ RECORD(GR04S,END=0290)GR04$
0270 LET PLAYER$=PLAYER$+"<option value='"+GR04.ID$+"'>"+CVS(GR04.FIRSTNAME$,2)+" "+CVS(GR04.LASTNAME$,2)+"</option>"+$0A$
0280 GOTO 0260
0290 CLOSE (GR04S)
0300 IF LEN(PLAYER$)=0
:       THEN
:          LET MSG$="Use Golfer Maintenance to add Golfer or set 'Play This Year' before replacing Golfer.";
:          RUN "GW011"
0310 LET PLAYER$="<select name='NEWID'>"+$0A$+PLAYER$+"</select>"
0320 LET WHERE$="GR04.LEAGUE$="""+WASES.LEAGUE$+""" and gr04.FLT$<>"" "" and gr04.SEQ"
0330 LET SORTBY$="GR04.LEAGUE$+GR04.FLT$+GR04.SEQ$"
0340 LET FILENAME$="XXGR04"
0350 LET DISP$="FLT|SEQ\\\\c|GW001.NAMERADIO"
0360 LET FOOT$="Replace with "+PLAYER$+"<br>[CANCEL] <input type='submit' value='Submit'>"
0370 CALL "CDW038",Y$,Y5$,Y5A$,HTML$,W038$,FILENAME$,KNO,KEYBEGIN$,KEYEND$,DISP$,WHERE$,SORTBY$,MODE$,LIMITVAL,SUBHEAD$,FOOT$,ENV$
0380 IF LEN(HTML$)=0
:       THEN
:          LET MSG$="Assign Flight & Sequence Numbers before Replacing Golfer";
:          RUN "GW011"
0390 LET HTML$="<form method='post' action='[DSN]?p=GW014'>"+HTML$+"</form>"
0400 RUN "CDW999"

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 IF TCB(13) THEN EXIT
9020 RUN "CDS001"
