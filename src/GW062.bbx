0100 rem "GW062 - 05/30/17 Golf Weekly Email
0110 setesc 8000; seterr 8000
0120 tmp$ = stbl("WMS_CDW000", "D")
0125 y.passparm$ = "E"
0130 call "GW054", y$, wases$, num(cgi.date$), flts$, html$[all]
0140 call "CDS041", "CDS084", s084$, "YY"
0150 sent = 0
0160 s084.mode$ = "E", s084.name$ = "MAIL", s084.emailtype$ = "H"
0170 s084.fromname$ = "ScoresAndCharts.com", s084.fromemail$ = "webmaster@scoresandcharts.com"
0180 call "CDS095", gr03, "XXGR03", "YNY", gr03$
0190 call "CDS095", gr04, "XXGR04", "YNY", gr04$
0200 call "CDS095", gr05, "XXGR05", "YNY", gr05$, gr05k$
0210 call "SW001", "XXGR01", 0, wases.league$, gr01$, sw001$
0212 call "SW001", "XXGR03", 0, wases.league$ + bin(num(cgi.date$), 3), gr03$, sw001$
0213 call "SW001", "XXGR03", 0, wases.league$ + bin(num(cgi.date$) - 7, 3), gr03a$, sw001$
0215 call "SW001", "XXGR08", 0, gr01.id$, gr08$, sw001$
0220 s084.subject$ = gr01.name$
0230 gr05s = unt
0240 select (gr05s)gr05$ from y.compcode$ + "GR05" where gr05.league$ = wases.league$ and gr05.date = num(cgi.date$)
0250 read record(gr05s, end=eoj)gr05$
0260 read record(gr04, knum=0, key=gr05.league$ + gr05.id$)gr04$
0270 if pos(" " <> gr04.email$) = 0 then goto 0250
0280 html$ = "", tab$ = "", row$ = ""
0290 if gr03.half = 1 or gr03.half = 2 or gr03a.half = 1 or gr03.half = 2 then
:      call "CDW000", row$, "hbcs3", gr01.name$;
:      call "CDW000", tab$, "r", row$
:    fi
0310 dim gr05a$:fattr(gr05$)
0320 gr05a.league$ = gr05.league$
0330 gr05a.id$ = gr05.id$
0340 gr05a.date = num(cgi.date$) - 7;
:    rem  todo - modify to use last round actually played by using select where score is posted sorted by highest data first,  limit 1
0350 read record(gr05, knum=0, key=kgen(gr05a$, gr05k$, 0), dom=0360)gr05a$;
:    goto 0370
0360 read record(gr05, end=0380)gr05a$
0370 if gr05a.league$ = gr05.league$ and gr05a.id$ = gr05.id$ and (gr03a.half = 1 or gr03.half = 2) then
:      wk = 1, tit$ = "Last Round";
:      gosub week
:    fi
0380 if gr03.half = 1 or gr03.half = 2 then
:      gr05a$ = gr05$, wk = 2, tit$ = "Next Round";
:      gosub week
:    fi
0390 call "CDW000", html$, "t", tab$
0400 z = pos(gr05.flt$ = flts$)
0410 if z then html$ = html$ + "<br>" + html$[z] + "<br>"
0420 html$ = "<div style='font-family:verdana,tahoma,sans-serif'>" + html$ + "</div>"
0430 html$ = html$ + "<div style='font-size:0;height:5px;'></div>"
0440 gosub chart
0445 gosub bestrounds
0450 s084.toname$ = fnn$(gr04$), s084.toemail$ = gr04.email$
0460 if 0 then s084.toemail$ = "len@excellware.com"
0470 if 0 then call "CDW999", html$; run "GW011"
0480 call "CDS084", y$, y5$, u0, s084$, y6, y6$
0490 note$ = cgi.note$
0491 call "SW002", note$, $e2809d$, """"
0492 call "SW002", note$, $e28099$, "'"
0494 cgi.note$ = note$
0500 call "SW002", note$, $0d0a$, "<br>"
0510 print (y6.ch)note$
0520 call "CDS257", y$, wases.league$ + "e", "", "XXGR02", "W", 0, 0, 0, 0, "", "", "", "", cgi.note$
0530 print (y6.ch)"<p><strong>Please call or email your opponent if you can't play this week.</strong></p>"
0540 if pos(" " <> gr08.name$) and pos(" " <> gr08.phone$) then
:      print (y6.ch)"<p><strong>Call " + cvs(gr08.name$, 2) + " at " + gr08.phone$ + " if you can't play and did not get in touch with your opponent.</strong></p>"
:    fi
0550 print (y6.ch)"<p>You can login to <a href='http://scoresandcharts.com'>ScoresAndCharts.com</a> anytime to:</p>"
0560 print (y6.ch)"<ul>"
0570 print (y6.ch)"<li>View results of last round,</li>"
0580 print (y6.ch)"<li>View next opponent info,</li>"
0585 print (y6.ch)"<li>View point ranking,</li>"
0590 print (y6.ch)"<li>Change information including your email address &amp; phone#, and</li>"
0600 print (y6.ch)"<li>View latest charts and your best 10 rounds!</li>"
0610 print (y6.ch)"</ul>"
0620 print (y6.ch)html$
0630 call "CDS094", y6$, "e"
0640 sent = sent + 1
0650 if 0 then ts = ts + 1; if ts = 2 then goto eoj
0660 goto 0250

1000 week:
1010 dim gr04a$:fattr(gr04$)
1020 read record(gr04, knum=0, key=gr05.league$ + gr05a.opp$, dom=1030)gr04a$
1030 dim gr05b$:fattr(gr05$)
1040 read record(gr05, knum=0, key=gr05.league$ + gr05a.opp$ + gr05a.date$, dom=1050)gr05b$
1050 call "CDW000", row$, "hcs3", tit$
1060 call "CDW000", tab$, "r", row$
1070 call "CDW000", row$, "dr", date(gr05a.date:"%Ds %Mz/%Dz/%Yz")
1080 call "CDW000", row$, "dc", fnn$(gr04$)
1090 call "CDW000", row$, "dc", fnn$(gr04a$)
1100 call "CDW000", tab$, "r", row$
1110 if wk = 1 then
:      call "CDW000", row$, "dr", "Gross";
:      call "CDW000", row$, "dc", fns$(gr05a.score);
:      call "CDW000", row$, "dc", fns$(gr05b.score);
:      call "CDW000", tab$, "r", row$
:    fi
1120 call "CDW000", row$, "dr", "Handicap";
:    call "CDW000", row$, "dc", fns$(gr05a.hc);
:    call "CDW000", row$, "dc", fns$(gr05b.hc);
:    call "CDW000", tab$, "r", row$
1130 if wk = 1 then
:      call "CDW000", row$, "dr", "Net";
:      call "CDW000", row$, "dc", fns$(gr05a.net);
:      call "CDW000", row$, "dc", fns$(gr05b.net);
:      call "CDW000", tab$, "r", row$
:    fi
1140 if wk = 1 then
:      call "CDW000", row$, "dr", "Putts";
:      call "CDW000", row$, "dc", fns$(gr05a.putts);
:      call "CDW000", row$, "dc", fns$(gr05b.putts);
:      call "CDW000", tab$, "r", row$
:    fi
1150 if wk = 1 then
:      call "CDW000", row$, "dr", "Points";
:      call "CDW000", row$, "dc", fns$(gr05a.points);
:      call "CDW000", row$, "dc", fns$(gr05b.points);
:      call "CDW000", tab$, "r", row$
:    fi
1160 if wk = 2 then
:      call "CDW000", row$, "dr", "Email";
:      call "CDW000", row$, "dc", gr04.email$;
:      call "CDW000", row$, "dc", "<a href='mailto:" + cvs(gr04a.email$, 3) + "?subject=Golf'>" + gr04a.email$ + "</a>";
:      call "CDW000", tab$, "r", row$
:    fi
1170 if wk = 2 then
:      call "CDW000", row$, "dr", "Phone#";
:      call "CDW000", row$, "dc", gr04.phone$;
:      call "CDW000", row$, "dc", gr04a.phone$;
:      call "CDW000", tab$, "r", row$
:    fi
1180 return

1200 functions:
1210 def fnn$(rec$)
1220 dim rec1$:fattr(gr04$); rec1$ = rec$
1230 nam$ = cvs(cvs(rec1.firstname$, 2) + " " + cvs(rec1.lastname$, 2), 1)
1240 return nam$
1250 fnend

1260 def fns$(val)
1270 if val then out$ = str(val) else out$ = ""
1280 return out$
1290 fnend

1300 def fnb$(txt$)
1310 while pos(" " = txt$)
1320   txt$(pos(" " = txt$), 1) = "+"
1330 wend
1340 return txt$
1350 fnend

1400 chart:
1410 tmp$ = stbl("WMS_CDW000_RC", ""); rem "Disable odd/even row background
1420 dim r$:"S:C(2*),STOT:B,SRND:B,SMIN:B,SMAX:B,P:C(2*),PTOT:B,PRND:B,PMIN:B,PMAX:B,H:C(2*),HTOT:B,HRND:B,HMIN:B,HMAX:B"
1430 r.smin = 99, r.pmin = 99, r.hmin = 99
1440 dim l$:fattr(r$); l$ = r$
1450 dim yy$:fattr(r$); yy$ = r$
1460 chxl0$ = "", chxl1$ = "", chxl2$ = "", chxl0y$ = "", yscores$ = "", yputts$ = "", yhc$ = "", gr05c = unt, tots = 0, totp = 0, toth = 0, rndp = 0, rndh = 0, mins = 99, maxs = 0, minp = 99, maxp = 0, minh = 99, maxh = 0, cy = 0, ytots = 0, ytotp = 0, ytoth = 0, yrnds = 0, yrndp = 0, yrndh = 0, ys$ = "", yp$ = "", yh$ = "", yrs = 0
1470 dim gr05c$:fattr(gr05$)
1480 select (gr05c)gr05c$ from y.compcode$ + "GR05" where gr05c.league$ = gr05.league$ and gr05c.id$ = gr04.id$ and gr05c.score
1490 read record(gr05c, end=1570)gr05c$
1500 if l.srnd < 20 then gosub latest
1510 ty = num(date(gr05c.date:"%Yl"))
1520 if ty <> cy and cy then gosub newyr
1530 cy = ty, ytots = ytots + gr05c.score, yrnds = yrnds + 1
1540 if gr05c.putts then ytotp = ytotp + gr05c.putts, yrndp = yrndp + 1
1550 if gr05c.hc then ytoth = ytoth + gr05c.hc, yrndh = yrndh + 1
1560 goto 1490
1570 close (gr05c)
1580 gosub newyr
1590 r$ = l$; gosub adj; l$ = r$
1600 r$ = yy$; gosub adj; yy$ = r$
1610 if l.srnd then
:      avgv = round(l.stot / l.srnd, 1), tit$ = "Scores", minv = l.smin, maxv = l.smax, delta = 5, val$ = l.s$, clr$ = "3072F3";
:      gosub dochl
:    fi
1620 if yy.srnd then
:      avgv = round(yy.stot / yy.srnd, 1), tit$ = "Scores", minv = yy.smin, maxv = yy.smax, delta = 1, val$ = yy.s$, clr$ = "3072F3";
:      gosub dochy
:    fi
1630 if l.srnd or yy.srnd then call "CDW000", tab$, "r style='padding:0px'", row$
1640 if l.prnd then
:      avgv = round(l.ptot / l.prnd, 1), tit$ = "Putts", minv = l.pmin - 1, maxv = l.pmax + 1, delta = 1, val$ = l.p$, clr$ = "33cc33";
:      gosub dochl
:    fi
1650 if yy.prnd then
:      avgv = round(yy.ptot / yy.prnd, 1), tit$ = "Putts", minv = yy.pmin - 1, maxv = yy.pmax + 1, delta = 1, val$ = yy.p$, clr$ = "33cc33";
:      gosub dochy
:    fi
1660 if l.prnd or yy.prnd then call "CDW000", tab$, "r style='padding:0px'", row$
1670 if l.hrnd then
:      avgv = round(l.htot / l.hrnd, 1), tit$ = "Handicap", minv = l.hmin - 1, maxv = l.hmax + 1, delta = 1, val$ = l.h$, clr$ = "ff9900";
:      gosub dochl
:    fi
1680 if yy.hrnd then
:      avgv = round(yy.htot / yy.hrnd, 1), tit$ = "Handicap", minv = yy.hmin - 1, maxv = yy.hmax + 1, delta = 1, val$ = yy.h$, clr$ = "ff9900";
:      gosub dochy
:    fi
1690 if l.hrnd or yy.hrnd then call "CDW000", tab$, "r style='padding:0px'", row$
1700 call "CDW000", html$, "t style='border:0px'", tab$
1710 return

1800 adj:
1810 if r.srnd then r.s$ = "_" + r.s$ + ",_", r.p$ = "_" + r.p$ + ",_", r.h$ = "_" + r.h$ + ",_"
1820 r.smin = r.smin - mod(r.smin, 5)
1830 r.smax = r.smax - mod(r.smax, 5) + 5
1840 r.pmin = r.pmin - mod(r.pmin, 1) - 0
1850 r.pmax = r.pmax - mod(r.pmax, 1) + 0
1860 r.hmin = r.hmin - mod(r.hmin, 1) - 0
1870 r.hmax = r.hmax - mod(r.hmax, 1) + 0
1880 return

1900 latest:
1910 chxl0$ = "|" + date(gr05c.date:"%Mz") + chxl0$
1920 chxl1$ = "|" + date(gr05c.date:"%Dz") + chxl1$
1930 chxl2$ = "|" + date(gr05c.date:"%Yz") + chxl2$
1940 r$ = l$, s = gr05c.score, p = gr05c.putts, h = gr05c.hc; gosub tots; l$ = r$
1950 return

2000 tots:
2010 r.s$ = "," + str(s) + r.s$
2020 r.stot = r.stot + s
2030 r.srnd = r.srnd + 1
2040 r.smin = min(r.smin, s)
2050 r.smax = max(r.smax, s)
2060 if p then
:      r.p$ = "," + str(p) + r.p$;
:      r.ptot = r.ptot + p;
:      r.prnd = r.prnd + 1;
:      r.pmin = min(r.pmin, p);
:      r.pmax = max(r.pmax, p)
:    else
:      r.p$ = ",_" + r.p$
:    fi
2070 if h then
:      r.h$ = "," + str(h) + r.h$;
:      r.htot = r.htot + h;
:      r.hrnd = r.hrnd + 1;
:      r.hmin = min(r.hmin, h);
:      r.hmax = max(r.hmax, h)
:    else
:      r.h$ = ",_" + r.h$
:    fi
2080 return

2100 newyr:
2110 if yrnds = 0 then return
2120 chxl0y$ = "|" + str(cy) + chxl0y$
2130 r$ = yy$, s = round(ytots / yrnds, 1), p = round(ytotp / max(1, yrndp), 1), h = round(ytoth / max(1, yrndh), 1);
:    gosub tots;
:    yy$ = r$
2140 ytots = 0, yrnds = 0, ytotp = 0, yrndp = 0, ytoth = 0, yrndh = 0
2150 yrs = yrs + 1
2160 return

2200 dochl:
2210 ch$ = "<img src='http://chart.apis.google.com/chart"
2220 ch$ = ch$ + "?chf=bg,s,FFFF99"
2230 ch$ = ch$ + "&chxl=0:|" + chxl0$ + "||1:|" + chxl1$ + "||2:|" + chxl2$ + "||4:|Avg+" + str(avgv)
2240 ch$ = ch$ + "&chxp=4," + str(avgv)
2250 ch$ = ch$ + "&chxr=3," + str(minv) + "," + str(maxv) + "," + str(delta) + "|4," + str(minv) + "," + str(maxv)
2260 ch$ = ch$ + "&chxs=3,676767,11.5,0,lt,676767|4,FF0000,11.5,0,lt,FF0000"
2270 ch$ = ch$ + "&chxt=x,x,x,y,r"
2280 ch$ = ch$ + "&chxtc=3,-600|4,-600"
2290 ch$ = ch$ + "&chs=500x250"
2300 ch$ = ch$ + "&cht=lxy"
2310 ch$ = ch$ + "&chco=" + clr$
2320 ch$ = ch$ + "&chds=" + str(minv) + "," + str(maxv) + "," + str(minv) + "," + str(maxv)
2330 ch$ = ch$ + "&chd=t:-1|" + val$
2340 ch$ = ch$ + "&chls=3"
2350 ch$ = ch$ + "&chma=25,60,0,50"
2360 ch$ = ch$ + "&chm=s," + clr$ + ",0,-1,5"
2370 ch$ = ch$ + "&chtt=" + tit$ + "+-+Latest+Rounds'"
2380 gosub rep
2390 ch$ = ch$ + " style='border:1px solid #c0c0c0' width=500 height=250>"
2400 call "CDW000", row$, "d style='padding:0px'", ch$
2410 return

2500 dochy:
2510 wid = 230 + (max(0, yrs - 3)) * 40
2520 ch$ = "<img src='http://chart.apis.google.com/chart"
2530 ch$ = ch$ + "?chf=bg,s,FFFF99"
2540 ch$ = ch$ + "&chxl=0:|" + chxl0y$ + "||2:|Avg+" + str(avgv)
2550 ch$ = ch$ + "&chxp=2," + str(avgv)
2560 ch$ = ch$ + "&chxr=1," + str(minv) + "," + str(maxv) + "," + str(delta) + "|2," + str(minv) + "," + str(maxv)
2570 ch$ = ch$ + "&chxs=1,676767,11.5,0,lt,676767|2,FF0000,11.5,0,lt,FF0000"
2580 ch$ = ch$ + "&chxt=x,y,r"
2590 ch$ = ch$ + "&chxtc=1,-600|2,-600"
2600 ch$ = ch$ + "&chs=" + str(wid) + "x250"
2610 ch$ = ch$ + "&cht=lxy"
2620 ch$ = ch$ + "&chco=" + clr$
2630 ch$ = ch$ + "&chds=" + str(minv) + "," + str(maxv) + "," + str(minv) + "," + str(maxv)
2640 ch$ = ch$ + "&chd=t:-1|" + val$
2650 ch$ = ch$ + "&chls=3"
2660 ch$ = ch$ + "&chma=25,60,0,50"
2670 ch$ = ch$ + "&chm=s," + clr$ + ",0,-1,5"
2680 ch$ = ch$ + "&chtt=" + tit$ + "+-+Year+by+Year'"
2690 gosub rep
2700 ch$ = ch$ + " style='border:1px solid #c0c0c0' width=" + str(wid) + " height=250>"
2710 call "CDW000", row$, "d style='padding:0px'", ch$
2720 return

2800 rep:
2810 call "SW002", ch$, "|", "%7c"
2820 if 0 then call "SW002", ch$, ":", "%3a"
2830 return

2900 bestrounds:
2905 call "CDS041", "CDW038", w038$, "YY"
2906 w038.useclass$ = "N"
2907 y.compname$ = ""
2910 call "CDW038", y$, "Best 10 Scores", y5a$, htmlbest$, w038$, "XXGR05", 0, "", "", "GR05.DATE|SCORE|HC|NET|PUTTS", "GR05.LEAGUE$=""" + wases.league$ + """ AND GR05.ID$=""" + gr04.id$ + """ and GR05.NET", "REC.SCORE$+NOT(REC.DATE$)", "", 10
2920 html$ = html$ + "<br>" + htmlbest$
2990 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9003 tmp$ = stbl("!CLEAR", "WMS_CDW000")
9004 html$ = ""
9005 if sent then
:      msg$ = "Job Completed, " + str(sent) + " emails sent."
:    else
:      msg$ = "Select 'Golf Score Posting Sheets' before 'Weekly Email'"
:    fi
9010 run "GW011"
