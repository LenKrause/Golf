0100 rem "LKS640 - 11/18/00 COPY BOSS/IX CWRITE TAPE CARTRIDGE TO DISK
0110 begin
0120 call "CDS069", 0, 0, 0, 0, "Insert tape cartridge in drive-+"
0130 if ctl = 4 then goto eoj
0150 dir$ = "conv/", dir$ = stbl("LKS640", err=0160)
0160 call "CDS069", 1, 20, 0, 0, "+empty directory where files are to be copied+", "A", 0, dir$
0170 if ctl = 4 then goto eoj
0180 call "CDS095", err=0150, 1, dir$, "Y"
0190 close (1)
0195 if dir$(len(dir$)) <> "/" then dir$ = dir$ + "/"
0200 open (1)"|ls " + dir$ + " | wc -l"
0201 read (1, err=0150)a
0202 if a <> 0 then call "CDS069", 0, 0, 0, 0, "Directory not empty-"; goto 0150
0203 gosub 1000; rem "REWIND
0204 tmp$ = stbl("LKS640", dir$)
0210 f$ = dir$ + str(i:"0000")
0220 print "File", i
0230 a = scall("dd if=/dev/rmt1.5 of=" + f$ + " >/dev/null 2>/dev/null")
0240 if a then escape; rem "Unable to execute dd command
0250 call "CDS095", 1, f$
0260 dim f[9]; call "CDS087", 1, "", "", "", "", f[all]
0265 if i = 0 then gosub label; if z$ = "N" then print 'cs', ; goto 0110
0270 if f[5] then print f[5]; i = i + 1; goto 0210
0280 gosub rewind
0290 call "CDS069", 0, 0, 0, 0, "Copy completed-"
0300 goto eoj

1000 rewind:
1005 print @(0,23), "Rewinding...", 
1010 a = scall("tctl -f/dev/rmt1 rewind")
1011 if a then
:      call "CDS069", 0, 0, 0, 0, "Error rewinding tape, exit code " + str(a) + "-";
:      goto eoj
:    fi
1015 print @(0,23), 'cl', 'rs', 
1020 return

1100 label:
1110 read record(1, siz=96)lab$
1115 call "CDS075", 1
1120 if len(lab$) < 96 or lab$(1,4) <> "VOL " then
:      call "CDS069", 0, 0, 0, 0, "Invalid label on tape-";
:      goto eoj
:    fi
1130 if lab$(6,1) <> "2" then
:      call "CDS069", 0, 0, 0, 0, "This tape was not made using cwrite-";
:      goto eoj
:    fi
1140 print 'sb', @(0,3), "Tape Set Name:", @(0,4), "Tape ID:", @(0,5), "Sequence#:", @(0,6), "Date first labelled:", @(0,7), "Date last written:", @(0,8), "Time written:", 'sf', @(21,3), lab$(7,8), @(21,4), lab$(15,8), @(21,5), lab$(23,2), @(21,6), fnd$(37), @(21,7), fnd$(43), @(21,8), lab$(49,4), 
1150 z$ = "Y"; call "CDS069", 1, 1, 0, 0, "Process this tape (Y/N)", "AYN", 0, z$
1160 return
2000 def fnd$(z) = lab$(z, 2) + "/" + lab$(z + 2, 2) + "/" + lab$(z + 4, 2)

9000 eoj:
9010 run "CDS001"
