REM /**
REM  * teeSheet.bbj
REM  * @author len
REM  *
REM  */

use ::DTFileRecords.bbj::DTFileRecords
use ::DTHtmlTable.bbj::DTHtmlTable
use ::scorePosting.bbj::scorePosting

class public teeSheet
  field private BBjString msg! = ""
  field private BBjString html$
  field private BBjVector players!
  field private BBjString coursePhone$
  field private BBjString courseId$
  
  method public teeSheet(BBjString adminId$, BBjInt date%)
    declare DTFileRecords gr10File!
    declare BBjVector     gr10Recs!
    declare DTHtmlTable   table!
    
    gr10File! = new DTFileRecords("LK", "GR10", "YNY")
    gr10File!.setKeyPre(1, adminId$ + bin(date%, 3))
    gr10Recs! = gr10File!.getAllRecords()
    if gr10Recs!.size() = 0 then
      #msg! = "No tee times found for admin id ' " + adminId$ + "' on " + date(date%:"%Ds %Ms %D")
      methodret
    fi
    
    dim gr10$:gr10File!.getTemplate()
    gr10$ = gr10Recs!.get(0).toString()
    
    #players! = new BBjVector()
    
    table! = new DTHtmlTable(BBjAPI.TRUE)
    table!.addHeaderCell("Tee Time Manager", "cs3")
    table!.addRow()
    
    table!.addHeaderCell(date(date%:"%Dl %Ms %D"), "cs3")
    table!.addRow()
    
    call "SW001", "XXGR03", 0, "SAW" + bin(date%, 3), gr03$, sw001$
    if sw001.onfile then nine$ = iff(gr03.nine$ = "F", " - Front Nine", " - Back Nine")
    
    #courseId$ = gr10.gr08id$
    call "SW001", "XXGR08", 0, #courseId$, gr08$, sw001$
    #coursePhone$ = gr08.phone$
    courseName$ = cvs(gr08.name$, 2)
    tags$ = "target=_blank "
    outing = (gr03.half = 0)
    if #courseId$ = "RT " then courseName$ = "<a " + tags$ + "href='https://www.google.com/maps/dir//ridge+top+golf+course/@41.2017946,-81.9148965,11z/data=!4m8!4m7!1m0!1m5!1m1!1s0x8830c97d136e17d9:0xdb9f1bf6beae8f60!2m2!1d-81.8160934!2d41.0708847'>" + courseName$ + "</a>"
    
    table!.addHeaderCell(courseName$ + nine$, "cs" + str(2 + outing) + "&")
    table!.addRow()
    
    table!.addHeaderCell("Time", "c")
    table!.addHeaderCell("Player")
    if outing then table!.addHeaderCell("Handicap", "c")
    table!.addRow()
    
    for i = 0 to gr10Recs!.size() - 1
      gr10$ = gr10Recs!.get(i).toString()
      if last_time and last_time < 16 and gr10.time >= 16 then ids$ = ""; rem  players now make 2 tee times per day
      table!.addCell(date(gr10.date, gr10.time:"%h:%mz %p"), "cR4")
      for player = 1 to 4
        name$ = "&nbsp;"
        if pos(" " <> gr10.id$[player]) then
          call "SW001", "XXGR09", 0, gr10.aid$ + gr10.id$[player], gr09$, sw001$
          if sw001.onfile then
            name$ = cvs(gr09.firstname$, 2) + " " + cvs(gr09.lastname$, 2)
            if pos(gr09.id$ = ids$, 4) then
              name$ = name$ + " - Guest"
              guest = 1
            else
              guest = 0
            fi
            
            #players!.add(cvs(gr09.email$, 2))
            if pos(gr09.id$ = ids$, 4) = 0 then ids$ = ids$ + gr09.id$
          fi
          emptyRow = 0
        else 
          emptyRow = 1
        fi
        table!.addCell(name$, "l&")
        if outing then
          if len(gr04$) = 0 then
            call "CDS041","LKGR04",  gr04$, "YP"
            call "CDS041","LKGW009", gw009$, "YP"
            call "CDS091", y$
          fi
          gr04$ = scorePosting.getGr04(gr09$)
          call "GW009", y$, gr04$, gw009$
          hc$ = iff(! emptyRow and ! guest and gw009.hc, str(int(gw009.hc) + (fpt(gw009.hc) > 0)), "")
          table!.addCell(hc$, "c")
        fi
        table!.addRow()
      next player
      last_time = gr10.time
    next i
    
    table!.addPageStyle("th a {text-decoration:none;background-color:#346904;color:#fefefe !important;border-radius:4px;padding:1px 5px 1px 5px;white-space:nowrap;display:block;} th a:hover {color:#ffffcc !important;}")
    table!.addTable()
    #html$ = table!.getHtml()
  methodend

  method public void email()
    #email(BBjAPI.FALSE)
  methodend

  method public void email(Boolean testMode!)
    if #players!.size() = 0 then
      #msg! = "No players scheduled"
      methodret
    fi
    
    call "CDS041", "CDS084", s084$, "YP"
    tmp$ = stbl("SMS_CDS084.SYMBOL", "E29BB3")
    s084.mode$      = "N"
    s084.name$      = "MAIL"
    s084.emailtype$ = "H"
    s084.subject$   = "Tee Sheet"
    s084.emailbcc$  = "len@excellware.com"

    if ! testMode! then
      if #courseId$ = "PH " then
        s084.toname$  = "David Kinnell"
        s084.toemail$ = "david@golfpinehills.net"
      fi
      for i = 0 to #players!.size() - 1
        s084.emailbcc$ = s084.emailbcc$ + "," + #players!.get(i).toString()
      next i
    fi
    
    call "CDS084", y$, y5$, u0, s084$, y6, y6$    
    if jul(0,0,0) = jul(2021,8,17) then
      print (y6.ch)"<p>Note: Tee times have been revised.</p><p>Cookout after golf tonight!</p>"
    fi
    print (y6.ch)#html$
    rem if #courseId$ = "PH " then print (y6.ch)"<br><center>Please phone the course at " + #coursePhone$ + " to prepay by credit card.</center><br>"
    call "CDS094", y6$, "e"
  methodend
  
  method public BBjString getErrMsg()
    methodret #msg!
  methodend
  
  method public BBjString getHtml()
    methodret #html$
  methodend
  
  method public BBjString getCoursePhone()
    methodret #coursePhone$
  methodend

classend

declare teeSheet sheet!

if fid(0) = "IO" then
  sheet! = new teeSheet(argv(4), int(y.jdate + 1)); rem  Scheduled for 6pm day before play
  if ! sheet!.getErrMsg().isEmpty() then
    print sheet!.getErrMsg()
  else
    sheet!.email()
  fi
  call "CDS081", y$, -1
fi

begin
y$ = "LK"
y5$ = "Tee Sheet"
call "CDS011", y$, y5$

sheet! = new teeSheet("lk01", jul(2022,9,11))
if ! sheet!.getErrMsg().isEmpty() then
  print sheet!.getErrMsg()
else
  call "CDW999", sheet!.getHtml()
  sheet!.email(BBjAPI.FALSE)  
fi
