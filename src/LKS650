0100 REM "LKS650 - 01/06/96 CONVERT MBF BOSS/VS TAPE TRANSFER FORMAT FILES
0105 REM BEGIN
0110 PRINT 'CS','SB',@(0,10),"From directory:",@(0,11),"To directory:",@(0,12),"Convert DIR/SRT to SKY:",'SF',

0112 REM CALL "CDS084",ERR=9000,Y$,Y5$,6,"NS",Y6,Y6$
0120 DEF FNS$(Z$)=AND(Z$,FILL(LEN(Z$),$7F$))
0140 LET FRDIR$="/u/pc/tape/"
0141 CALL "CDS069",1,20,30,10,"+directory where files have been copied from tape+","A",0,FRDIR$
0142 IF FRDIR$(LEN(FRDIR$))<>"/"
:       THEN
:          LET FRDIR$=FRDIR$+"/";
:          PRINT @(30,10),FRDIR$,
0143 IF CTL=4 THEN GOTO 9000
0144 CALL "CDS095",ERR=0141,1,FRDIR$,"Y"; CLOSE (1)
0150 LET TODIR$="/u/pc/conv/"
0151 CALL "CDS069",1,20,30,11,"+directory where converted files will be stored+","A",0,TODIR$
0152 IF TODIR$(LEN(TODIR$))<>"/"
:       THEN
:          LET TODIR$=TODIR$+"/";
:          PRINT @(30,11),TODIR$,
0153 IF CTL=4 THEN GOTO 9000
0154 CALL "CDS095",ERR=0151,1,TODIR$,"Y"; CLOSE (1)
0155 LET SKY$="Y";
:    CALL "CDS069",1,1,30,12,"Define all DIR & SRT files as SKY (Y/N)","AYN",0,SKY$
0159 PRINT ""
0160 FOR I=1 TO 2000
0170    CLOSE (1)
0180    LET F$=FRDIR$+STR(I:"0000")
0190    CALL "CDS095",ERR=0230,1,F$,"D"
0200    READ RECORD(1,SIZ=4096)A$
0210    IF LEN(A$)
:          THEN
:             IF A$<>"*eoi*"+BIN(0,3)
:                THEN
:                   IF I>0 OR LEN(A$)<>96
:                      THEN
:                         GOSUB 1000;
:                         REM "SKIP TAPE LABEL (IF ANY)
0215    CALL "CDS075",1
0220 NEXT I
0230 CALL "CDS069",0,0,0,0,"Job completed-"
0240 GOTO 9000

1000 REM "PROCESS FILE
1010 IF A$(8,1)=$00$
:       THEN
:          LET D$=A$(2,6),S0=25
:       ELSE
:          LET D$=A$(2,6)+A$(23,36),S0=61
1020 LET D$=FNS$(D$(1,POS($00$<>D$,-1)))
1025 LET Z=POS(".CDI."=D$,-1)
1026 IF Z THEN LET D$=D$(Z+5)
1027 LET D0$=CVS(D$,16)
1030 PRINT I," ",D0$
1040 IF D$<>D0$
:       THEN
:          PRINT (6)"File",I," ",D0$," Invalid character replaced in file name"
1045 LET Z=POS("/"=D0$);
:    IF Z
:       THEN
:          LET D0$(Z,1)="_";
:          PRINT (6)"File",I," ",CVS(D$,16)," renamed to ",D0$;
:          GOTO 1045
1046 LET Z=POS(""""=D0$);
:    IF Z
:       THEN
:          LET D0$(Z,1)="_";
:          PRINT (6)"File",I," ",CVS(D$,16)," renamed to ",D0$;
:          GOTO 1046
1050 IF A$(1,1)<>$B6$ THEN ESCAPE
1060 IF POS(A$(9,1)=$0001020304$)=0 THEN ESCAPE
1070 LET F0=ASC(A$(9,1)),F2=DEC(A$(10,3)),F3=DEC(A$(13,2)),F1=ASC(A$(16,1)),F4=DEC(A$(17,3)),F5=DEC(A$(17,2))
1080 LET D0A$=TODIR$+D0$,SFX=0
1090 ON F0 GOSUB 1200,1400,1700,1900,2100; REM "PROG
1095 IF SFX
:       THEN
:          PRINT (6)"File",I," ",D0$," on tape more than once, renamed to ",D0$,".",STR(SFX)
1100 RETURN

1200 REM "INDEXED
1210 INDEXED D0A$,F2,F3,ERR=2400
1220 OPEN (2)D0A$
1230 LOCK (2)
1240 FOR L1=1 TO F2
1250    IF S0+F3-1>LEN(A$) THEN GOSUB 2300; REM "READ
1260    LET C0A$=FNS$(A$(S0,F3)),S0=S0+F3
1270    WRITE RECORD(2)C0A$
1280 NEXT L1
1290 CLOSE (2)
1300 RETURN

1400 REM "SERIAL DEFINE
1410 SERIAL D0A$,ERR=2400
1420 IF F5 THEN GOSUB 1500
1430 RETURN

1500 REM "SERIAL PROCESS
1510 OPEN (2)D0A$
1520 LOCK (2)
1530 FOR L1=1 TO F5
1540    IF S0+2>LEN(A$) THEN GOSUB 2300; REM "READ
1550    LET L0=DEC(A$(S0,2))
1560    IF S0+L0-1>LEN(A$) THEN GOSUB 2300; REM "READ
1570    LET C0A$=FNS$(A$(S0+2,L0-2)),S0=S0+L0
1580    IF C0A$(1,1)=$7F$ THEN PRINT (6)'FF',; LET C0A$=C0A$(2)
1590    LET Z=POS($1B1C$=C0A$);
:       IF Z>0
:          THEN
:             LET C0A$=C0A$(1,Z-1)+C0A$(Z+2);
:             PRINT (6)"File",I," ",D0$,"removed $1B1C$ from serial fileó"GOTO 1590
1600    LET Z=POS($1B2F$=C0A$);
:       IF Z>0
:          THEN
:             LET C0A$=C0A$(1,Z-1)+FILL(ASC(C0A$(Z+2,1))-Z-1)+C0A$(Z+3);
:             GOTO 1600
1610    WRITE RECORD(2)C0A$
1620 NEXT L1
1630 CLOSE (2)
1640 RETURN

1700 REM "DIRECT DEFINE
1710 IF SKY$="N"
:       THEN
:          DIRECT D0A$,F1,F2,F3,ERR=2400
:       ELSE
:          MKEYED D0A$,F1,0,F3,ERR=2400
1720 IF F4>0 THEN GOSUB 1800; REM "HAVE ENCOUNTERED NEGATIVE F4
1730 RETURN

1800 REM "DIRECT PROCESS
1810 OPEN (2)D0A$
1820 LOCK (2)
1830 FOR L1=1 TO F4
1840    IF S0+F1+F3-1>LEN(A$) THEN GOSUB 2300; REM "READ
1850    LET K$=FNS$(A$(S0,F1)),C0A$=FNS$(A$(S0+F1,F3)),S0=S0+F1+F3
1860    WRITE RECORD(2,KEY=K$)C0A$
1870 NEXT L1
1880 CLOSE (2)
1890 RETURN

1900 REM "SORT DEFINE
1910 IF SKY$="N" THEN SORT D0A$,F1,F2,ERR=2400 ELSE MKEYED D0A$,F1,0,0,ERR=2400
1920 IF F4>0 THEN GOSUB 2000; REM "HAVE ENCOUNTERED NEGATIVE F4
1930 RETURN

2000 REM "SORT PROCESS
2010 OPEN (2)D0A$
2020 LOCK (2)
2030 FOR L1=1 TO F4
2040    IF S0+F1-1>LEN(A$) THEN GOSUB 2300; REM "READ
2050    LET K$=FNS$(A$(S0,F1)),S0=S0+F1
2060    WRITE (2,KEY=K$)
2070 NEXT L1
2080 CLOSE (2)
2090 RETURN

2100 REM "PROG
2110 STRING F$+".STR"
2120 OPEN (2)F$+".STR"
2130 LOCK (2)
2140 FOR L1=1 TO DEC(A$(17,2))
2150    IF S0+1>LEN(A$) THEN GOSUB 2300; REM "READ
2160    LET L0=DEC(A$(S0,2))
2170    IF L0<1
:          THEN
:             PRINT (6)"File",I," ",D0$," invalid program statement length after line ",C0A$(1,5);
:             EXITTO 2250
2180    IF S0+L0-1>LEN(A$) THEN GOSUB 2300; REM "READ
2190    LET C0$=FNS$(A$(S0+2,L0-2))
2200    LET C0A$=CVS(C0$,16)
2210    IF C0$<>C0A$
:          THEN
:             PRINT (6)"File",I," ",D0$," Invalid characters replaced with blanks in line ",C0A$(1,5)
2220    WRITE (2)C0A$
2230    LET S0=S0+L0
2240 NEXT L1
2245 PROGRAM D0A$,ERR=2400
2250 WRITE (2)"SAVE "+""""+D0A$+""""

2255 REM             WRITE (2)"ERASE "+""""+F$+".STR"+""""
2260 CLOSE (2)
2270 LET A=SCALL(ARGV(0)+" -c/u/bbx/config.bbx -m256 < "+F$+".STR > /dev/null &")
2275 LET A=SCALL("ps | grep bbx4 | wc -l")
2280 RETURN

2300 REM "READ NEXT
2310 LET A$=A$(S0),S0=1
2320 READ RECORD(1,SIZ=4096)Z$
2330 LET A$=A$+Z$,Z$=""
2340 RETURN

2400 REM "ERROR DEFINING FILE
2410 IF ERR<>12 THEN ESCAPE; REM "ERROR DEFINING FILE
2420 LET SFX=SFX+1,D0A$=TODIR$+D0$+"."+STR(SFX)
2430 RETRY

9000 EOJ:
9010 RUN "CDS001"
