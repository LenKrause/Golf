0100 rem "LKTIME - 07/26/12 Read Timing Test
0105 begin
0110 setesc 8000; seterr 8000
0114 fil$ = "DN/DNSO41"
0115 call "CDS069", 1, 40, 0, 0, "+File Name+", "A", 0, fil$
0116 if ctl = 4 then goto eoj
0120 open (1, err=0115)fil$
0130 precision 10; tim0 = tim; precision 2
0140 read record(1, end=0200)
0150 recs = recs + 1
0160 if mod(recs, 10000) = 0 then gosub calc
0170 goto 0140
0200 gosub calc
0210 call "CDS069"
0220 goto eoj

1000 calc:
1010 precision 10
1020 elap = tim - tim0
1030 if elap then rps = recs / (3600 * elap) else rps = 0
1050 precision 2
1060 print recs:"##,###,##0", " records, ", rps:"###,##0", " records/second"
1070 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9010 if tcb(13) then exit
9020 run "CDS001"
