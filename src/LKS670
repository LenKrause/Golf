0100 REM "LKS670 - 01/06/96 DISPLAY/PRINT MBF BOSS/VS TAPE TRANSFER FORMAT DIRECTORY
0110 BEGIN
0120 PRINT 'CS','SB',@(0,10),"Directory:",'SF',
0130 CALL "CDS084",ERR=9000,Y$,Y5$,6,"NS",Y6,Y6$
0140 DEF FNS$(Z$)=AND(Z$,FILL(LEN(Z$),$7F$))
0150 LET FRDIR$="/u/pc/tape/"
0160 CALL "CDS069",1,20,30,10,"+directory where files have been copied from tape+","A",0,FRDIR$
0170 IF FRDIR$(LEN(FRDIR$))<>"/"
:       THEN
:          LET FRDIR$=FRDIR$+"/";
:          PRINT @(0,10),FRDIR$,
0180 IF CTL=4 THEN GOTO 9000
0190 CALL "CDS095",ERR=0160,1,FRDIR$,"Y"; CLOSE (1)
0200 FOR I=1 TO 2000
0210    CLOSE (1)
0220    LET F$=FRDIR$+STR(I:"0000")
0230    CALL "CDS095",ERR=0270,1,F$,"D"
0240    READ RECORD(1,SIZ=4096)A$
0250    IF I>0 OR LEN(A$)<>96 THEN GOSUB 1000; REM "SKIP TAPE LABEL (IF ANY)
0260 NEXT I
0270 CALL "CDS069",0,0,0,0,"Job completed-"
0280 GOTO 9000

1000 REM "PROCESS FILE
1010 IF LEN(A$)=8 AND FNS$(A$)="*eoi*"+BIN(0,3) THEN LET I=9999; GOTO 1150
1020 IF A$(8,1)=$00$
:       THEN
:          LET D$=A$(2,6),S0=25
:       ELSE
:          LET D$=A$(2,6)+A$(23,36),S0=61
1030 LET D$=FNS$(D$(1,POS($00$<>D$,-1)))
1040 LET Z=POS(".CDI."=D$,-1)
1050 IF Z THEN LET D$=D$(Z+5)
1060 LET D0$=CVS(D$,16)
1070 IF D$<>D0$ THEN PRINT (6)I:"####B",D0$," $",HTA(D$),"$ Invalid file name"
1080 LET Z=POS("/"=D0$);
:    IF Z
:       THEN
:          LET D0$(Z,1)="_";
:          PRINT (6)"File",I," ",CVS(D$,16)," renamed to ",D0$;
:          GOTO 1080
1090 LET Z=POS(""""=D0$);
:    IF Z
:       THEN
:          LET D0$(Z,1)="_";
:          PRINT (6)"File",I," ",CVS(D$,16)," renamed to ",D0$;
:          GOTO 1090
1100 IF A$(1,1)<>$B6$ THEN ESCAPE
1110 IF POS(A$(9,1)=$0001020304$)=0 THEN ESCAPE
1120 LET F0=ASC(A$(9,1)),F2=DEC(A$(10,3)),F3=DEC(A$(13,2)),F1=ASC(A$(16,1)),F4=DEC(A$(17,3)),F5=DEC(A$(17,2))
1130 LET D0A$=TODIR$+D0$,SFX=0
1135 IF L<1 THEN GOSUB NEWPAGE
1140 PRINT (6)I:"####B",D0$,@(20),F0:"##B",F2:"#,###,##0B",F4:"#,###,##0B",F3:"##,##0B"
1145 LET L=L-1
1150 RETURN

1160 NEWPAGE:
1170 LET L=Y6
1180 CALL "CDS094",ERR=9000,Y6$
1190 RETURN

1200 REM "INDEXED
1210 INDEXED D0A$,F2,F3,ERR=2500
1220 OPEN (2)D0A$
1230 LOCK (2)
1240 FOR L1=1 TO F2
1250    IF S0+F3-1>LEN(A$) THEN GOSUB 2400; REM "READ
1260    LET C0A$=FNS$(A$(S0,F3)),S0=S0+F3
1270    WRITE RECORD(2)C0A$
1280 NEXT L1
1290 CLOSE (2)
1300 RETURN

1400 REM "SERIAL DEFINE
1410 SERIAL D0A$,ERR=2500
1420 IF F5 THEN GOSUB 1500
1430 RETURN

1500 REM "SERIAL PROCESS
1510 OPEN (2)D0A$
1520 LOCK (2)
1530 FOR L1=1 TO F5
1540    IF S0+2>LEN(A$) THEN GOSUB 2400; REM "READ
1550    LET L0=DEC(A$(S0,2))
1560    IF S0+L0-1>LEN(A$) THEN GOSUB 2400; REM "READ
1570    LET C0A$=FNS$(A$(S0+2,L0-2)),S0=S0+L0
1580    IF C0A$(1,1)=$7F$ THEN PRINT (6)'FF',; LET C0A$=C0A$(2)
1590    LET Z=POS($1B1C$=C0A$);
:       IF Z>0
:          THEN
:             LET C0A$=C0A$(1,Z-1)+C0A$(Z+2);
:             PRINT (6)"File",I," ",D0$,"removed $1B1C$ from serial fileó"GOTO 1590
1600    LET Z=POS($1B2F$=C0A$);
:       IF Z>0
:          THEN
:             LET C0A$=C0A$(1,Z-1)+FILL(ASC(C0A$(Z+2,1))-Z-1)+C0A$(Z+3);
:             GOTO 1600
1610    WRITE RECORD(2)C0A$
1620 NEXT L1
1630 CLOSE (2)
1640 RETURN

1700 REM "DIRECT DEFINE
1710 IF SKY$="N"
:       THEN
:          DIRECT D0A$,F1,F2,F3,ERR=2500
:       ELSE
:          MKEYED D0A$,F1,0,F3,ERR=2500
1720 IF F4>0 THEN GOSUB 1800; REM "HAVE ENCOUNTERED NEGATIVE F4
1730 RETURN

1800 REM "DIRECT PROCESS
1810 OPEN (2)D0A$
1820 LOCK (2)
1830 FOR L1=1 TO F4
1840    IF S0+F1+F3-1>LEN(A$) THEN GOSUB 2400; REM "READ
1850    LET K$=FNS$(A$(S0,F1)),C0A$=FNS$(A$(S0+F1,F3)),S0=S0+F1+F3
1860    WRITE RECORD(2,KEY=K$)C0A$
1870 NEXT L1
1880 CLOSE (2)
1890 RETURN

1900 REM "SORT DEFINE
1910 IF SKY$="N" THEN SORT D0A$,F1,F2,ERR=2500 ELSE MKEYED D0A$,F1,0,0,ERR=2500
1920 IF F4>0 THEN GOSUB 2000; REM "HAVE ENCOUNTERED NEGATIVE F4
1930 RETURN

2000 REM "SORT PROCESS
2010 OPEN (2)D0A$
2020 LOCK (2)
2030 FOR L1=1 TO F4
2040    IF S0+F1-1>LEN(A$) THEN GOSUB 2400; REM "READ
2050    LET K$=FNS$(A$(S0,F1)),S0=S0+F1
2060    WRITE (2,KEY=K$)
2070 NEXT L1
2080 CLOSE (2)
2090 RETURN

2100 REM "PROG
2110 STRING F$+".STR"
2120 OPEN (2)F$+".STR"
2130 LOCK (2)
2140 FOR L1=1 TO DEC(A$(17,2))
2150    IF S0+1>LEN(A$) THEN GOSUB 2400; REM "READ
2160    LET L0=DEC(A$(S0,2))
2170    IF L0<1
:          THEN
:             PRINT (6)"File",I," ",D0$," invalid program statement length after line ",C0A$(1,5);
:             EXITTO 2260
2180    IF S0+L0-1>LEN(A$) THEN GOSUB 2400; REM "READ
2190    LET C0$=FNS$(A$(S0+2,L0-2))
2200    LET C0A$=CVS(C0$,16)
2210    IF C0$<>C0A$
:          THEN
:             PRINT (6)"File",I," ",D0$," Invalid characters replaced with blanks in line ",C0A$(1,5)
2220    WRITE (2)C0A$
2230    LET S0=S0+L0
2240 NEXT L1
2250 PROGRAM D0A$,ERR=2500
2260 WRITE (2)"SAVE "+""""+D0A$+""""
2270 CLOSE (2)
2280 LET A=SCALL(ARGV(0)+" -c/u/bbx/config.bbx -m256 < "+F$+".STR > /dev/null &")
2290 LET A=SCALL("ps | grep bbx4 | wc -l")
2300 RETURN

2400 REM "READ NEXT
2410 LET A$=A$(S0),S0=1
2420 READ RECORD(1,SIZ=4096)Z$
2430 LET A$=A$+Z$,Z$=""
2440 RETURN

2500 REM "ERROR DEFINING FILE
2510 IF ERR<>12 THEN ESCAPE; REM "ERROR DEFINING FILE
2520 LET SFX=SFX+1,D0A$=TODIR$+D0$+"."+STR(SFX)
2530 RETRY

9000 EOJ:
9005 CALL "CDS094",Y6$,"E"
9010 RUN "CDS001"
