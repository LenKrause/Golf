0100 REM "GW044 - 09/06/14 Weekly Score Posting Update
0110 SETESC DT_ERR; SETERR DT_ERR
0120 CALL "CDS095",GR05,"XXGR05","YNY",GR05$,GR05K$
0130 LET FLDS$=FATTR(CGI$,""),FLDS=POS($0A$=FLDS$,1,0)
0140 DIM FLD$:"NAME["+STR(FLDS)+"]:C(1*)"
0150 LET FLD$=FLDS$
0160 FOR FLD=1 TO FLDS
0170    IF POS("SCORE"=FLD.NAME$[FLD])=1 THEN GOSUB SCORE
0175    IF POS("NOTE"=FLD.NAME$[FLD])=1 THEN GOSUB NOTEPAD
0180 NEXT FLD
0190 DIM GR05$:FATTR(GR05$)
0200 LET HTML$=""
0210 RUN "GW011"

1000 SCORE:
1010 DIM GR05$:FATTR(GR05$)
1020 LET GR05.LEAGUE$=WASES.LEAGUE$
1030 LET ID$=FLD.NAME$[FLD](6)
1040 LET GR05.ID$=ID$
1050 LET GR05.DATE=NUM(CGI.DATE$)
1060 LET K$=KGEN(GR05$,GR05K$,0)
1070 EXTRACT RECORD(GR05,KNUM=0,KEY=K$,DOM=1071)GR05$;
:    REM "DOM= needed when golfer is added after GR05 record with HC was created when sheets were printed.
1080 LET GR05.SCORE=NUM(FIELD(CGI$,"SCORE"+ID$))
1100 IF GR05.SCORE
:       THEN
:          LET GR05.NET=GR05.SCORE-GR05.HC;
:          LET GR05.PUTTS=NUM(FIELD(CGI$,"PUTTS"+ID$));
:          LET GR05.POINTS=NUM(FIELD(CGI$,"POINTS"+ID$,ERR=1130))
:       ELSE
:          LET GR05.NET=0,GR05.PUTTS=0,GR05.POINTS=0
1130 WRITE RECORD(GR05)GR05$
1140 RETURN

1200 NOTEPAD:
1210 CALL "CDS257",Y$,WASES.LEAGUE$+FLD.NAME$[FLD](5),"","XXGR02","W",COLS,ROWS,XPOS,YPOS,ATTRB$,ATTRW$,WT$,WA$,FIELD(CGI$,FLD.NAME$[FLD])
1220 RETURN

8000 DT_ERR:
8010 CALL "CDS063",STR(TCB(5+3*(ERR=127))),Y$,PGM(-2)
8020 IF Y.ERRSTS=0 THEN SETERR 0 ELSE IF Y.ERRSTS=2 THEN RETURN
8030 RETRY

9000 EOJ:
9010 IF TCB(13) THEN EXIT
9020 RUN "CDW999"
