0100 rem "LKS551 - 12/12/99 Program Utility - Replace Error Routine/Search & Replace
0110 seterr 8000; setesc 8000
0120 terms$ = stbl("!TERMS")
0130 gosub openpipe
0135 dim srch$:"ARG1:C(1*=92),ARG2:C(1*=92),ARG3:C(1*=)"
0140 l3 = 3, l2 = l3 - 1; rem "First Line
0150 print 'sb', @(0,2), "Currently Processing:", 'ce', 
0160 read (1, end=0280)f2$
0170 f4$ = "", f9$ = "Skipped: "
0180 close (6); open (6, err=0190)f2$; goto 0210
0190 if err=12 then f9$ = f9$ + "Deleted"; goto 0240
0200 f9$ = f9$ + "Error " + str(err); goto 0240
0210 call "CDS087", 6, "", f2$, f3$, f4$, f[all], f5$
0220 if pos(f3$ = "PGMPG4", 3) = 0 then
:      f9$ = f9$ + f3$
:    else
:      if f[6] < 10 then
:        f9$ = f9$ + "Empty"
:      else
:        f9$ = ""
:      fi
:    fi
0230 if len(f4$) > 3 + len(f2$) then
:      if f4$(1, len(f2$) + 3) = f2$ + " - " then
:        f4$ = f4$(len(f2$) + 4)
:      fi
:    fi
0240 if y.termid$(1,1) = "T" then
:      if l2 < 20 then
:        l2 = l2 + 1
:      else
:        print @(0, l3), 'ld', 
:      fi
:    fi
0250 if y.termid$(1,1) = "T" then
:      print @(0, l2), f2$, " ", f4$(1, min(len(f4$), 57 - len(f2$))), @(59, l2), f9$, 
:    fi
0260 if len(f9$) = 0 then gosub process
0270 goto 0160
0280 call "CDS069", 0, 0, 0, 0, "Processing completed-"
0290 goto eoj

1000 process:
1010 snd$ = "LOAD """ + f5$ + """"; gosub writepipe
1020 if res$ <> $0a$ + "READY" then f9$ = "Unable to LOAD"; goto 1040
1030 gosub search
1040 print @(59, l2), f9$, 
1050 if reserr then gosub openpipe
1060 return

1700 search:
1710 u5 = -1, modified = 0, matchcount = 0, f9$ = "Unable to process"
1720 call "CDS093", err=1800, y$, 6, f5$, buf$, vbtbl$, l$, u5, "", linetyp
1730 if l$ = "END" then goto 1790
1740 l0 = pos(" " = l$), l4$ = l$, h = l0 + 1
1750 g7 = pos("""" = l4$(h))
1760 if g7 then
:      h = h + g7 - 1, g8 = pos("""" = l4$(h + 1));
:      if g8 = 0 then
:        l4$(h) = ""
:      else
:        l4$(h, g8 + 1) = "", h = h + g8 + 1;
:        goto 1750
:      fi
:    fi
1770 gosub search_line
1780 goto 1720
1790 if modified = 0 then
:      f9$ = "No matches found";
:      erase f5$
:    else
:      f9$ = str(matchcount) + " replacement(s)", snd$ = "SAVE";
:      gosub writepipe;
:      if res$ <> f5$ then
:        f9$ = "Unable to save"
:      fi
:    fi
1800 return

1900 search_line:
1910 linemodified = 0
1920 for seq = 1 to argcount
1930   args$ = data$[seq]
1940   if args.type$ = "2" and len(l$(l0 + 1)) > 4 and l$(l0 + 1, 4) = "REM " then
:        goto 1990;
:        rem "Bypass search on remarks if excluding text between quotes (even if no quote on remark)
1950   if args.type$ = "2" then l5$ = l4$ else l5$ = l$
1960   h = l0, srch$ = args.searcharg$ + fill(max(0,2 - pos("\" = args.searcharg$, 1, 0)), "\")
1970   z1 = pos(srch.arg1$ = l5$(h))
1980   if z1 then
:        linemodified = 1, matchcount = matchcount + 1, h = h + z1 - 1, sargl = len(srch.arg1$), vbllen = pos(",2))" = l5$(h + sargl)) - 1;
:        if vbllen <= 0 or vbllen > 10 or pos("," = l5$(h + sargl, vbllen)) or pos("$" = l5$(h + sargl, vbllen), 1, 0) <> 1 then
:          escape
:        else
:          args.replacearg$ = "$(str(19+(" + l5$(h + sargl, vbllen) + ",2)<""90""))+", l5$ = l5$(1, h - 1) + args.replacearg$ + l5$(h + sargl), l$ = l$(1, h - 1) + args.replacearg$ + l$(h + sargl), l4$ = l4$(1, h - 1) + args.replacearg$ + l4$(h + sargl), h = h + len(args.replacearg$);
:          goto 1970
:        fi
:      fi
1990 next seq
2000 if linemodified then
:      modified = 1, snd$ = l$;
:      gosub writepipe;
:      if len(res$) then
:        call "CDS069", 0, 0, 0, 0, 'rb' + "Warning: " + res$ + " compiling line " + l$(1, l0 - 1) + "-"
:      fi
:    fi
2010 return

2100 writepipe:
2110 snd$ = snd$ + $0a$, snd = len(snd$)
2120 write record(pipe)snd$
2130 gosub readpipe
2140 if len(res$) >= snd and res$(1, snd) = snd$ then res$ = res$(snd + 1)
2150 res = len(res$)
2160 if res and res$(res) = $0a$ then res = res - 1, res$ = res$(1, res)
2170 return

2200 readpipe:
2210 res$ = "", reserr=1, z$ = stbl("!TERMS", ">" + $00$)
2220 read (pipe, tim=10, err=2250)z$
2230 res$ = res$ + z$, z = len(res$), reserr=0
2240 if z and res$(z) <> $0a$ then res$ = res$ + ">"; goto 2220
2250 z$ = stbl("!TERMS", terms$)
2260 return

2300 newpage:
2310 call "CDS094", y6$
2320 p = p + 1, l = y6 + 3
2330 print (y6.ch)@(y.at80), y.compname$, 'lf', y.date$, @(20), "Program Utility - Replace Error Routine", @(72), "Page", p, 'lf'
2340 return

2400 openpipe:
2410 if pipe then close (pipe)
2420 pipe = unt
2430 open (pipe)"|BBTERM=G0 " + argv(0) + " -c${BBXDIR}/config.bbx -m512"
2440 gosub readpipe
2450 if reserr or res$ <> $0a$ + "READY" + $0a$ then
:      call "CDS069", 0, 0, 0, 0, "Unable to open pipe to bbx-";
:      goto eoj
:    fi
2460 return

8000 dt_err:
8010 call "CDS063", str(tcb(5 + 3 * (err=127))), y$, pgm(-2)
8020 if y.errsts = 0 then seterr 0 else if y.errsts = 2 then return
8030 retry

9000 eoj:
9005 call "CDS094", y6$, "E"
9010 clear
9020 run "CDS001"
